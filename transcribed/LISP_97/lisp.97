TITLE LISP INTERPRETER

	;CONDITIONAL ASSEMBLY FLAGS
TS==1	;TIME-SHARING OPTIONS
RL==0	;1 MEANS RELOCATABLE
PL==1	;PLOTTER ROUTINES
VD==1	;VISUAL DISPLAY ROUTINES (340CRT)
PKPKF==1	;PICPAC ROUTINES
MPXRF==1	;VIDISSECTOR, A/D, D/A, AND 760 ROUTINES
LAPFL==1	;ROUTINES FOR LAP

	;ACCUMULATOR USAGE
NIL=0	;ATOM HEAD FOR NIL
A"=1	;ARG 1; VALUE; MARKED FROM
B"=2	;ARG 2, MARKED FROM
C"=3	;ARG 3; MARKED FROM
AR1=4	;ARG 4; MARKED FROM
AR2A=5	;ARG 5; MARKED FROM
T"=6	;USED WITH LSUBR CALLS; MARKED FROM
TT=7	;TEMP; MARKED FROM
FP=10	;FLONUM WORDS AC

R=11	;NOT MARKED, USED  BY PRINT,GC, ETC.
D=12	;DITTO
;13	;UNUSED BY LISP
P"=14	;PDL AC; NOT MARKED
F=15	;FREE-STORAGE AC
FF=16	;FULL WORDS AC
SP=17	;SPECIAL PDL AC
NACS==5
OBTSIZ==777	;OBLIST LENGTH


IFE TS, RL==1
IFN RL,RELOCATABLE

DEFINE INFORM A,B
IF1,[PRINTX /A=B
/]
TERMIN

IFN TS,[PRINTX "MODIFIED FOR T.S.
"]
IRPS S,,RL PL VD PKPKF MPXRF OBTSIZ
INFORM S,\S
TERMIN
BSUP=140000

IFE TS,[
PLT=654
VID=754
MPXX=764
MPXR=744
CRK=710
KRC=714
PCHCHN"=3
APRCHN"=4
LPTCHN"=5
TTYCHN==6
PLTCHN==6
DISCHN==7
FLGCHN==6
PRS==PI
MACSA=BSUP+37762
MACCR=BSUP+37777
]

CALLF=76000,,
JCALLF=77000,,
CALL=74000,,
JCALL=75000,,

LERR=1000,,	;ORDINARY LISP ERROR
LER2=2000,,	;NO STRG LEFT OR PDL OVFL
LER3=3000,,	;I/O ERROR - MUST RESET I/O SWITCHES.
STRT=4000,,	;STRING TYPEOUT

UUOMAX==4	;NO OF ERROR-TYPE UUO'S

IFN TS,[
ITSMSK=220661	;MAGIC NUMBER FOR ITS INTERRUPT ENABLE
BYTSWD==5	;NO. OF ASCII BYTES PER WORD
]

ZZ==1
IRP A,,[TYIC,TYOC,UTIC,UTOC,LPTC,VIDC,PLTC
	NVDC,IMXC,OMXC,BVDC,DISC,NOFCH]
	A==ZZ
	ZZ==ZZ+1
TERMIN
]

BVDC"=BVDC

NSFC==0
DEFINE SFX A/
SFSTO \.,\NSFC
NSFC==NSFC+1
A
TERMIN
DEFINE SFSTO PT,NM
DEFINE ZZM!NM
PT
TERMIN
TERMIN

DEFINE CONC A,B
A!B! TERMIN

IFN TS,[
 DEFINE TSOPEN A,B
	.OPEN A,B
	JSP TT,OPNER
	TERMIN
]

LISPGO":
LOC 41
	JSR UUOH

IFN TS,	JSR INT

IFE TS,[
LOC 40+2*APRCHN
	JSR APRINT

LOC 40+2*TTYCHN
	JSR PITELE
IFN VD, JSR RECYC

IFN VD,[
LOC 40+2*DISCHN
	BLKO DIS,BLKOP
	CONO PI,4000+1_<7-FLGCHN>
]
]
LOC 70
REPEAT 6,.RPCNT,,.RPCNT	;COMMONLY USED LAP CONSTANTS
LOC LISPGO
IFN TS, JRST ALLOC
IFE TS,[
	CONO PRS,11377
	CONO 635550
	CONO TTY,10+TTYCHN
IFN VD, AOS DISON
]
LOSP":	SETZB F,FF
	MOVE SP,SC2
	MOVE P,C2
	SETOM RTSPFL
LISP:	IFE TS,[
	CONO PI,12200+1_<7-TTYCHN>+1_<7-APRCHN>+1_<7-PCHCHN>+1_<7-DISCHN>+1_<7-LPTCHN>
	CONO  3000+APRCHN
]
IFN TS,[	MOVEI A,ITSMSK	;MAGIC NUMBER FOR INTERRUPT ENABLE MASK
	.SETMSK A,
	HLLM A,OTYIC
	HRLI A,1
	HLLM A,OTYOC
	TSOPEN TYIC,OTYIC
	TSOPEN TYOC,OTYOC
	.STATUS TYOC,A
	ANDI A,77
	SOS A
	MOVEM A,VTTY
	JUMPE A,TTYFIN
	MOVEI A,45.+1
	MOVEM A,VLINEL
	MOVSI A,10
	IORM A,OTYIC
	IORM A,OTYOC
	TSOPEN TYIC,OTYIC
	TSOPEN TYOC,OTYOC
	JRST TTYFIN
OTYIC:	(SIXBIT /TTY/)
	SIXBIT /.LISP./
	SIXBIT /INPUT/
OTYOC:	(SIXBIT /TTY/)
	SIXBIT /.LISP./
	SIXBIT /OUTPUT/
TTYFIN:	.SUSET [16,,USN]
	SETOM UTIOPD
]
IFN RL,[
	HRROI NIL,CNIL2
	PUSHJ P,LNK
	MOVSI A,(JFCL)
	MOVEM A,.-2
]

LSPRT1:	SETOM ERRSW
	SETZM ERRTN
	SETZM TAPRED
	SETZM TAPWRT
	SETZM TTYOFF
	SETZM EOFF
IFN TS,[
	SETOM LPTOPD
IFN VD,[SETZM DISPON
	SETOM DISOPD
]
IFN MPXRF,[
	SETOM BVDOPD
	SETOM VIDOPD
	SETOM NVDOPD
]
]
IFN VD, SETZM DSTPF'
IFN PL, SETZM PLTON
	MOVE P,C2
	MOVE FP,FC2
	MOVE SP,SC2
	MOVEI A,CEVAL
	SKIPE B,VERRLIST
	CALLF 2,CMAPC
	SKIPN B,51	;HAIRY MITCHELL HAC
	JRST LISP1
	MOVE A,[(SIXBIT /DSK/)]
	MOVEMA,UTIN
	MOVE A,50
	SETZM 51
	AOS TAPRED
	JRST UREAD1

LISP1":	SETZB A,PSYMF	;MAIN EVAL LOOP.
	SETZM PA3
	SETZM PA4
	MOVE NIL,[1,,2]
	BLT NIL,NACS+2
NILPTR:	HRROI NIL,CNIL2
	PUSHJ P, TERPRI
	PUSHJ P,READ
PPEVAL:	PUSHJ P,EVAL
	PUSHJ P,PRINT
	JRST,LISP1

LSPRET:	MOVE B,SC2	;RETURN FROM LISP ERROR
	SKIPN V.RSET
	PUSHJ P,UBD
	JRST LSPRT1

.RSET:	MOVEM A,V.RSET
	POPJ P,

C2:	0	;INITIAL SYSTEM PDL SETTING, SET BY ALLOC.
SC2:	0	;INITIAL SPECIAL PDL, DITTO.
FC2:	0	;INITIAL FLONUM PDL, DITTO.


IFN TS,[
UTIBP:	070700,,EOFC1
UTOBP:	4407,,UTOB
]

IFN RL,[

CONSTANTS
VARIABLES

LNK":	PUSH P,70	;SAVE PLACE FOR READ-IN SEXPR
	MOVE A,LNKPL	;FOR "(VALUE SUBR FSUBR LSUBR MACRO)"
	PUSHJ P,LNKRD
	PUSH P,A
LNK3:	MOVEI A,.LINKLIST"	;A = .LINKLIST
	JUMPE A,LNKEND	;GO EXIT IF A=NIL
	ADD A,LNKC1	;A = SEXPR SPEC."BQ CJAR2STRING AT (A)+1
	PUSHJ P,LNKRD
	MOVEM A,-1(P)
LNK6:	HRRZ B,LNK3	;B = (PTR LINK)
	LDB R,[350600,,1(B)]	;R = TYPE
	CAIN R,4
	JRST LNK8
	PUSHJ P,ATOM
	JUMPE A,LNKERR
	MOVE A,-1(P)
LNK8:	HRRZ C,(P)	;C = "(SUBR FSUBR LSUBR MACRO)
	HRRZ C,(C)
	CAILE R,7
	JRST LNKERR
	JRST @.+1(R)	;DISPATCH ON R
	LNKSBR	;0=SUBR
	LNKFSB	;1=FSUBR
	LNKLSB	;2=LSUBR
	LNKMAC	;3-MACRO
	LNKQUO	;4=QUOTE
	LNKSPE	;5=SPECIAL
	LNKE	;6=EXECUTE INITIALIZATION
	LNKPRP 	;7=PROPERTY GENERATION

LNKEND:	SUB P,[2,,2]
	SKIPE LNKINT'
	JRST @LNKINT
	POPJ P,

LNKRD":	MOVEM A,LNK5
LNKRD1:	MOVEI A,.+2
	JRST READ+1
	ILDB A,LNK5
	SKIPN A,
	MOVEI A,40
	POPJ P,
LNK5:	0
LNKPQ:	440700,,.+1
	ASCII /QLIST /
LNKPL:	440700,,.+1
	ASCII /(VALUE SUBR FSUBR LSUBR MACRO) /
LNKC1:	350700,,1

LNKSPE:	MOVEI B,VALUE
	PUSHJ P,GET
	JUMPN A,LNK2B
	MOVE A,-1(P)
	MOVEI B,UNBOUND
	PUSHJ P,SET
	MOVE A,-1(P)
	JRST LNKSPE

LNKQUO:	CAIGE A,INUM
	JRST LNK2B
	PUSHJ P,ATOM
	JUMPE A,LNK3A
	MOVE A,-1(P)
	PUSHJ P,NUMBERP
	JUMPN A,LNK3A
	MOVE A,-1(P)
	MOVE B,(P)
	PUSHJ P,GETL
	JUMPE A,LNK3A
LNK2A:	MOVE A,-1(P)
LNK2B:	HLRZ B,@LNK3
	HRRM A,(B)
	JRST LNK4
LNK3A:	SKIPN LNKQL	;AD TO QLIST
	JRST LNK3B
	MOVE A,-1(P)
	MOVE B,@LNKQL
	PUSHJ P,MEMBER
	JUMPN LNK3C
	MOVE A,-1(P)
	MOVE B,@LNKQL
	PUSHJ P,CONS
	MOVEM A,@LNKQL
	JRST LNK2A
LNK3B:	MOVE A,LNKPQ
	PUSHJ P,LNKRD
	SETZM B
	PUSHJ P,SET
	HLRZ A,1(SP)
	MOVEM A,LNKQL
	JRST LNK3A
LNK3C:	HLRZ A,(A)
	JRST LNK2B

LNKQL:	0


LNKE:	HLRZ B,(B)	;ADDITIONAL INITIALIZATION IN
	MOVEM B,LNKINT	;LOSER'S PROGRAM.
	JRST LNK4

LNKMAC:	HRRZ C,(C)	;TYPE 3 - MACRO
LNKLSB:	HRRZ C,(C)	;TYPE 2 - LSUBR
LNKFSB:	HRRZ C,(C)	;TYPE 1 -FSUBR
LNKSBR:	HLRZ C,(C)	;TYPE 0 - SUBR
	HLRZ B,(B)	;(PUTPROP A (CAR B) "XSUBR)
	PUSHJ P,PUTPROP
LNK4:	HRRZ A,@LNK3	;A = (SET .LINKLIST (CDR .LINKLIST))
	HRRM A,LNK3
	JRST LNK3+1	;LOOP

LNKPRP:	PUSHJ P,LNKRD1
	PUSH P,A
	PUSHJ P,ATOM
	JUMPE A,LNKERR
	MOVE A,-2(P)
	PUSHJ P,NCONS
	MOVE B,(P)
	EXCH A,B
	PUSHJ P,GETL
	JUMPN A,LNKP1
	MOVE C,-2(P)
	SETZM B
	MOVE A,(P)
	PUSHJ P,PUTPROP
	HRRZ A,(T)
	HRRZ A,(A)
LNKP1:	SUB P,[2,,2]
	POPJ P,


LNKERR:	STRT LNKMES
	MOVE A,-1(P)
	PUSHJ P,PRINC
	PUSHJ P,TERPRI
	JRST LNK4
LNKMES:	SIXBIT /LINKING ERROR IN !/

CONSTANTS 
VARIABLES

]


IFN TS,[
	ZZ==<.-UTOBP-1>/2
	IFL ZZ-102,[
		ZZ==40
		BLOCK ZZ+ZZ+3+UTOBP-.
	]
UTBSIZ==ZZ
UTIB=.-UTBSIZ-1
UTOB=UTIB-UTBSIZ-1
]


IFN TS,[
DEFINE OPNGEN A,B,E
A!OPN":	.OPEN A!C,O!A!C
	JSP TT,OPNER
	SETZM A!OPD
	POPJ P,
O!A!C":
IFSE E,, (B+SIXBIT /A/)
IFSN E,, (B+SIXBIT /E/)
A!OPD":	-1	
TERMIN

IFN VD, OPNGEN DIS,1
IFN PL, OPNGEN PLT,1
	OPNGEN LPT,1
IFN MPXRF, OPNGEN VID,0
IFN MPXRF, OPNGEN NVD,0
IFN MPXRF, OPNGEN BVD,2,NVD

OPNER":	LDB A,[270400,,-2(TT)]
	CAIE A,0
	CAIL A,NOFCH
	.VALUE
	 CAIN A,LPTC
	 SETZM LPTON
IFN PL, CAIN A,PLTC
IFN PL, SETZM PLTON
IFN VD, CAIN A,DISC
IFN VD, SETZM DISPON
	DPB A,[270400,OPNR1]
	HRRZ A,@-2(TT)
	DPB A,[142200,,OPNERM]
	SKIPE ERRSW
	STRT OPNERM
OPNR1:	.STATUS 00,A	;IMPURE!! AC FIELD
	LDB A,[221000,,1]
	CAIL A,LOMEST-1
	MOVEI A,LOMEST-1
	LERR @OMEST(A)

OPNERM:	SIXBIT /_XXX !/
OMEST:	[SIXBIT /ISE0.!/]
	[SIXBIT /NO SUCH DEVICE.!/]
	[SIXBIT /WRONG DIRECTION.!/]
	[SIXBIT /TOO MANY TRANSLATIONS.!/]
	[SIXBIT /FILE NOT FOUND.!/]
	[SIXBIT /DIRECTORY FULL.!/]
	[SIXBIT /DEVICE FULL.!/]
	[SIXBIT /DEVICE NOT READY.!/]
	[SIXBIT /DEVICE NOT AVAILABLE.!/]
	[SIXBIT /ILLEGAL FILE NAME.!/]
	[SIXBIT /MODE NOT AVAILABLE.!/]
	[SIXBIT /ISE11.!/]

LOMEST==.-OMEST
]

IFE TS,[
APRINT:	0
IFN VD, CONSZ CRK,10
IFN VD, JRST CRKBRK
	CONSO,1000
	JRST APRIN1
	AOS,TEMPUS
	JSR APRBRK"
	CONO,1000+APRCHN
IFN VD,[SKIPE VDISLIST
	AOSG DISTM'
	JRST 12,@APRINT
	CONO DIS,3100+FLGCHN_3
	DATAO DIS,.-1]
	JRST 12,@APRINT

APRIN1:	CONSO 200000
	JRST APRIN2

IFN VD,[
CSPEED=-1_8
CRKBRK:	SKIPN MPXSTA
	CONO CRK,0
	EXCH A,CHCM
	DATAO MPXR,DESTIN(A)
	AOS A
	ANDI A,77
	EXCH A,CHCM
	DATAO KRC,[CSPEED]
	JRST 12,@APRINT

CHCM:	0
]
APRIN2:	HRRZ R,APRINT
	PUSHJ P,ERRADR
	LERR [SIXBIT /_MADE ILGL MEM REF!/]
]

APDLO:	IFN TS, .DISMIS [.+1]	;PDL OVERFLOW
	JUMPN NIL,MES21
	AOSG DIEF
	JRST MES21
	MOVE P,C2
	STRT [SIXBIT /PDL OV FROM GC--LISP IS DEAD!/]
	.VALUE
MES21:	CLEARB F,FF
	SETZM 40
	SKIPL P
	LER2 [SIXBIT /REG PDL OVFLO!/]
	JUMPGE SP,SPOV
IFE TS, JRST 12,@APRINT
IFN TS,[MOVE A,INT
	JRST @INT+1
]
SPOV:	MOVE R,SPSV
	MOVEM R,(SP)
	LER2 [SIXBIT /SPEC PDL OVFLO!/]

DIEF:	-1
IFN TS,[
INT:	0
	0
	EXCH A,INT
	TRZE A,1
	JRST TTYINT
INT1:	TRNE A,20
	JRST DISPRO	;DISPLAY PROTECTION VIOLATION
	TRZE A,200000
	JRST APDLO
	TRZE A,400
	JRST IOERR
	HRRZ R,INT+1
	PUSHJ P,ERRADR
	.DISMISS [.+1]
	LERR [SIXBIT / MADE ILGL MEM REF !/]


TTYINT:	MOVEM A,INTSV'
	.ITYI A,
	JRST INTEX
	PUSHJ P,TYOB1
INTEX:	SKIPE A,INTSV
	JRST INT1
	MOVE A,INT
	.DISMIS INT+1

DISPRO:	.DISMIS [.+1]
	LERR [SIXBIT /DISPLAY MEMORY PROTECT !/]

IOERR:	MOVEM A,INTSV
	MOVEI A,@GCP4
	BLT A,@GCP4+1
	HRRZ A,INT+1
	LDB A,[270400,,-1(A)]
	CAIL A,NOFCH
	.VALUE 
	DPB A,[270400,,IOST]
IOST:	.STATUS 00,A
	LDB A,[330400,,A]
	CAIN A,11
	JRST IODF
	MOVEI C,[SIXBIT #I/O SCREW!#]
	CAIN A,4
	MOVEI C,[SIXBIT /NON-EXISTANT DEVICE CHANNEL!/]
	CAIE A,10
	JRST IOE1
IOE2:	HRRM C,IOE3
	.DISMIS [IOE3]

IOE1:	LDB B,[270400,,IOST]
	CAIE B,IMXC
	CAIN B,OMXC
	MOVEI C,[SIXBIT /MPX NOT OPENED!/]
	CAIN B,UTIC
	MOVEI C,MES27
	CAIN B,UTOC
	MOVEI C,[SIXBIT /NO OUTPUT FILE OPEN!/]
	JRST IOE2

IOE3:	LER3 .

IODF:	MOVEM R,GC10SV
	MOVEI B,[-1,,[SUBR,,[IODF1,,0]]]
	MOVEI A,NIL
	PUSHJ P,IOG
	.VALUE
	MOVSI R,@GCP4
	BLT R,NACS+2
	MOVE R,GC10SV
	SOS INT+1
	JRST INTEX
IODF1:	HRRZ B,UWRT
	HRRM A,DFMES
	STRT DFMES
	POPJ P,
DFMES:	SIXBIT / __XXX FULL - DELETE SOME FILE_AND TYPE $P TO RESUME_!/

]


IFE TS,[
IFN VD,[
PRECYC:	MOVE A,PITELE
	MOVEM A,RECYC
	CONSO DIS,2000	;LIGHT PEN
	JRST RECYC+2
	SKIPGE LTPOFF
	JRST LTPSNX
	AOS LTPCNT
	DATAI DIS,A
	ANDI A,1777
	ADDM A,LTPX
	DATAI DIS,A
	LSH A,-18.
	ADDM A,LTPY
LTPSNX:	CONO DIS,FLGCHN_3+DISCHN
	JRST RECYC2
RECYC:	0
	PUSH P,A
	SKIPN DISON
	JRST DISTP
	SKIPE A,DISPNR
	JRST RECYC1
	MOVNI A,60.
	EXCH A,DISTM
	CAME A,DISTM
	SKIPN A,VDISLIST
	JRST DISTP
RECYC1:	HRL A,(A)
	HLRZM A,DISPNR
	HLRZ A,(A)
	MOVN A,-1(A)
	SETCAM A,BLKOP
	CONSZ DIS,7
	CONSZ DIS,7400
	CONO DIS,100+FLGCHN_3+DISCHN
RECYC2:	POP P,A
	JRST 12,@RECYC
DISTP:	CONO DIS,100
	SETZM DISTM
	JRST RECYC2
DSTRT:	AOS DISON
	SETZM DSTPF
	POPJ P,
DSTOP:	SETOM DSTPF
	SETZM DISON
	POPJ P,
]

APILPT:	CAIE A,15
	JRST PILPT"
	PUSHJ P,PILPT
	MOVEI A,12
	PUSHJ P,PILPT
	MOVEI A,15
	POPJ P,
]

IFN TS,[
IFN VD,[
DSTRT":		.DSTRTL VDISLIST
	JRST DST2
DST1:	AOS DISON
	SETZM DSTPF
	POPJ P,
DST2:	PUSHJ P,CN.Y1
	.DSTRTL VDISLIST
	.DISMIS [[LERR [SIXBIT /DISPLAY NOT AVAILABLE!/]]]
	JRST DST1
DSTOP":	SKIPN DISON
	POPJ P,
	.DSTOP
	CLEARM DISON
	SETOM DSTPF
	POPJ P,
]

APILPT:	SKIPE LPTOPD
	PUSHJ P,LPTOPN
	.IOT LPTC,A
	CAIN A,15
	.IOT LPTC,.+1
	POPJ P,12


URED:	SKIPE UTIOPD
	LERR MES27
	ILDB A,UTIBP
EOFC1:	CAIE A,3
	JRST CPOPJ1
	MOVEI A,UTIB+UTBSIZ
	CAIE A,@UTIBP
	POPJ P,
	MOVE A,[-UTBSIZ,,UTIB]
	.IOT UTIC,A
	CAMN A,[-UTBSIZ,,UTIB]
	POPJ P,
	MOVE A,[440700,,UTIB]
	MOVEM A,UTIBP
	JRST URED

]

TYI:	SKIPN TAPRED
	JRST TYIN
	PUSHJ P,URED"
	JRST .UEOF
	POPJ P,
.UEOF:
IFN TS,[	.CLOSE UTIC,
	MOVE A, [440700,,UTIB]
	MOVEM A, UTIBP
	HRRZ A,EOFC1
	DPB A, [350700,,UTIB]
	SETOM UTIOPD
]
	AOSE EOFF
	JRST TYIN
	SETZM TAPRED
	JRST ORD1

EOFF:	0


ITYO:	PUSHJ P,NUMVAL
TYO":	PUSH P,B
	SETCM B,VLINEL
	CAIN A,15
	MOVNM B,VCHRCT
	CAIE A,11	;TAB
	JRST TOL1
	ORCMI B,7
	EXCH B,VCHRCT
	ADD B,VCHRCT
	TRZ B,7
	SUBM B,VCHRCT
TOL1:	POP P,B
	SOSG VCHRCT
	JRST ICR


IFE TS,[
	SKIPLE LPTON
	PUSHJ P,APILPT
	SKIPN TAPWRT
	JRST TYOUT
UTYO:	CAIN A,15
	JRST UCR1
	CAIN A,12
	AOSE LINF
	PUSHJ P,UCR2
TYOUT:	SKIPG TORM
	JRST .-1
	SKIPE TTYOFF
	POPJ P,
	IDPB A,TOIP
	SOS TORM
	MOVE A,TOIP
	CAMN A,[(10700)TOBE-1]
	HRRI A,TOBO-1
	MOVEM A,TOIP
STYO:	CONSO TTY,30
	CONO TTY,10+TTYCHN
	POPJ P,


UCR1:	SETOM LINF
	PUSHJ P,UCR2
	MOVEI A,12
	PUSHJ P,UCR2
	MOVEI A,15
	JRST TYOUT

UCR2:	PUSHJ P,UWR"
	LERR [SIXBITCH /TAPE FULL!/]
	POPJ P,
]

IFN TS,[
IFN VD,[SKIPE DISPON
	PUSHJ P,DISCHAR
]
	SKIPLE LPTON
	PUSHJ P,APILPT
	SKIPE TAPWRT
	PUSHJ P,UTYO
TYOUT:	SKIPN TTYOFF
	.IOT TYOC,A
	POPJ P,


UTYO:	PUSH P,A
	PUSHJ P,UTTYO
	MOVE A,(P)
	SUBI A,3
	CAIN A,12
	PUSHJ P,UTTYO
	JRST POPAJ
	
UTTYO:	IDPB A,UTOBP
	SOSLE UTOBYT
	POPJ P,
	MOVE A,[-UTBSIZ,,UTOB]
	.IOT UTOC,A
UTOINT:	MOVE A,[440700,,UTOB]
	MOVEM A,UTOBP
	MOVEI A,UTBSIZ*BYTSWD
	MOVEM A,UTOBYT
	POPJ P,

UTOBYT:	-1
]

ICR":	PUSH P,A
	PUSHJ P,TERPRI
	POP P,A
	JRST TYO

IFN TS*VD,[
DISCHAR:	SKIPE DISOPD
	PUSHJ P,DISOPN
	.IOT DISC,A
	CAIN A,15
	.IOT DISC,.+1
	POPJ P,12

]

IFN TS,[
TYIN":	SETZM TAPRED
	.IOT TYIC,A
	POPJ P,
]

IFE TS,[
TYIN":	ILDB A,MACCR	;OR JRST .+3
	JUMPN A,CPOPJ
	SETZM MACCR
	SETZM TAPRED
	SKIPG TICC
	JRST .-1
	MOVE A,TIOP
	CAMN A,[(10700)TIBE-1]
	MOVEI A,TIBO-1
	HRRM A,TIOP
	ILDB A,TIOP
	SOS TICC
	POPJ P,

PLTBRK":
PITELE:	0
	PUSH P,A
IFN VD,[ CONSZ DIS,7400
	JRST PRECYC
]
	PUSH P,B
	CONSZ TTY,10
	JRST TYP11
	CONSO TTY,40
	JRST TTYRET
	DATAI TTY,A
	ANDI A,177
	PUSHJ P,TYOB
	MOVE B,ECHOCC
	ADD B,TICC
	CAIL B,TIBS
	JRST DING
	IDPB A,TIIP
PITEL2:	MOVE A,TIIP
	CAMN A,[(10700)TIBE-1]
	MOVEI A,TIBO-1
	HRRM A,TIIP
	AOSA ECHOCC
DING:	SETOM DINGF
	PUSHJ P,STYO
	JRST TTYRET

TYP11:	AOSE TYOBFC
	JRST TYP1
	MOVEI A,TOBS
	MOVEM A,TORM
	MOVE A,TOIP
	MOVEM A,TOOP
TYP1:	MOVEI A,.
	AOSN LIF
	JRST TYP3
	MOVEI A,12
	HRRM A,TYP1
	MOVEI A,7
	AOSN DINGF
	JRST TYP3
	MOVEI A,177
	AOSG SPCCC
	JRST TYP3
	MOVE A,TORM
	CAIL A,TOBS
	JRST TYP4
	MOVE A,TOOP
	CAMN A,[(10700)TOBE-1]
	MOVEI A,TOBO-1
	HRRM A,TOOP
	ILDB A,TOOP
	AOS TORM

TYPCOM:	CAIN A,15
TYP6:	SETOM LIF
	CAIN A,10
	JRST TYO1
	CAILE A,6
	CAIL A,16
	JRST TYO1
TYP3:	CAIE A,11
	JRST TYP31
	MOVE A,TICHRC
	ANDI A,7
	SUBI A,14
	ASH A,-2
	MOVEM A,SPCCC
	MOVEI A,11
TYP31:	CAIN A,12
LCHAR:	CAIE A,.
	JRST .+2
	SOSE LIF
	DATAO TTY,A
	HRRM A,LCHAR
	CAIL A,40
	AOS TICHRC
	CAIN A,15
	CLEARM TICHRC
	JRST TTYRET


TYP4:	SKIPLE ECHOCC
	JRST TYP4A
TYP5:	CONO TTY,200+TTYCHN
TTYRET:
IFN PL,[	CONSZ PLT,3000
	JRST PLTINT
PLTRET:]	POP P,B
	POP P,A
	JRST 12,@PITELE


TYP4A:	MOVE A,ECHOP
	CAMN A,[(10700)TIBE-1]
	MOVEI A,TIBO-1
	HRRM A,ECHOP
	ILDB A,ECHOP
	SOS ECHOCC
	AOS TICC
	JRST TYPCOM

TYO1:	CAIL A,40
	JRST TYP3
	ADDI A,100
	HRRM A,TYP1
	MOVEI A,"^
	JRST TYP6


TYOB:	CAIN A,^W
	SETOM TYOBFC
	JRST TYOB1

TOIP:	(10700)TOBO-1
TOOP:	(10700)TOBO-1
TORM:	TOBS
TIIP:	(10700)TIBO-1
TIOP:	(10700)TIBO-1
ECHOP:	(10700)TIBO-1
TICC:	0
TIBO:	BLOCK 26.
TIBE:
TIBS=<TIBE-TIBO-1>*5
TOBO:	BLOCK 40
TOBE:
TOBS=<TOBE-TOBO>*5
LIF:	0
DINGF:	0
SPCCC:	0
TICHRC:	0
ECHOCC:	0
TYOBFC:	0
LINF:	0	;UTAPE LINF
]


IFN TS,[
ACORE":	CAIGE A,.	;MODIFIED AT ALLC1D
	MOVEI A,@ACORE
	.CORE -1(A)
	JRST FALSE
 	HRRZM A,VCORE
	HRRM A,BCORE
	MOVE B,A
	LSH B,10.
	SUBI B,2001
	HRRM B,BPSH
BCORE":	POPJ P,21

TIME":	.RDTIME A,
	JRST FIX1A
RUNTIME":	.RRTIM A,
	MULI A,4069.
	DIVI A,1000.
	JRST FIX1A
SLEEP":	PUSH P,A
	PUSHJ P,NUMVAL
	.SLEEP A,
	JRST POPAJ

ALISTEN":	.LISTEN A,
	JRST FIX1A
]

IFE TS,[
TIME":	MOVE A,TEMPUS

SETTIME":	PUSHJ P,NUMVAL
	MOVEM A,TEMPUS
	JRST,MAKNUM

TEMPUS:	0
BLKOP:	0
IFN VD, DISPNR:	0
]

IOC":	MOVEI R,CNTROL
	JRST PRINTA

IOG":	JSP T,SPECBIND
	TTYOFF
	TAPRED
	TAPWRT
	LPTON
IFN TS*VD, DISPON
	PUSH P,B
	SKIPE A
	PUSHJ P,IOC
	POP P,A
	CALLF 0,(1)
	JRST SPECSTR

CNTROL:	ANDI A,77
IFN TS,TYOB:
TYOB1:	CAIG A,"
	XCT CNTBL(A)
CNTBL:	POPJ P,		;^@
	MOVEM A,SIGNAL	;^A
	AOS LPTON 	;^B
	SETZM GCGAGV	;^C
	AOS GCGAGV	;^D
IFN TS,	JRST CN.E	;^E
IFE TS,	SETZM LPTON
IFN TS*VD, JRST DSTRT
IFE TS*VD, POPJ P,	;^F
	JRST CN.G	;^G
	AOS RDMOD	;^H
	POPJ P,		;UNUSED CONTROL CHARACTERS, ETC.
	POPJ P,
	SETZM RDMOD	;^K
	POPJ P,		^L
IFN TS,	JRST CN.CR	;^M
IFE TS,	POPJ P,
IFN TS*VD, AOS DISPON	;^N
IFE TS*VD, POPJ P, 
	POPJ P,		;^O
	JRST CN.P	;^P
IFE PL, POPJ P,		
IFE TS, AOS TAPRED	;^Q
IFN TS, JRST CN.Q
IFE TS,	AOS TAPWRT 	;^R
IFN TS,	JRST CN.R
	SETZM TAPRED	;^S
	SETZM TAPWRT 	;^T
IFN PL,[IFE TS, SETZM PLTON
	IFN TS, JRST CN.U]	;^U
IFE PL, POPJ P,
	SETZM TTYOFF	;^V
IFE TS,	AOS TTYOFF	;^W
IFN TS,	JRST CN.W
	JRST CN.X	;^X
IFN TS*VD, JRST CN.Y	;^Y
IFE TS*VD, POPJ P,

IFN TS,[
CN.E:	.CLOSE LPTC,
	SETZM LPTON
	SETOM LPTOPD
	POPJ P,
CN.W:	AOS TTYOFF
	.RESET TYOC,
	POPJ P,
IFN PL,[
CN.P:	SKIPE PLTOPD
	PUSHJ P,PLTOPN
	AOS PLTON
	POPJ P,
CN.U:	.CLOSE PLTC,
	SETOM PLTOPD
	SETZM PLTON
	HRRZ A,INT+1
	CAIN A,CPLOT+1	;DONT GET HUNG ON A
	AOS INT+1	;PLOTTER IOT.
	MOVEI A,"
	POPJ P,
]
CN.Q:	AOSA TAPRED
CN.R:	AOS TAPWRT
CN.CR:	PUSH P,A
	HRRZ A,-1(P)
	CAIN A,INTEX
	SKIPN VTTY

	JRST POPAJ
	.IOT TYOC,(P)
	JRST POPAJ
IFN VD,[
CN.Y:	.DCLOSE 
	SETZM DISON
CN.Y1:	SETZM DISPON
	SKIPN DISOPD
	.CLOSE DISC,
	SETOM DISOPD
	POPJ P,
]
]
CN.X:	AOSA ERSQTF	;CAUGHT BY ERRSET.
CN.G:	SETZM ERSQTF	;NOT CAUGHT, IRREVOCABLE.
	JSR QUIT
	POPJ P,

QUIT":	0
	SETOM BELFLG
	JUMPGE NIL,@QUIT
	MOVEM A,QITA
	CAMN SP,SC2
	JRST QIT
	MOVE A,(SP)
	CAME A,SPSV
	JRST QIT1
QIT:	SETZM BELFLG
	SKIPE ERSQTF
	JRST RQITR
	SKIPN PSYMF
	SETZM ERRTN
	SETOM ERRSW
RQITR:	IFN TS, .DISMIS [.+1]
	PUSHJ P,ERRCOM
	LERR [SIXBIT /QUIT!/]

BELFLG:	0
ERSQTF:	0

QITA:	0
QITB:	0
QITC:	0
QITUF:	-1
QIT1:	MOVEM C,QITC
	MOVEM B,QITB
	AOSE QITUF
	JRST QITRET
	MOVSI B,-LSPST
QIT2:	MOVE A,SPEXT(B)
	MOVE C,(A)
	MOVEM C,SPST(B)
	MOVE C,[JRST QITR]
	MOVEM C,(A)
	AOBJN B,QIT2
QITRET:	MOVE A,QITA
	MOVE B,QITB
	MOVE C,QITC
	JRST 2,@QUIT

QITR:	MOVSI B,-LSPST
QITR1:	MOVE A,SPEXT(B)
	MOVE C,SPST(B)
	MOVEM C,(A)
	AOBJN B,QITR1
	SETOM QITUF
	JRST QIT

UUOH":
ERROR:	0
	MOVEM T,UUTSV'
	MOVEM TT,UUTTSV'
	MOVEM R,UURSV'
	LDB T,[331100,,40]
	CAIGE T,74
	JRST ERRA	;FOR UUO'S < UUOMAX+1


	LDB T,[270400,,40]	;LISP "CALL" TYPE UUO'S
	CAIG T,15
	TDZA R,R
	MOVEI R,-15(T)
	HLRE TT,@40
	AOJN TT,@UUNAF(R)	;NONATOMIC FCN
	HRRZ T,@40
UUOH1:	HLRZ TT,(T)
	HRRZ T,(T)
	CAIN TT,SUBR
	JRST @UUST(R)
	CAIN TT,FSUBR
	JRST @UUFST(R)
	CAIN TT,LSUBR
	JRST @UULT(R)
	CAIN TT,EXPR
	JRST @UUET(R)
	CAIN TT,FEXPR
	JRST @UUFET(R)
	CAIN TT,CARRAY
	JRST @UUAT(R)
	HRRZ T,(T)
	JUMPN T,UUOH1
	SKIPE EXTUUO
	PUSHJ P,@EXTUUO
	PUSH P,A
	PUSH P,B
	HRRZ A,40
	MOVEI B,VALUE
	PUSHJ P,GET
	JUMPN A,UUOH2
	HRRZ A,40
	PUSHJ P,EPRINT
	LERR [SIXBIT /UNDEFINED UUO!/]

EXTUUO:	0	;ALLOW NON-STANDARD FUNCTION FORMS


UUOARR:	HLRZ T,(T)
	TLOA T,20	;INDIRECT BIT.
UUOSBR":	HLRZ T,(T)
	MOVE TT,40
	TLO T,(PUSHJ P,)
	TLNE TT,1000	;1000 MEANS NO PUSH
	TLCA T,(JRST#PUSHJ P,)
	PUSH P,UUOH
UUOCL:	SKIPN VNOUUO
	TLNE TT,2000	;2000 MEANS NO CLOBBER
	JRST UUONCL
	PUSH P,T	;MAKE XCT OF UUO WORK
	SOS UUOH
	LDB T,[331100,,@UUOH]
	MOVSI TT,(@)
	IOR TT,UUOH
	CAIN T,XCT_-33
	MOVEI TT,@TT
	POP P,T
	MOVEM T,(TT)
UUONCL:	EXCH T,UUTSV
	MOVE TT,UUTTSV
	MOVE R,UURSV
	JRST @UUTSV

UUOH2:	HRRZ TT,(A)
	POP P,B
	POP P,A
	JRST UUOEX1

UUOS:	SKIPA TT,40
UUOEXP:	HLRZ TT,(T)
UUOEX1:	LDB T,[270500,,40]
UUOEX3:	TRZN T,20
	PUSH P,UUOH
	PUSH P,TT
	JUMPE T,IAPPLY
	MOVNS T
	HRLZ TT,T
	PUSH P,A(TT)
	AOBJN TT,.-1
	JRST IAPPLY

NOUUO":	HRRZM A,VNOUUO
	POPJ P,


;R=0 => COMPILER CALLING A -
;R=1 => COMPILER CALLING A LSUBR
;R=2 => COMPILER CALLING F TYPE
UUST:	UUOSBR
	UUOS1	;CALLING L ITS A SUBR
	UUOS2	;CALLING F
UUFST:	UUOS9	;CALLING - ITS A F
	UUOS10	;CALLING L
	UUOSBR
UULT:	UUOS7	;CALLING - ITS A L
	UUOSBR
	UUOS8
UUET:	UUOEXP
	UUOS5	;CALLING L ITS AN EXPR
	UUOS6	;CALLING F ITS AN EXPR
UUFET:	UUOS3	;CALLING - ITS A FEXPR
	UUOS4	;CALLING L
	UUOEX2
UUAT:	UUOARR
	UUOS1A
	UUOS2A
UUNAF:	UUOS
	UUL2N
	UUF2N

UUOEX2:	TLO A,400000
	MOVEI B,1
	DPB B,[270400,,40]
	JRST UUOEXP

UUOS1A:	HLRZ R,(T)
	SKIPA R,(R)
UUOS1:	HLRZ R,(T)
	MOVE T,UUTSV
	JSP TT,PDLARG
	HRRM R,UUOS1B
	MOVE TT,UUTTSV
	MOVE R,UURSV
UUOS1B:	JRST .

UUOS2A:	HLRZ T,(T)
	HRRZ T,(T)	;SUBR CALLED LIKE FSUBR
	JRST UUOS2+1

UUOS3:	PUSH P,(T)
	JSP TT,ARGPDL
UUOS4A:	JSP TT,QTLFY
	MOVEI TT,1
	DPB TT,[270400,,40]
UUOS6A:	POP P,TT
	HLRZS TT
	JRST UUOEX1

UUOS4:	PUSH P,(T)
	MOVE T,UUTSV
	JRST UUOS4A

UUOS5:	HLRZ R,(T)
	MOVE T,UUTSV
	JSP TT,PDLARG
	MOVNS T
	LDB TT,[330100,,40]
	DPB TT,[40100,,T]
	MOVE TT,R
	JRST UUOEX3

UUOS6:	PUSH P,(T)
	PUSH P,UUOH
	PUSH P,40
	JSP TT,ILIST
	JSP TT,PDLARG
	POP P,40
	POP P,UUOH
	MOVNS T
	DPB T,[270400,,40]
	JRST UUOS6A

UUL2N:	PUSH P,(P)
	HRROI TT,-2(P)
	SKIPL T,UUTSV
	AOJA TT,UUL21
	AOJE T,UUL21
	POP TT,1(TT)
	AOJL T,.-1
UUL21:	HRRZ T,40
	MOVEM T,1(TT)
	MOVE T,UUTSV
	JRST IAPPLY

UUF2N:	LDB T,[330100,,40]
	SKIPE T
	PUSH P,UUOH
	HRRZ T,40
	PUSH P,T
	JSP TT,ILIST
	JRST IAPPLY

UUOS8:	SKIPA TT,CILIST
UUOS7:	MOVEI TT,ARGPDL
	HRRM TT,UUOS7A
	MOVE TT,40
	TLNN TT,1000
	PUSH P,UUOH
	HLRZ TT,(T)
UUOS7A:	JRST ARGPDL

UUOS9:	PUSH P,T
	JSP TT,ARGPDL
UUS10A:	JSP TT,QTLFY
	MOVSI T,2000
	IORM T,40
	POP P,T
	JRST UUOSBR

UUOS10:	PUSH P,T
	MOVE T,UUTSV
	JRST UUS10A

UBD:	CAMN SP, B
	POPJ P,
	PUSHJ P,UNBIND
	JRST UBD



ARGPDL:	LDB T,[270400,,40]	;ARGS => PDL  -CNT=> T
	MOVNS T
	HRLZ R,T
ARGP1:	JUMPE R,(TT)
	PUSH P,A(R)
	AOBJN R,.-1
	JRST (TT)

QTIFY:	PUSHJ P,NCONS
	MOVEI B,CQUOTE
	JRST XCONS

QTLFY:	MOVEI A,0
QTLFY1:	JUMPE T,(TT)
	EXCH A,(P)
	PUSHJ P,QTIFY
	POP P,B
	PUSHJ P,CONS
	AOJA T,QTLFY1

PDLARG:	JRST .+NACS+2(T)
	REPEAT NACS+1,POP P,A+NACS-.RPCNT
	JRST (TT)


ERRA:	IFE TS,	CONO APR,410000+APRCHN
	CAIG T,UUOMAX
IFE TS,QQ=10
IFN TS,QQ=0
	JRST QQ,.+1(T)
	JRST QQ,ERRBAD	;PROG TRAPPED - ILLEGAL OPERATION
	JRST QQ,ERROR1	;ORDINARY LISP ERROR - ERRSETTABLE
	JRST QQ,ERROR2	;HALTING ERROR - E.G., PDL OVFL
	JRST ERROR3	;I/O OUTPUT ERROR - RESET I/O SWITCHES.
STRTYP:	MOVE TT,ERROR	;STRING TYPEOUT UUO.
ERRPRT:	MOVSI A,440600
	HRR A,40
	MOVEM A,ERROR
ERP1:	ILDB A,ERROR
	CAIN A,'!
	JRST (TT)
	CAIN A,'_
	TRCA A,'_#15
	ADDI A,40
	PUSHJ P,TYO
	JRST ERP1

ERROR2:	SKIPN PSYMF
	SKIPA P,C2
	SKIPA P,ERRTN
	SETZM ERRTN
	SETOM ERRSW
ERROR1:	SKIPN PSYMF
	SKIPE ERRSW
CERPRT:	JSP TT,ERRPRT
	JRST ERSET1
ERROR3:	SKIPE PSYMF
	JRST .+3
	SKIPN ERRSW
	JRST ERSET1
	PUSH P,CERPRT
	MOVEI TT,ERRSET1
ERRCOM:	SETZM TAPRED
	SETZM TAPWRT
	SETZM TTYOFF
	SETZM PLOTON
	SETZM LPTON
	JRST TERPRI

ERRBAD:	MOVE P,C2
	PUSHJ P,TERPRI
	HRRZ TT,ERROR
	HRRM TT,PRINL1
	PUSHJ P,PRINL2
	HRRZ R,PRINL1
	STRT [SIXBIT /_PROGRAM TRAPPED WHILE _ILGL INSTRUCTION IN !/]
	PUSHJ P,ERRADR
	JRST LOSP


ERRADR:	MOVEI A,QM	;QUESTION MARK
	HRROI NIL,CNIL2
	MOVEI AR2A,LISP
	MOVSI B,-OBTSIZ

ERRO2F:	HLRZ C,OBTBL(B)
ERRO2B:	JUMPE C,ERRO2A
	HLRZ TT,(C)
ERRO2C:	HRRZ TT,(TT)
	JUMPE TT,ERRO2G
	HLRZ AR1,(TT)
	CAIN AR1,LSUBR
	JRST ERRO2H
	CAIE AR1,SUBR
	CAIN AR1,FSUBR
	JRST ERRO2H
	HRRZ TT,(TT)
	JRST ERRO2C

ERRO2A:	AOBJN B,ERRO2F
	PUSHJ P,PRINC
CERR1:	POPJ P,ERROR1

ERRO2H:	HRRZ TT,(TT)
	HLRZ TT,(TT)
	CAMLE TT,AR2A	;LE TO PREFER CAR TO QUOTE
	CAMLE TT,R
	JRST ERRO2G
	MOVE AR2A,TT
	HLRZ A,(C)
ERRO2G:	HRRZ C,(C)
	JRST ERRO2B


BACRTN:	MOVEI A,0
ERR":	SKIPN ERRTN
	JRST LSPRET	;TOP LEVEL ERROR
	MOVE P,ERRTN
	MOVEI C,ERRP4
ERR1:	POP P,FP
	POP P,B
	PUSHJ P,UBD	;RESTORE CONDITIONS AND PROCEED
	POP P,ERRSW
	POP P,ERRTN
	JRST (C)

ERSTP:	PUSH P,PA3
	PUSH P,PA4
	PUSH P,ERRTN
	PUSH P,ERRSW
	SETZM ERRSW
	PUSH P,SP
	PUSH P,FP
	MOVEM P,ERRTN
	JRST (C)

ERRSET":	JSP C,ERSTP
	HRRZ C,(A)
	SKIPE C
	TDZA C,C
	MOVEI C,TRUTH
	MOVEM C,ERRSW
	HLRZ A,(A)
	PUSHJ P,EVAL
	PUSHJ P,NCONS
	JSP C,ERR1
POP2J:	SUB P,[2,,2]
	POPJ P,
ERSET1:	SKIPE ERRSW
	SKIPN VBAKGAG
	JRST ERRTN	;NO BACKTRACE
BACTRC:	HRRZ D,P
	SETOM BACTYF'
BKTR2:	HRRZ A,C2
	HRRZ B,1(A)
	CAIN B,PPEVAL+1
	CAMG D,A
	JRST BACRTN
	AOSN BACTYF
	STRT [SIXBIT /_BACKTRACE_!/]
	HRRZ A,(D)
BPSH":	CAIGE A,.
BPSL":	CAIGE A,.
	CAIGE A,FS
	JUMPN A,.+2
	SOJA D,BKTR2
	CAIN A,ILIST3
	JRST BKTR1A
BKTR1B:	CAIN A,CPOPJ
	JRST BKTR1C
	HLRZ B,-1(A)
	TRZ B,(@)
	CAILE B,77000
	CAIN B,(PUSHJ P,)
	CAIGE B,74000
	JRST BKTR1
	MOVEI R,@-1(A)
	PUSH P,R
BKTR4:	MOVEI R,-1(A)
	PUSHJ P,ERRADR
	MOVEI A,"-
	PUSHJ P,TYO
	POP P,R
	PUSHJ P,ERRADR
	MOVEI A,40
	PUSHJ P,TYO
BKTR1:	SOJA D,BKTR2

BKTR1A:	HRRZ B,-1(D)
	CAIE B,EXP2
	CAIN B,ESB1
	JRST .+2
	JRST BKTR1B
	HLRE B,-1(D)
	ADD B,D
	HLRZ A,-3(B)
	JUMPE A,BKTR1
	PUSHJ P,PRINC
	XCT "-,CTY
	STRT [SIXBIT /EVALARGS !/]
	JRST BKTR1

BKTR1C:	HLRZ A,(D)
	PUSHJ P,PRINC
	XCT "-,CTY
	STRT [SIXBIT /ENTER !/]
	JRST BKTR1

BAKGAG:	MOVEM A,VBAKGAG
	POPJ P,

ERRTN:	0	;0 => TOP LEVEL
	;- => PDL TO RESET TO - STORED BY ERRORSET
ERRSW:	-1	;0 MEANS NO PRNT ON ERROR


;ERROR MESSAGES

ER1:	LERR MES2	;FIRST OBJECT ON INPUT LIST ILLEGAL -READ
ER2:	LERR MES3	;CONTEXT ERROR WITH DOT NOTATION -READ

QA2A:	HLRZ A,(AR1)
QA2:	PUSHJ P,EPRINT
UNDFN:	LERR MES5	;FUNCTION OBJECT HAS NO DEFINITION-EVAL
QA8:	PUSHJ P,EPRINT
	LERR MES6	;UNBOUND VARIABLE-EVAL

E5:	LERR MES11	;NON-NUMERIC ARGUMENT -NUMARG
E6:	LERR MES12	;NO PRINT NAME -INTERN
E7:	LERR MES13	;NO LIST -MAKNAM


QF2:	LERR MES18	;TOO MANY ARGUMENTS SUPPLIED -APPLY
QF3:	LERR MES19	;TOO FEW ARGUMENTS SUPPLIED -APPLY
QF4:	LERR MES26

QA1:	PUSHJ P,EPRINT
	LERR MES25	;FUNCTION OBJECT HAS NO DEFINITION -APPLY

EG1:	HRRZ A,T
	PUSHJ P,EPRINT
	LERR MES22

MES2:	SIXBIT /ILLEGAL OBJECT - READ!/
MES3:	SIXBIT /DOT CONTEXT ERROR!/
MES5:	SIXBIT /UNDEFINED FUNCTION - EVAL!/
MES6:	SIXBIT /UNBOUND VARIABLE - EVAL!/
MES11:	SIXBIT /NON-NUMERIC ARGUMENT!/
MES12:	SIXBIT /NO PRINT NAME - INTERN!/
MES13:	SIXBIT /NO LIST-MAKNAM!/
MES18:	SIXBIT /TOO MANY ARGUMENTS SUPPLIED - APPLY!/
MES19:	SIXBIT /TOO FEW ARGUMENTS SUPPLIED - APPLY!/
MES22:	SIXBIT /UNDEFINED PROG TAG-GO!/
MES25:	SIXBIT /UNDEFINED FUNCTION - APPLY!/
MES26:	SIXBIT /WNA - DEFPROP!/
MES27:	SIXBIT /NO INPUT FILE OPENED!/


IFN VD,[
IFE TS,[
LPEN":	SETOM LTPOFF
	MOVE A,LTPY
	IDIV A,LTPCNT
	PUSHJ P,FIX1A
	MOVEM A,LTPY
	MOVE A,LTPX
	IDIV A,LTPCNT
	PUSHJ P,FIX1A
	MOVE B,LTPY
	PUSHJ P,CONS
	MOVEM A,LTPX
	MOVE A,LTPCNT
	PUSHJ P,FIX1A
	MOVE B,LTPX
	PUSHJ P,CONS
	SETZB B,LTPCNT
	MOVEM B,LTPX
	MOVEM B,LTPY
	MOVEM B,LTPOFF
	POPJ P,

LTPCNT:	0
LTPOFF:	0
LTPX:	0
]
IFN TS,[
LPEN":	SETZM LTPBLK
	.LTPEN LTPBLK
	MOVE A,LTPBLK+3
	IDIV A,LTPBLK+1
	PUSHJ P,FIX1A
	MOVEM A,LTPY
	MOVE A,LTPBLK+2
	IDIV A,LTPBLK+1
	PUSHJ P,FIX1A
	MOVE B,LTPY
	PUSHJ P,CONS
	MOVEM A,LTPY
	MOVE A,LTPBLK+1
	PUSHJ P,FIX1A
	MOVE B,LTPY
	JRST CONS
LTPBLK:	BLOCK 6
]

LTPY:	0

]

IFN MPXRF,[ 
IFN TS,[
MPX":	JUMPE A,MPX1	;FIRST ARG FOR IMXC,
	SOJE A,CIMX	;SECOND FOR OMXC
	SOSE A		;	NIL - DO NOTHING
	MOVSI A,4	;		0 - CLOSE CHANNEL
	HRRI A,(SIXBIT \IMX\)	;	1 - OPEN IN NORMAL MODE
	TSOPEN IMXC,A	;		2 - OPEN IN FAST MODE (ASCII)
MPX1:	JUMPE B,CPOPJ
	SOJE B,COMX
	SOSE B
	MOVEI B,4
	HRLZI B,1(B)
	HRRI B,(SIXBIT \OMX\)
	TSOPEN OMXC,B
	JRST TRUE

CIMX:	.CLOSE IMXC,
	JRST MPX1
COMX:	.CLOSE OMXC,
	JRST TRUE

OMPX":	PUSH P,A
	MOVE A,B
	PUSHJ P,NUMVAL
	MOVE B,(P)
	SOS B
	DPB B,[140600,,A]
	LSH A,18.
	.IOT OMXC,A
	JRST POPAJ

IMPX":	SOS A
	.IOT IMXC,A
	JRST FIX1A
] 

IFE TS,[
NOVAL==128.

MPX":	JUMPE A,CMPX
	SKIPE MPXSTA
	POPJ P,
	SETOM MPXSTA
	PUSHJ P,DCSWP
	MOVE A,[-<NOVAL+2>/3,,RWORLD-1]
	MOVEM A,DCPTR
	CONO MPXX,26000
	CONO DC,4260+TPCHN"
	CONO KRC,777761
	CONO KRC,16
	CONO CRK,10+APRCHN
	DATAO KRC,[CSPEED]
	JRST MPXEX

CMPX:	SKIPN MPXSTA
	POPJ P,
	SETZM MPXSTA
	PUSHJ P,DCSWP
MPXEX:	MOVEI A,0
	POPJ P,

DCSWP:	CONO DC,0
	MOVE A,40+2*TPCHN
	EXCH A,TPWD1
	MOVEM A,40+2*TPCHN
	MOVE A,41+2*TPCHN
	EXCH A,TPWD2
	MOVEM A,41+2*TPCHN
	POPJ P,

TPWD1:	BLKI DC,DCPTR
TPWD2:	JSR MPXBRK
MPXSTA:	0

DCPTR:	0

MPXBRK:	0
	SKIPN MPXSTA
	LER2 [SIXBIT \MPX LOSSAGE!\]
	MOVEM A,DCPTR
	MOVE A,[-<NOVAL+2>/3,,RWORLD-1]
	EXCH A,DCPTR
	CONO MPXX,26000
	CONO DC,4260+TPCHN
	JRST 12,@MPXBRK

IMPX":	SOS A
	IDIVI A,3
	LDB A,IMPXT(B)
	JRST FIX1A

IMPXT:	REPEAT 3,<.RPCNT*12.>_12.+12._6,,RWORLD(A)

RWORLD:	BLOCK <NOVAL+2>/3

OMPX":	CAILE A,101
	JRST FALSE
	PUSH P,A
	MOVE A,B
	PUSHJ P,NUMVAL
	POP P,B
	DPB A,[221400,,DESTIN-1(B)]
	JRST TRUE

DESTIN:	REPEAT 100,.RPCNT_12.+2000,,
]

WR760:	PUSHJ P,NUMVAL
	IFE TS,[TRZ A,7
		CONO 760,(A)]
	IFN TS,.WR760 A,
RD760:	IFE TS,CONI 760,A
	IFN TS,.RD760 A,
	JRST FIX1A

]


IFN PL,[IFE TS,[

LBUFF==400
LWBUFF==1000

PLTINT:	SOSLE SCCNT
	JRST CCONO
NEWI:	ILDB A,IBPT
NEXT:	JUMPE A,NEWC
	TRNE A,10
	JRST NEWI1
	TRZN A,1
PEN:	JRST PENCTL
DOT:	HRRM A,CT
	TRNE A,2
	SKIPA A,IBPT2'
	MOVE A,[(400)DT-1]
	EXCH A,IBPT
	MOVEM A,IBPT2
	MOVSI A,30000
	XORM A,SETCNT
CT:	MOVEI A,
PENCTL:	LSH A,6
	TRZ A,777177
	TRO A,70000+PLTCHN
	JRST CPLOT

NEWI1:	HRL A,SCALE
SETCNT:	HLRZM A,SCCNT
CR1:	MOVE A,CVRTAB-10(A)
CVRROT:	ROT A,3
XYI:	TRZ A,777607
	TRO A,64000+PLTCHN
CPLOT:	HRRM A,CCONO
CCONO:	CONO PLT,
	JRST PLTRET


PLOT":	PUSHJ P,NUMVAL
PLOTC":	JUMPG A,CHARPL"
SPEC:	JUMPE A,INIT
	SKIPN WCNT
	JRST .-1
	IDPB A,WPTI
	SOS WCNT
	HRRZ A,WPTI
	CAIN A,WBUFF+LWBUFF-1
	HRRI A,WBUFF-1
	HRRM A,WPTI
	MOVEI A,0

CHARPL:	IDPB A,BPTI
	AOSE BCNT
	JRST .+4
	AOS BCNT
	CONO PLT,3000+PLTCHN"
	CONO PI,2200+1_<7-PLTCHN>
	MOVEI A,LBUFF*4
	CAMN A,BCNT
	JRST .-1
	MOVE A,BPTI
	CAMN A,BPTEND
	HRRI A,BUFF-1
	HRRM A,BPTI
	JRST TRUE
]
IFN TS,[

PLOT":	PUSHJ P,NUMVAL
PLOTC":	JUMPL A,SPECPR
CHARPL":	ANDI A,377
	CLEARM IBPT
SHIFT:	MOVE A,UCTAB(A)
SETC":	TLNE A,40
	JRST @A
	MOVEM A,IBPT
	LDB A,A
	IMUL A,SCALE
	ADDM A,X
	ILDB A,IBPT
	IMUL A,SCALE
	ADDM A,Y
NEWI:	ILDB A,IBPT
	JUMPE A,NEWC
	TRNE A,10
	JRST NEWI1
	TRZN A,1
PEN:	JRST PENCTL
DOT:	HRRM A,CT
	TRNE A,2
	SKIPA A,IBPT2'
	MOVE A,[(400)DT-1]
	EXCH A,IBPT
	MOVEM A,IBPT2
	MOVSI A,30000
	XORM A,SETCNT
CT:	MOVEI A,
PENCTL:	LSH A,3
	JRST CPLOT

NEWI1:	HRL A,SCALE
SETCNT:	HLRZM A,SCCNT
CR1:	MOVE A,CVRTAB-10(A)
CVRROT:	ROT A,0
	TRZ A,777760
CPLOT:	SKIPE PLTON
	.IOT PLTC,A
	SOSLE SCCNT
	JRST CPLOT
	JRST NEWI
]
SSHFTT:	MOVEI A,SLCTAB
	JRST PSHIFT
SSHFTN:	MOVEI A,SUCTAB
	JRST PSHIFT
SHIFTT:	SKIPA A,LSHFTT
SHIFTN:	MOVEI A,UCTAB
PSHIFT:	HRRM A,SHIFT

IFE TS,[

NEWC":	SOSGE BCNT
	JRST NMC
	MOVE A,BPTO
	CAMN A,BPTEND
	HRRI A,BUFF-1
	HRRM A,BPTO
NEXTC:	CLEARM IBPT
	ILDB A,BPTO
	JUMPE A,SPECPR
SHIFT:	MOVE A,UCTAB(A)
SETC":	TLNE A,40
	JRST @A
	MOVEM A,IBPT
	LDB A,A
	IMUL A,SCALE
	ADDM A,X
	ILDB A,IBPT
	IMUL A,SCALE
	ADDM A,Y
	JRST NEWI

NMC:	CONO PLT,60000
	CLEARM IBPT
	JRST PLTRET

INIT:	CONO PLT,60000
	MOVEI A,(HLRZM A,)
	HRLM A,SETCNT
	MOVEI A,UCTAB
	HRRM A,SHIFT
	HRRM A,LSHIFT
	CLEARM SCCNT
	CLEARM IBPT
	MOVE A,BPTBEG
	MOVEM A,BPTI
	MOVEM A,BPTO
	MOVEI A,WBUFF-1
	HRRM A,WPTI
	HRRM A,WPTO
	MOVEI A,LWBUFF
	MOVEM A,WCNT
	MOVEI A,NEWC
	HRRM A,NEXT
CPEN:	MOVEI A,PENCTL
	HRRM A,PEN
	SETOM BCNT
	JRST TRUE
PLTON":	0
]
IFN TS,[

NEWC":	AOSN BELFLG
	JSR QUIT
	JRST TRUE

INIT:	.OPEN PLTC,OPLTC
	JRST FALSE
	AOS PLTON
	MOVEI A,(HLRZM A,)
	HRLM A,SETCNT
	MOVEI A,UCTAB
	HRRM A,SHIFT
	HRRM A,LSHIFT
	CLEARM SCCNT
CPEN:	MOVEI A,PENCTL
	HRRM A,PEN
	JRST TRUE

CLOSEP:	PUSHJ P,SHIFTN
	.CLOSE PLTC,
	CLEARB A,PLTON
	POPJ P,

PLOTON":	0
]

PLTON":	0

DEFINE PLTRN
IFE TS,[JRST NEWC
]IFN TS,[JRST TRUE
]TERMIN

CVRTAB:	403011022001
	050240241010
	204406014002
	120120500404
	453251263011
	254646255012
	324526514406
	523131522405

CHDIR:	LSH A,-24.
	DPB A,[(20300)CVRROT]
	TRC A,0
	ROT A,-3
	HRRM A,.-2

	TLNN A,100000
	JRST .+4
	EXCH A,X
	EXCH A,Y
	EXCH A,X

	TLNN A,200000
	JRST .+5
	EXCH A,Y
	MOVNS A
	ADDI A,4000
	EXCH A,Y

	TLNN A,400000
	PLTRN
	MOVN A,X
	ADDI A,4000
	MOVEM A,X
	PLTRN
CR0:	MOVE A,X
	JUMPE A,NEWC
	CLEARM X
	MOVMM A,SCCNT
	HRRI A,L
	JUMPG A,CR1
	JRST HTB1

HTAB:	MOVEI A,40
	IMUL A,SCALE
	HRRM A,SCHT
	EXCH X
	IDIVI (A)
	AOS
SCHT:	IMULI
	EXCH X
	SUBI A,@SCHT
	MOVNM A,SCCNT
HTB1:	HRRI A,RT
	JRST CR1

LF:	MOVNI A,9
	IMUL A,SCALE
	ADDM A,Y
LF1:	MOVMM A,SCCNT
	HRRI A,DN
	JRST CR1

VTAB:	MOVEI A,40
	IMUL A,SCALE
	HRRM A,SCVT
	EXCH Y
	SOS
	IDIVI (A)
SCVT:	IMULI
	EXCH Y
	AOJA A,LF1

PFF:	MOVEI A,4000
	EXCH A,Y
	ANDI A,3777
	ADDI A,400
	JRST LF1

ZEROSC:	CLEARM SCALE
	PLTRN

ADSC:	ASH A,-24.
	ADDM A,SCALE
	PLTRN

SUBSCR:	MOVNI A,3
	IMUL A,SCALE
	ADDM A,Y
	JRST LF1

BACKSP:	MOVNI A,6
	IMUL A,SCALE
	ADDM A,X
	MOVNM A,SCCNT
	HRRI A,L
	JRST CR1

VALIGN:	MOVE C,[577772377767]
	JRST XYFORM

STEP:	MOVEI B,1
	TLZN A,40000
	MOVE B,SCALE
	MOVEM B,SCCNT
	TLZE A,20000
	MOVNS B
	TLZE A,10000
	ADDM B,X
	TLZE A,4000
	MOVNS B
	TLZE A,2000
	ADDM B,Y
	LSH A,-30
	JRST CR1

LNOPEN:	SKIPA A,CPEN
ENOPEN:	MOVEI A,NEWI
	HRRM A,PEN
	PLTRN

SCALE":	1
SCCNT:	0
IFE TS,[
BPTI:	(41000)BUFF-1
BPTO:	(41000)BUFF-1
BCNT:	-1
WPTI:	(14300)WBUFF-1
WPTO:	(14300)WBUFF-1
BPTBEG:	(41000)BUFF-1
BPTEND:	(41000)BUFF+LBUFF-1
WCNT:	LWBUFF
BUFF:	BLOCK LBUFF
WBUFF:	BLOCK LWBUFF
]
IBPT:	0

X":	0
Y":	0
DIR:	0

DEFINE	TBGN A3,A4
	A3!A4
	IF1,A3!A4==0
TERMIN

UCTAB":	REPEAT 200,TBGN UC,\.RPCNT
LCTAB":	REPEAT 200,TBGN LC,\.RPCNT

SUCTAB":	REPEAT 200,TBGN SUC,\.RPCNT
SLCTAB":	REPEAT 200,TBGN SLC,\.RPCNT

DEFINE	I A1,A5,A2
	A1!A5=.OP <IBP 4> . 0
	IRP A,,[A2]
		A
	TERMIN
		0
TERMIN

DU==3
DD==5
PU==2
PD==4
RT==10
U==11
L==12
DN==13
UR==14
UL==15
DL==16
DR==17

.BYTE 4

DT:	IRP A,,[U,UR,L,L,UR,DN,DN,UL,RT,RT,DL,U,DU]
		\A
	TERMIN

.WALGN

CHTAB:	I UC,1,[0,0,PD]
	I UC,3,[0,0,PU]
	I UC,4,[0,3,U,U,U]
	I UC,\"A,[6,0,PD,U,U,U,U,UR,UR,DR,DR,DN,DN,L,L,L,L,RT,RT,RT,RT,DN,DN,PU,RT,RT]
	I UC,\"B,[6,0,U,U,U,PD,RT,RT,RT,UR,U,UL,L,L,L,DN,DN,DN,DN,DN,DN,RT,RT,RT,UR,U,UL,PU,DR,DR,DR]
	I UC,\"C,[6,0,UR,UR,UR,UR,U,PD,UL,L,L,DL,DN,DN,DN,DN,DR,RT,RT,UR,PU,DR,RT]
	I UC,\"D,[6,0,PD,U,U,U,U,U,U,RT,RT,RT,DR,DN,DN,DN,DN,DL,L,L,L,PU,RT,RT,RT,RT,RT,RT]
	I UC,\"E,[6,0,UR,UR,UR,UR,U,U,PD,L,L,L,L,DN,DN,DN,RT,RT,RT,L,L,L,DN,DN,DN,RT,RT,RT,RT,PU,RT,RT]
	I UC,\"F,[6,0,PD,U,U,U,RT,RT,RT,L,L,L,U,U,U,RT,RT,RT,RT,PU,DR,DR,DN,DN,DN,DN]
	I UC,\"G,[6,0,UR,UR,UR,UR,U,PD,UL,L,L,DL,DN,DN,DN,DN,DR,RT,RT,UR,U,L,L,PU,DR,DR,RT,RT]
	I UC,\"H,[6,0,PD,U,U,U,U,U,U,DN,DN,DN,RT,RT,RT,RT,U,U,U,DN,DN,DN,DN,DN,DN,PU,RT,RT]
	I UC,\"I,[4,0,PD,RT,RT,L,U,U,U,U,U,U,L,RT,RT,PU,DR,DR,DN,DN,DN,DN]
	I UC,\"J,[6,0,U,U,PD,DN,DR,RT,RT,UR,U,U,U,U,U,PU,DR,DR,DN,DN,DN,DN]
	I UC,\"K,[6,0,PD,U,U,U,U,U,U,DN,DN,DR,UR,UR,UR,DL,DL,DL,DR,DR,DR,PU,RT,RT]
	I UC,\"L,[6,0,U,U,U,U,U,U,PD,DN,DN,DN,DN,DN,DN,RT,RT,RT,RT,PU,RT,RT]
	I UC,\"M,[6,0,PD,U,U,U,U,U,U,DR,DR,UR,UR,DN,DN,DN,DN,DN,DN,PU,RT,RT]
	I UC,\"N,[6,0,PD,U,U,U,U,U,U,DR,DR,DR,DR,U,U,U,U,DN,DN,DN,DN,DN,DN,PU,RT,RT]
	I UC,\"O,[6,0,RT,PD,UL,U,U,U,U,UR,RT,RT,DR,DN,DN,DN,DN,DL,L,L,PU,RT,RT,RT,RT,RT]
	I UC,\"P,[6,0,PD,U,U,U,U,U,U,RT,RT,RT,DR,DN,DL,L,L,L,PU,DR,DR,DR,RT,RT,RT]
	I UC,\"Q,[6,0,RT,PD,UL,U,U,U,U,UR,RT,RT,DR,DN,DN,DN,DN,DL,L,L,PU,UR,RT,PD,DR,PU,RT,RT]
	I UC,\"R,[6,0,PD,U,U,U,U,U,U,RT,RT,RT,DR,DN,DL,L,L,L,RT,DR,DR,DR,PU,RT,RT]
	I UC,\"S,[6,0,U,PD,DR,RT,RT,UR,U,UL,L,L,UL,U,UR,RT,RT,DR,PU,DR,DR,DN,DN,DN]
	I UC,\"T,[6,0,RT,RT,PD,U,U,U,U,U,U,L,L,RT,RT,RT,RT,PU,DR,DR,DN,DN,DN,DN]
	I UC,\"U,[6,0,U,U,U,U,U,U,PD,DN,DN,DN,DN,DN,DR,RT,RT,UR,U,U,U,U,U,PU,DR,DR,DN,DN,DN,DN]
	I UC,\"V,[6,0,U,U,U,U,U,U,PD,DN,DN,DN,DN,DR,DR,UR,UR,U,U,U,U,PU,DR,DR,DN,DN,DN,DN]
	I UC,\"W,[6,0,U,U,U,U,U,U,PD,DN,DN,DN,DN,DN,DN,UR,UR,DR,DR,U,U,U,U,U,U,PU,DR,DR,DN,DN,DN,DN]
	I UC,\"X,[6,0,PD,U,UR,UR,UR,UR,U,DN,DL,DL,UL,UL,U,DN,DR,DR,DR,DR,DN,PU,RT,RT]
	I UC,\"Y,[6,0,U,U,U,U,U,U,PD,DN,DN,DR,DR,DN,DN,U,U,UR,UR,U,U,PU,DR,DR,DN,DN,DN,DN]
	I UC,\"Z,[6,0,U,U,U,U,U,U,PD,RT,RT,RT,RT,DN,DL,DL,DL,DL,DN,RT,RT,RT,RT,PU,RT,RT]
	I LC,\"A,[5,0,U,U,U,PD,UR,RT,DR,DN,DN,DL,L,UL,UR,RT,RT,DN,DN,PU,RT,RT]
	I LC,\"B,[5,0,PD,U,U,U,U,U,U,DN,DN,DN,UR,RT,DR,DN,DN,DL,L,UL,PU,DR,RT,RT,RT,RT]
	I LC,\"C,[5,0,UR,UR,UR,PD,UL,L,DL,DN,DN,DR,RT,UR,PU,DR,RT]
	I LC,\"D,[5,0,RT,RT,RT,PD,U,U,U,U,U,DN,DN,UL,L,DL,DN,DN,DR,RT,UR,PU,DR,RT]
	I LC,\"E,[5,0,U,U,PD,RT,RT,RT,U,UL,L,DL,DN,DN,DR,RT,UR,PU,DR,RT]
	I LC,\"F,[5,0,RT,PD,U,U,U,L,RT,RT,RT,L,L,U,U,UR,RT,DR,PU,DR,DN,DN,DN,DN]
	I LC,\"G,[5,0,DN,PD,DR,RT,UR,U,U,U,U,UL,L,DL,DN,DN,DR,RT,UR,PU,DR,RT]
	I LC,\"H,[5,0,PD,U,U,U,U,U,U,DN,DN,DN,UR,RT,DR,DN,DN,DN,PU,RT,RT]
	I LC,\"I,[2,0,PD,U,U,U,U,PU,U,DD,DR,DR,DN,DN,DN]
	I LC,\"J,[5,0,DN,PD,DR,RT,UR,U,U,U,U,U,PU,U,DD,DR,DR,DN,DN,DN]
	I LC,\"K,[5,0,PD,U,U,U,U,U,U,DN,DN,DN,DR,UR,UR,DL,DL,DR,DR,PU,RT,RT]
	I LC,\"L,[2,0,PD,U,U,U,U,U,U,PU,DR,DR,DN,DN,DN,DN]
	I LC,\"M,[6,0,PD,U,U,U,U,DN,UR,DR,DN,DN,DN,U,U,U,UR,DR,DN,DN,DN,PU,RT,RT]
	I LC,\"N,[5,0,PD,U,U,U,U,DN,UR,RT,DR,DN,DN,DN,PU,RT,RT]
	I LC,\"O,[5,0,RT,PD,UL,U,U,UR,RT,DR,DN,DN,DL,L,PU,RT,RT,RT,RT]
	I LC,\"P,[5,0,U,PD,DR,RT,UR,U,U,UL,L,DL,U,DN,DN,DN,DN,DN,DN,PU,UR,UR,RT,RT,RT]
	I LC,\"Q,[5,0,UR,RT,RT,PD,DL,L,UL,U,U,UR,RT,DR,U,DN,DN,DN,DN,DN,DN,PU,UR,UR]
	I LC,\"R,[5,0,PD,U,U,U,U,DN,UR,RT,DR,PU,DR,DR,DN]
	I LC,\"S,[5,0,U,PD,DR,RT,UR,UL,L,UL,UR,RT,DR,PU,DR,DR,DN]
	I LC,\"T,[4,0,RT,RT,PD,UL,U,U,U,U,DN,L,RT,RT,PU,DR,DR,DN,DN]
	I LC,\"U,[5,0,U,U,U,U,PD,DN,DN,DN,DR,RT,UR,DN,U,U,U,U,PU,DR,DR,DN,DN]
	I LC,\"V,[6,0,U,U,U,U,PD,DN,DN,DR,DR,UR,UR,U,U,PU,DR,DR,DN,DN]
	I LC,\"W,[6,0,U,U,U,U,PD,DN,DN,DN,DN,UR,UR,DR,DR,U,U,U,U,PU,DR,DR,DN,DN]
	I LC,\"X,[6,0,PD,UR,UR,UR,UR,DL,DL,UL,UL,DR,DR,DR,DR,PU,RT,RT]
	I LC,\"Y,[6,0,U,U,U,U,PD,DN,DN,DR,DR,DL,DL,UR,UR,UR,UR,U,U,PU,DR,DR,DN,DN]
	I LC,\"Z,[6,0,U,U,U,U,PD,RT,RT,RT,RT,DL,DL,DL,DL,RT,RT,RT,RT,PU,RT,RT]
	I UC,\"&,[6,0,UR,UR,RT,RT,PD,DL,DL,L,UL,U,UR,RT,UR,U,UL,DL,DN,DN,DR,DR,DR,PU,RT,RT]
	I LC,\"-,[6,0,U,U,PD,RT,RT,PU,DR,DR]
	I UC,\"-,[6,0,U,U,U,PD,RT,RT,RT,RT,PU,DR,DR,DN]
	I UC,\"+,[6,0,UR,RT,PD,U,U,U,U,DN,DN,L,L,RT,RT,RT,RT,PU,DR,DR,DN]
	I LC,\"+,[5,0,U,U,PD,UR,DR,UR,PU,DR,DR,DN]
	I UC,\"=,[6,0,UR,UR,UR,UR,PD,L,L,L,L,PU,DN,DN,PD,RT,RT,RT,RT,PU,DR,DR]
	I LC,\"=,[6,0,U,U,PD,RT,RT,UL,UL,DR,DR,UR,UR,DL,DL,RT,RT,PU,DR,DR]
	I LC,\"$,[5,0,UR,U,U,U,U,U,PD,DN,RT,U,DN,DR,UL,DN,DN,L,U,U,DL,DR,DN,DN,UL,DR,DN,U,RT,U,U,DR,DL,DN,PU,RT,RT,RT]
	I UC,\"$,[5,0,U,PD,UR,UR,UR,PU,DN,PD,UL,L,DL,DN,DR,RT,UR,PU,DR,DR]
	I UC,\"@,[6,0,UR,UR,UR,PD,DL,L,U,UR,RT,DN,DN,UR,U,UL,L,L,DL,DN,DN,DR,RT,RT,UR,PU,DR,DR]
	I UC,\",,[3,0,DN,PD,UR,U,L,DN,RT,PU,RT,RT]
	I UC,\".,[3,0,PD,U,RT,DN,L,PU,RT,RT,RT]
	I UC,\"/,[6,0,U,PD,UR,UR,UR,UR,PU,DR,DR,DN,DN,DN]
	I UC,\"*,[6,0,U,PD,UR,UR,UR,UR,DL,DL,U,U,DN,DN,DN,DN,U,U,RT,RT,L,L,L,L,RT,RT,UL,UL,DR,DR,DR,DR,PU,DR,RT]
	I LC,\"*,[4,0,U,U,U,U,PD,UR,UR,DL,U,DN,DN,U,RT,L,L,RT,UL,DR,DR,PU,DR,DR,DN,DN]
	I UC,\"^,[4,0,UR,PD,U,U,U,U,DL,UR,DR,PU,DR,DR,DN,DN]
	I LC,\"^,[4,0,UR,U,U,U,U,PD,DN,DN,DN,DN,UL,DR,UR,PU,DR,DR]
	I UC,\"_,[6,0,UR,U,PD,UL,UR,DL,RT,RT,RT,RT,PU,DR,DR,DN]
	I LC,\"_,[6,0,U,U,U,PD,RT,RT,RT,RT,UL,DR,DL,PU,DR,DR,RT]
	I UC,\"!,[2,0,DD,U,PD,U,U,U,U,U,PU,DR,DR,DN,DN,DN,DN]
	I UC,\"?,[6,0,RT,RT,DD,U,PD,U,UR,UR,U,UL,L,L,DL,PU,DR,DR,DR,DR,DR,RT]
	I UC,\"\,[6,0,U,U,U,U,U,PD,DR,DR,DR,DR,PU,DR,RT]
	I LC,\"\,[6,0,U,PD,UR,UR,UR,UR,PU,DL,DL,DL,DL,DN]
	I UC,7,[6,0,UR,RT,PD,U,L,L,U,UR,U,RT,U,DN,RT,DN,DR,DN,L,L,PU,DR,DR,RT,RT]
	I UC,\";,[3,0,DN,PD,UR,U,L,DN,RT,PU,U,U,U,PD,U,L,DN,RT,PU,DR,DR,DN]
	I UC,\":,[3,0,PD,U,RT,DN,L,PU,UR,U,U,PD,U,L,DN,RT,PU,DR,DR,DN]
	I UC,\"(,[3,0,RT,PD,UL,U,U,U,U,UR,PU,DR,DR,DN,DN,DN,DN]
	I UC,\"),[3,0,PD,UR,U,U,U,U,UL,PU,DR,DR,DR,DN,DN,DN]
	I UC,\"[,[3,0,RT,PD,L,U,U,U,U,U,U,RT,PU,DR,DR,DN,DN,DN,DN]
	I UC,\"],[3,0,PD,RT,U,U,U,U,U,U,L,PU,DR,DR,DR,DN,DN,DN]
	I UC,\"<,[4,0,UR,RT,PD,UL,UL,UR,UR,PU,DR,DR,DN,DN,DN]
	I LC,\"<,[6,0,U,U,PD,UR,UR,DR,DR,PU,DR,DR]
	I UC,\">,[4,0,U,PD,UR,UR,UL,UL,PU,DR,DR,DR,DR,DN]
	I LC,\">,[6,0,U,U,U,U,PD,DR,DR,UR,UR,PU,DR,DR,DN,DN]
	I UC,\"',[2,0,U,U,U,U,U,U,PD,DN,DN,PU,DR,DR,DN,DN]
	I UC,\"",[3,0,U,U,U,U,PD,U,U,PU,RT,PD,DN,DN,PU,DR,DR,DN,DN]
	I UC,\"#,[6,0,UR,PD,U,U,U,U,DN,L,RT,RT,RT,RT,L,U,DN,DN,DN,DN,U,RT,L,L,L,L,PU,DR,DR,RT,RT,RT,RT]
	I UC,\"%,[6,0,U,PD,UR,UR,UR,UR,PU,L,L,PD,UL,DL,DR,UR,PU,DR,DN,DN,PD,DL,DR,UR,UL,PU,DR,DR,RT]
	I UC,\"0,[6,0,U,PD,UR,UR,UR,UR,UL,L,L,DL,DN,DN,DN,DN,DR,RT,RT,UR,U,U,U,U,PU,DR,DR,DN,DN,DN]
	I UC,\"1,[3,0,RT,PD,U,U,U,U,U,U,DL,PU,DR,DR,DR,DN,DN]
	I UC,\"2,[6,0,U,U,U,U,U,PD,UR,RT,RT,DR,DN,DL,DL,DL,DL,RT,RT,RT,RT,PU,RT,RT]
	I UC,\"3,[6,0,U,PD,DR,RT,RT,UR,U,UL,L,L,RT,RT,UR,U,UL,L,L,DL,PU,DR,DR,DR,DR,DR,RT]
	I UC,\"4,[6,0,RT,RT,RT,RT,PD,U,U,U,U,U,U,DL,DL,DL,DL,RT,RT,RT,RT,PU,DR,DR]
	I UC,\"5,[6,0,U,PD,DR,RT,RT,UR,U,U,UL,L,L,DL,U,U,U,RT,RT,RT,RT,PU,DR,DR,DN,DN,DN,DN]
	I UC,\"6,[6,0,U,U,U,PD,RT,RT,RT,DR,DN,DL,L,L,UL,U,U,U,U,UR,RT,RT,DR,PU,DR,DR,DN,DN,DN]
	I UC,\"7,[6,0,RT,PD,U,U,UR,UR,UR,U,L,L,L,L,PU,DR,DR,DR,DR,DR,DR]
	I UC,\"8,[6,0,UR,U,U,PD,RT,RT,UR,U,UL,L,L,DL,DN,DR,DL,DN,DR,RT,RT,UR,U,UL,PU,DR,DR,DR]
	I UC,\"9,[6,0,U,PD,DR,RT,RT,UR,U,U,U,U,UL,L,L,DL,DN,DR,RT,RT,RT,PU,DR,DR,DN]
	I UC,\" ,[4,0,RT,RT,RT,RT]
	I LC,\" ,[3,0,RT,RT,RT]
	I UC,170,[0,0,DD]
IFN TS,[UC0=(40)INIT
	LC0=(40)CLOSEP
]	UC2=(40)SUBSCR
	UC5=(40)ENOPEN
	UC6=(40)LNOPEN
	UC15=(40)CR0
	UC16=(40)SHIFTN
	UC17=(40)SHIFTT
	UC11=(40)HTAB
	UC12=(40)LF
	UC13=(40)VTAB
	UC14=(40)PFF
	UC10=(40)ZEROSC
	UC20=(140)ADSC
	UC21=(240)ADSC
	UC22=(440)ADSC
	UC23=(1040)ADSC
	UC24=(2040)ADSC
	UC25=(4040)ADSC
	UC26=(10040)ADSC
	UC27=(20040)ADSC
	LC20=(777740)ADSC
	LC21=(777640)ADSC
	LC22=(777440)ADSC
	LC23=(777040)ADSC
	LC24=(776040)ADSC
	LC25=(774040)ADSC
	LC26=(770040)ADSC
	LC27=(760040)ADSC
	UC30=(0040)CHDIR	;ALONG X AXIS
	UC31=(5340)CHDIR	;ALONG Y AXIS
	UC32=(6640)CHDIR	;UPSIDE DOWN ALONG X AXIS
	UC33=(3540)CHDIR	;UPSIDE DOWN ALONG Y AXIS
	UC34=(2440)CHDIR	;BACKWARDS ALONG X AXIS
	UC35=(7740)CHDIR	;BACKWARDS ALONG Y AXIS
	UC36=(4240)CHDIR	;BACKWARDS AND UPSIDE DOWN ALONG X AXIS
	UC37=(1140)CHDIR	;BACKWARDS AND UPSIDE DOWN ALONG Y AXIS
	LC160=(50040+RT_6)STEP
	LC161=(42040+U_6)STEP
	LC162=(70040+L_6)STEP
	LC163=(46040+DN_6)STEP
	LC164=(52040+UR_6)STEP
	LC165=(76040+UL_6)STEP
	LC166=(72040+DL_6)STEP
	LC167=(56040+DR_6)STEP
	UC160=(10040+RT_6)STEP
	UC161=(2040+U_6)STEP
	UC162=(30040+L_6)STEP
	UC163=(6040+DN_6)STEP
	UC164=(12040+UR_6)STEP
	UC165=(36040+UL_6)STEP
	UC166=(32040+DL_6)STEP
	UC167=(16040+DR_6)STEP

DEFINE	LCEUC A1
	LC!A1=UC!A1
TERMIN
	IRP A,,[1,2,3,4,5,6,7,10,11,12,13,14,15,16,17,30,31,32,33,34,35,36,37,170
		"0,"1,"2,"3,"4,"5,"6,"7,"8,"9,"#,"@,"%,"&,[",],".,"/,"!,"?,";,":,"(,"),133,135,"',""]
		LCEUC \A
	TERMIN

SCHTAB:	SUC1=UC1
	SUC3=UC3
	SUC4=UC4
DEFINE	SUCEUC A1,A2
	SUC!A1=UC!A2
TERMIN
	REPEAT 10,SUCEUC \41+.RPCNT,\101+.RPCNT
	REPEAT 21,SUCEUC \52+.RPCNT,\112+.RPCNT
	I SUC,51,[6,0,RT,PD,RT,RT,L,U,U,U,U,U,U,L,RT,RT,PU,DR,DR,DR,DN,DN,DN]
	I SLC,41,[6,0,U,U,U,PD,UR,RT,RT,DR,DN,DN,DL,L,L,UL,UR,RT,RT,RT,DN,DN,PU,RT,RT]
	I SLC,42,[6,0,PD,U,U,U,U,U,U,DN,DN,DN,UR,RT,RT,DR,DN,DN,DL,L,L,UL,PU,DR,RT,RT,RT,RT,RT]
	I SLC,43,[6,0,UR,UR,UR,RT,PD,UL,L,L,DL,DN,DN,DR,RT,RT,UR,PU,DR,RT]
	I SLC,44,[6,0,RT,RT,RT,RT,PD,U,U,U,U,U,U,DN,DN,DN,UL,L,L,DL,DN,DN,DR,RT,RT,UR,PU,DR,RT]
	I SLC,45,[6,0,U,U,PD,RT,RT,RT,RT,U,UL,L,L,DL,DN,DN,DR,RT,RT,UR,PU,DR,RT]
	I SLC,46,[6,0,RT,PD,U,U,U,L,RT,RT,RT,L,L,U,U,UR,RT,DR,PU,DR,DR,DN,DN,DN]
	I SLC,47,[6,0,DN,PD,DR,RT,RT,UR,U,U,U,U,UL,L,L,DL,DN,DN,DR,RT,RT,UR,PU,DR,RT]
	I SLC,50,[6,0,PD,U,U,U,U,U,U,DN,DN,DN,UR,RT,RT,DR,DN,DN,DN,PU,RT,RT]
	I SLC,51,[6,0,RT,PD,RT,RT,L,U,U,U,U,L,PU,UR,DD,DR,DR,DR,DN,DN]
	I SLC,52,[6,0,DN,PD,DR,RT,UR,U,U,U,U,U,PU,U,DD,DR,DR,DR,DN,DN]
	I SLC,53,[6,0,RT,PD,U,U,U,U,U,U,DN,DN,DN,DR,UR,UR,DL,DL,DR,DR,PU,RT,RT]
	I SLC,54,[6,0,RT,PD,RT,RT,L,U,U,U,U,U,U,L,PU,DR,DR,DR,DN,DN,DN]
	SLC55=LC115
	I SLC,56,[6,0,PD,U,U,U,U,DN,UR,RT,RT,DR,DN,DN,DN,PU,RT,RT]
	I SLC,57,[6,0,RT,PD,UL,U,U,UR,RT,RT,DR,DN,DN,DL,L,L,PU,RT,RT,RT,RT,RT]
	I SLC,60,[6,0,U,PD,DR,RT,RT,UR,U,U,UL,L,L,DL,U,DN,DN,DN,DN,DN,DN,PU,UR,UR,RT,RT,RT,RT]
	I SLC,61,[6,0,UR,RT,RT,RT,PD,DL,L,L,UL,U,U,UR,RT,RT,DR,U,DN,DN,DN,DN,DN,DN,PU,UR,UR]
	I SLC,62,[6,0,RT,PD,U,U,U,U,DN,UR,RT,DR,PU,DR,DR,DN]
	I SLC,63,[6,0,U,PD,DR,RT,RT,UR,UL,L,L,UL,UR,RT,RT,DR,PU,DR,DR,DN]
	I SLC,64,[6,0,UR,UR,UR,U,PD,L,L,L,RT,U,DN,DN,DN,DN,DR,RT,UR,PU,DR,RT]
	I SLC,65,[6,0,U,U,U,U,PD,DN,DN,DN,DR,RT,RT,UR,DN,U,U,U,U,PU,DR,DR,DN,DN]
	SLC66=LC126
	SLC67=LC127
	SLC70=LC130
	SLC71=LC131
	SLC72=LC132
	SUC106=UC46
	SUC115=UC55
	SUC113=UC53
	I SLC,103,[6,0,U,U,PD,UR,DR,DR,UR,PU,DR,DR]
	SUC135=UC75
	I SUC,104,[6,0,U,U,PD,DR,RT,RT,UR,UL,L,L,UL,UR,RT,RT,DR,UL,L,U,DN,DN,DN,DN,DN,DN,PU,RT,RT,RT,RT]
	SUC40=UC100
	I SUC,114,[6,0,DR,PD,UR,U,L,DN,RT,PU,RT,RT,RT,RT]
	I SUC,116,[6,0,RT,PD,U,RT,DN,L,PU,RT,RT,RT,RT,RT]
	SUC117=UC57
	SUC112=UC52
	I SLC,106,[6,0,UR,RT,PD,U,U,U,U,DL,UR,DR,PU,DR,DR,DR,DN]
	I SLC,110,[6,0,UR,UR,U,U,U,PD,DN,DN,DN,DN,UL,DR,UR,PU,DR,DR,RT]
	SLC111=UC137
	SLC107=LC137
	I SLC,101,[6,0,RT,DD,U,PD,U,U,U,U,U,PU,DR,DR,DR,DR,DR,DN]
	SUC137=UC77
	SLC112=UC134
	SLC123=UC7
	I SUC,133,[6,0,DR,PD,UR,U,L,DN,RT,PU,U,U,U,PD,U,L,DN,RT,PU,DR,DR,DR,RT]
	I SUC,132,[6,0,UR,PD,RT,DN,L,U,PU,UR,U,PD,U,L,DN,RT,PU,DR,DR,DR,RT]
	I SUC,110,[6,0,RT,RT,RT,PD,UL,U,U,U,U,UR,PU,DR,DR,DR,DN,DN,DN]
	I SUC,111,[6,0,RT,PD,UR,U,U,U,U,UL,PU,DR,DR,DR,DR,DR,DN]
	I SUC,113,[6,0,RT,RT,RT,PD,L,U,U,U,U,U,U,RT,PU,DR,DR,DR,DN,DN,DN]
	I SLC,114,[6,0,RT,PD,RT,U,U,U,U,U,U,L,PU,DR,DR,DR,DR,DR,DN]
	I SUC,134,[6,0,UR,RT,RT,PD,UL,UL,UR,UR,PU,DR,DR,DR,DN,DN]
	SLC101=LC74
	I SUC,136,[6,0,UR,PD,UR,UR,UL,UL,PU,DR,DR,DR,DR,DR]
	SLC102=LC76
	I SUC,107,[6,0,UR,UR,U,U,U,U,PD,DN,DN,PU,DR,DR,DR,DR]
	I SUC,102,[6,0,UR,U,U,U,PD,U,U,PU,RT,RT,PD,DN,DN,PU,DR,DR,DR,DN]
	SLC103=UC43
	SLC105=UC45
	SLC120=UC60
	I SUC,121,[6,0,RT,PD,RT,RT,L,U,U,U,U,U,U,DL,PU,DR,DR,DR,DR,DR]
	REPEAT 10,SUCEUC \122+.RPCNT,\62+.RPCNT
	I SUC,100,[6,0,RT,RT,RT,RT,RT,RT]
	I SLC,104,[6,0,U,PD,RT,RT,RT,UR,U,U,UL,L,L,L,PU,DR,DR,DR,DR,DR,RT]
	I SLC,105,[6,0,UR,UR,UR,UR,U,PD,L,L,L,DL,DN,DN,DR,RT,RT,RT,PU,DR,RT]
	I SLC,115,[6,0,RT,RT,RT,PD,UL,U,UL,UR,U,UR,PU,DR,DR,DR,DN,DN,DN]
	I SLC,116,[6,0,RT,PD,UR,U,UR,UL,U,UL,PU,DR,DR,DR,DR,DR,DN]
	I SLC,117,[6,0,UR,UR,U,DD,DR,DR,DR,RT]
	I SLC,120,[6,0,DL,PD,RT,RT,RT,RT,RT,RT,PU]
	I SLC,121,[6,0,UL,U,U,U,U,U,U,PD,RT,RT,RT,RT,RT,RT,PU,DR,DN,DN,DN,DN,DN,DN]
	I SLC,122,[6,0,RT,RT,PD,U,U,U,U,U,U,PU,DR,DR,DR,DR,DN,DN]
	I SLC,124,[6,0,UL,UL,UL,UL,UL,U,U,DD,RT,RT,DD,DR,UR,DR,DN,DN,DN,DN]
	I SLC,125,[6,0,UL,UL,UL,UL,UL,U,PD,UR,UR,PU,DR,DR,DR,DN,DN,DN,DN,DN]
	I SLC,126,[6,0,UL,UL,UL,UL,UL,U,U,U,PD,DR,DR,PU,DR,DR,DR,DN,DN,DN]
	I SLC,127,[6,0,UL,UL,UL,UL,UL,U,U,PD,UR,DR,PU,DR,DR,DR,DN,DN,DN,DN]
	I SLC,130,[6,0,PD,RT,RT,RT,RT,PU,RT,RT]
	I SLC,131,[0,12,U,U,U,U,U,U,U,U,U,U]
	SUC170=UC170
	REPEAT 10, 0
	SUC0=UC0
	SLC0=LC0
	SUC2=(40)SUBSCR
	SLC133=SUC2
	SLC137=SUC4
	SUC5=(40)ENOPEN
	SUC6=(40)LNOPEN
	SUC15=(40)CR0
	SUC74=SUC15
	SUC16=(40)SSHFTN
	SUC75=SUC16
	SUC17=(40)SSHFTT
	SUC76=SUC17
	SUC11=(40)HTAB
	SUC12=(40)LF
	SUC73=SUC12
	SUC13=(40)VTAB
	SUC14=(40)PFF
	SUC10=(40)ZEROSC
	SUC20=(140)ADSC
	SUC21=(240)ADSC
	SUC22=(440)ADSC
	SUC23=(1040)ADSC
	SUC24=(2040)ADSC
	SUC25=(4040)ADSC
	SUC26=(10040)ADSC
	SUC27=(20040)ADSC
	SLC20=(777740)ADSC
	SLC21=(777640)ADSC
	SLC22=(777440)ADSC
	SLC23=(777040)ADSC
	SLC24=(776040)ADSC
	SLC25=(774040)ADSC
	SLC26=(770040)ADSC
	SLC27=(760040)ADSC
	SUC30=(0040)CHDIR	;ALONG X AXIS
	SUC31=(5340)CHDIR	;ALONG Y AXIS
	SUC32=(6640)CHDIR	;UPSIDE DOWN ALONG X AXIS
	SUC33=(3540)CHDIR	;UPSIDE DOWN ALONG Y AXIS
	SUC34=(2440)CHDIR	;BACKWARDS ALONG X AXIS
	SUC35=(7740)CHDIR	;BACKWARDS ALONG Y AXIS
	SUC36=(4240)CHDIR	;BACKWARDS AND UPSIDE DOWN ALONG X AXIS
	SUC37=(1140)CHDIR	;BACKWARDS AND UPSIDE DOWN ALONG Y AXIS
	SLC134=SUC30
	SLC135=SUC31
	SLC160=(50040+RT_6)STEP
	SLC161=(42040+U_6)STEP
	SLC162=(70040+L_6)STEP
	SLC163=(46040+DN_6)STEP
	SLC164=(52040+UR_6)STEP
	SLC165=(76040+UL_6)STEP
	SLC166=(72040+DL_6)STEP
	SLC167=(56040+DR_6)STEP
	SUC160=(10040+RT_6)STEP
	SUC161=(2040+U_6)STEP
	SUC162=(30040+L_6)STEP
	SUC163=(6040+DN_6)STEP
	SUC164=(12040+UR_6)STEP
	SUC165=(36040+UL_6)STEP
	SUC166=(32040+DL_6)STEP
	SUC167=(16040+DR_6)STEP
	SLC132=(40)BACKSP
	SLC135=(40)VALIGN

DEFINE	SLCEUC A1
	SLC!A1=SUC!A1
TERMIN
	IRP A,,[1,2,3,4,5,6,7,10,11,12,13,14,15,16,17,30,31,32,33,34,35,36,37,73,74,75,76,100,170]
		SLCEUC \A
	TERMIN
.BYTE
CLENGTH":	PUSHJ P,NUMVAL
	ANDI A,377
CLNGTH":	CAIN A,16
	JRST LSHFTN
	CAIN A,17
	JRST LSHFTT
LSHIFT:	LDB A,UCTAB(A)
	POPJ P,
LSHFTN:	SKIPA A,SHIFTN
LSHFTT:	MOVEI A,LCTAB
	HRRM A,LSHIFT
	MOVEI A,0
	POPJ P,

SPECPR:
IFE TS,[
	HRRZ A,WPTO
	CAIN A,WBUFF+LWBUFF-1
	HRRI A,WBUFF-1
	HRRM A,WPTO
	AOS WCNT
	ILDB A,WPTO
]
SETS":	TLNN A,200000
	JRST XYFORM
	TLNN A,100000
	JRST DEFCH
	TLNN A,40000
IFE TS,[
	JRST GOC
]IFN TS,[
	JRST @A
]
	MOVSS A
	TRZ A,777770
	JRST @.+1(A)
	SETSC
	STHTAB
	SETLF
	STVTAB
	SETX
	SETY
	GOX
	GOY
SETSC:	HLRZM A,SCALE
	PLTRN
STHTAB:	HLRM A,HTAB
	PLTRN
SETLF:	HLRM A,LF
	PLTRN
STVTAB:	HLRM A,VTAB
	PLTRN
IFE TS,[
GOC:	POP P,B
	MOVEM A,XS
	MOVEI A,@XS
	EXCH A,(P)
	POPJ P,
]

DEFCH:	TLNE A,377
	JRST DEFCH1
CHSET:	HRRM A,SHIFT
	PLTRN
DEFCH1:	HRRZ B,A
	TLNE A,400
	TLOA B,40
	TLO B,400
	ROT A,9
	DPB A,[(360600)]
	LSH A,-27.
	MOVEM B,@SHIFT
	PLTRN
SETX:	HLREM A,X
	PLTRN
SETY:	HLREM A,Y
	PLTRN
GOX:	HLREM A,XS
	MOVE A,Y
	MOVEM A,YS
	JRST VECTXY
GOY:	HLRES A
	CLEARM XS
	JRST VECTY

XYFORM:	LSH A,2
	HRRM A,YS'
	ASH A,-2
	HLREM A,XS'
	HRL A,YS
	ASH A,-2
	HLREM A,YS
	TRNN A,140000
	JRST SETXY
	TRNN A,100000
	JRST VSCXY
	TRNN A,40000
	JRST VINCXY

VECTXY:	MOVE A,XS
	SUBM A,X
	EXCH A,X
	MOVEM A,XS
	MOVE A,YS
VECTY:	SUBM A,Y
	EXCH A,Y
	MOVEM A,YS
	JRST VINC1

VSCXY:	MOVE A,SCALE
	IMULM A,XS
	IMULM A,YS

VINCXY:	MOVE A,XS
	ADDM A,X
	MOVE A,YS
	ADDM A,Y
VINC1:	MOVE A,CVRTAB+RT-10
	SKIPGE XS
	MOVE A,CVRTAB+L-10
	XCT CVRROT
	HRRM A,DIR
	MOVMS XS
	MOVE A,CVRTAB+U-10
	SKIPGE YS
	MOVE A,CVRTAB+DN-10
	XCT CVRROT
	HRLM A,DIR
	MOVM A,YS
	CAMGE A,XS
	JRST .+4
	JUMPE A,NEWC
	MOVSS DIR
	EXCH A,XS
	MOVEI B,200000
	ASHC A,-1
	HRLZM B,YCNT'
	DIV A,XS
	MOVEM A,YS
IFE TS,[
	HRRI A,.+2
	HRRM A,NEXT

VRET:	MOVE A,DIR
	MOVE B,YCNT
	ADD B,YS
	TLZE B,200000
	TSO A,A
	MOVEM B,YCNT
	SOSLE XS
	JRST XYI
VRES:	HRLI A,NEWC
	HLRM A,NEXT
	JRST XYI
]IFN TS,[
VRET:	MOVE A,DIR
	MOVE B,YCNT
	ADD B,YS
	TLZE B,200000
	TSO A,A
	MOVEM B,YCNT
	SOSGE XS
	JRST TRUE
	TRZ A,777760
	SKIPE PLTON
	.IOT PLTC,A
	JRST VRET
]
SETXY:	MOVE A,XS
	MOVEM A,X
	MOVE A,YS
	MOVEM A,Y
	PLTRN


IPL":	MOVNI C",INITL
IPL0:	MOVE A",SPINIT+INITL(C)
	PUSHJ P",PLOTC"
	JUMPE A,CPOPJ
	AOJL C,IPL0
IPL1:	MOVE C,HERE
	MOVEM C,PTM'
	CLEARM KNOWN'
	CLEARM CARCAS'
	CLEARM PENPOS'
	MOVEI C,334
	MOVEM C,CARDIR'
	POPJ P,

SPINIT:	0
	3
	30
	400000,,2100.
	400000,,-100.
HERE:	400000,,600000
INITL==.-SPINIT

NEXTPL":	PUSHJ P,PENUP
	MOVEI A,30
	PUSHJ P,PLOTC
	MOVE A,[400000+2300.,,600000]
	PUSHJ P,PLOTC
	MOVSI A,400000
	PUSHJ P,PLOTC
	JRST IPL1

PLOTLIST":	AOJGE T,PLTL1
	ADDI B,37
	TRNE B,777600
	JRST PLTL1
	HRRM B,PTCHAR
	SETOM CHPTF
	CAIN B,170
	CLEARM CHPTF
PLTL1:	MOVEI B,CARRAY
	PUSHJ P,GET
	JUMPE A,CPOPJ
	HRRZ A,(A)
	HRRZ A,-1(A)
PLTLIS":	ADD A,[(2200)-1]
	MOVEM A,WPT'
	MOVE A,[600000,,SUCTAB]
	PUSHJ P,PLOTC
PARAM:	ILDB C,WPT
	TRNN C,100
	JRST NOSC
	MOVEI A,10
	PUSHJ P",PLOTC
	LDB A,[(40200)C]
	ADDI A,21
	PUSHJ P,PLOTC
NOSC:	TRNN C,10
	JRST NOINT
	MOVEI A,6
	TRNN C,7
	MOVEI A,5
	PUSHJ P,PLOTC
NOINT:	TRNE C,2000
	JRST PLEXIT
NWMODE:	LDB C,[(150300)C]
	SOJLE C,NOKCH
	SKIPN KNOWN
	PUSHJ P,PLOTIT
UKNOWN:	SETOM KNOWN
	JRST .(C)
	JRST PLEXIT
	JRST CH
	JRST VECT
	JRST PLEXIT
	JRST VINC
PLEXIT:	MOVE A,[600000,,UCTAB]
	PUSHJ P,PLOTC
	HRRZ A,WPT
	JRST FIX1A
NOKCH:	JUMPL C,PARAM

PT:	PUSHJ P,PENUP
	ILDB C,WPT
	SKIPN KNOWN
	JRST FAST
	TRNE C,200000
	JRST YPOS
XPOS:	DPB C,[(231200)PTM]
	HRRZS KNOWN
	LDB A,[(221300)PTM]
	HRLI A,777776
PLOT1:	PUSHJ P,PLOTC
	TRNN C,2000
	JRST NWMODE
INTEN:	MOVE A,[577775,,377776]
	SKIPE CHPTF
	PUSHJ P,PLOTC
PTCHAR:	MOVEI A,170
	PUSHJ P,PLOTC
	MOVE A,[400003,,377774]
	SKIPE CHPTF
	PUSHJ P,PLOTC
	JRST NWMODE
YPOS:	DPB C,[(11200)PTM]
	HLLZS KNOWN
	LDB A,[(1300)PTM]
	HRLI A,777777
	JRST PLOT1
FAST:	TRNE C,200000
	DPB C,[(11200)PTM]
	TRNN C,200000
	DPB C,[(231200)PTM]
	TRNN C,2000
	JRST NWMODE
	MOVE A,PTM
	PUSHJ P,PLOTC
	JRST INTEN
PLOTIT:	MOVE A,PTM
	JRST PLOTC

CHPTF:	0	;THIS IS NOT A COMMENT

VECT:	ILDB C,WPT
	TRNE C,200000
	PUSHJ P,PENDWN
	TRNN C,200000
	PUSHJ P,PENUP
	LDB A,[(700)C]
	TRNE C,200
	MOVNS A
	MOVEM A,CTEM'
	LDB A,[(100700)C]
	TRNE C,100000
	MOVNS A
	HRL A,CTEM
	TDZ A,[(200000)400000]
	TSO A,[(200000)400000]
	PUSHJ P,PLOTC
	TRNN C,400000
	JRST VECT
	JRST PARAM

PENDWN:	SKIPGE PENPOS
	POPJ P,
	SETOM PENPOS
	MOVEI A,1
	JRST PLOTC

PENUP:	AOSE PENPOS
	POPJ P,
	MOVEI A,3
	JRST PLOTC

VINC:	ILDB C,WPT
	TRNN C,200000
	PUSHJ P,PENUP
	MOVEI AR1,4
	ROT C,-20
VINC2:	ROT C,4
	LDB A,[(400)C]
	MOVE A,VINCTB(A)
	PUSHJ P,PLOTC
	CAIE AR1,4
	JRST .+3
	TRNE C,20
	PUSHJ P,PENDWN
	SOJG AR1,VINC2
	TRNN C,400000
	JRST VINC
	JRST PARAM

VINCTB:	75
	75
	161
	163
	75
	75
	161
	163
	160
	160
	164
	167
	162
	162
	165
	166

CH:	PUSHJ P,PENUP
	MOVE A,CARDIR
	PUSHJ P,PLOTC
CH1:	ILDB C,WPT
	MOVEI AR1,3
CH2:	HRLI C,0
	LSH C,6
	HLRZ A,C
	CAIN A,37
	JRST CHOUT
	CAIN A,35
	CLEARM CARCAS
	CAIE A,36
	JRST .+4
	MOVEI A,200
	MOVEM A,CARCAS
	JRST CH3
	TRO A,@CARCAS
	ADDI A,40
	CAIE A,334
	CAIN A,336
	MOVEM A,CARDIR
	PUSHJ P,PLOTC
CH3:	SOJG AR1,CH2
	JRST CH1
CHOUT:	MOVEI A,334
	PUSHJ P,PLOTC
	JRST PARAM
]

.NOPOINT:	HRRZM A,V.NOPOI
	POPJ P,

EPRINT:	SKIPN ERRSW
	POPJ P,
	SETZM TTYOFF

PRINT":	MOVEI R,TYO	;UTILIZES DIRECTLY OR INDIRECTLY
CTY:	JSR 15,TYOI	;A,B,C,TT,R,D
	PUSHJ P,PRIN1
	XCT " ,CTY
	POPJ P,
PRIN1":	SKIPA R,CTYO
PRINC":	HRROI R,TYO
	PUSH P,A
	PUSHJ P,PRINTA
	JRST POPAJ
PRINTA":	PUSH P,A
	PUSHJ P,PATOM
	JUMPN A,PRINT1
	XCT "(,CTY
PRINT3:	HLRZ A,@(P)
	PUSHJ P,PRINTA
	HRRZ A,@(P)
	JUMPE A,PRINT2
	MOVEM A,(P)
	XCT CTY
	PUSHJ P,PATOM
	JUMPE A,PRINT3
	XCT ".,CTY
	XCT " ,CTY
	PUSHJ P,PRIN1A
PRINT2:	XCT "),CTY
	JRST POPAJ
PRINT1:	PUSHJ P,PRIN1A
	JRST POPAJ
PRIN1A":	MOVE A,-1(P)
	HRRM A,PRINL1
	CAIG A,INUM
	SOJA A,PRINI5
	CAIGE A,@GCP1
	CAIGE A,FS
	JRST PRINL
PRIN1B:	HRRZ A,@-1(P)
	JUMPE A,PRINL
	HLRZ B,(A)
	HRRZ A,(A)
	CAIN B,PNAME
	JRST PRINN
	CAIN B,FIXNUM
	JRST PRINI1
	CAIN B,FLONUM
	JRST PRINO
	MOVEM A,-1(P)
	JRST PRIN1B


PRINN:	HLRZ B,(A)	;PRINT FROM PNAME
	SETOM PCNT
PRINN1:	JUMPE B,CPOPJ
	HLRZ C,(B)
	HRRZ B,(B)
	HRLI C,440700
PRINN2:	ILDB A,C
	JUMPE A,PRINN1
	AOSN PCNT
	CAILE A,"9
	CAIGE A,"0
	CAIGE A,"+
	CAIN A,177
	JUMPGE R,PRINN3
	CAIG A,")
	CAIGE A,"(
	CAIG A,40
	JUMPGE R,PRINN6
PRINN4:	PUSHJ P,(R)
	TLNN C,760000
	JRST PRINN1
	JRST PRINN2
PRINN3:	CAIG A,"-
	CAIN A,",
	JRST PRINN6
	LDB D,[013400,,@C]
	SKIPN PCNT
	JUMPN D,PRINN6
	JRST PRINN4
PRINN6:	XCT "/,CTY
	JRST PRINN4

PCNT:	0


PRINI1:	SKIPA A,(A)
PRINI5:	JUMPL A,PRIN1B
PRINI:	HRRZ C,VBASE	;PRINT INTEGER
	JUMPGE A,PRINI2
	XCT "-,CTY
	MOVNS A
PRINI2:	MOVEI B,".-"0
	HRLM B,(P)
	CAIN C,10.+1
	SKIPE V.NOPOINT
	JRST .+2
	PUSH P,PRINI4
PRINI3:	LSHC A,-43
	LSH B,-1
	DIVI A,-1(C)
	CAILE B,9
	ADDI B,"A-"9-1
	HRLM B,(P)
	SKIPE A
	PUSHJ P,PRINI3
PRINI4:	JRST FP7A1

PRINO:	MOVE A,(A)	;FLOATING POINT NUMBER
	CLEARB B,C
	JUMPG A,FP1
	JUMPE A,FP3
	MOVNS,A
	XCT "-,CTY
FP1:	CAMGE A,FT01
	JRST,FP4
	CAML A,FT8
	AOJA B,FP4
FP3:	MULI A,400
	ASHC B,-243(A)
	MOVE A,B
	CLEARM,FPTEM
	PUSHJ P,FP7
	XCT ".,CTY
	MOVNI T,10
	ADD T,FPTEM
	MOVE B,C
FP3A:	MOVE A,B
	MULI A,12
	PUSHJ P,FP7B
	SKIPE,B
	AOJL T,FP3A
CPOPJA:	POPJ P,
FP4:	MOVNI C,6
	MOVEI TT,0
FP4A:	ADDI TT,1(TT)
	XCT,FCP(B)
	TRZA TT,1
	FMPR A,@FCP+1(B)
FP4B:	AOJN C,FP4A
	PUSH P,TT
	MOVNI B,-2(B)
	DPB B,[300200,,FP4C]
	PUSHJ P,FP3
	MOVEI A,105
	PUSHJ P,(R)
FP4C:	XCT "+,CTY
	POP P,A
FP7:	JUMPE A,FP7A1
	IDIVI A,12
	AOS,FPTEM
	HRLM B,(P)
	JUMPE A,FP7A1
	PUSHJ P,FP7
FP7A1:	HLRE A,(P)
FP7B:	ADDI A,60
	JRST (R)


	1.0^32.
	1.0^16.
FT8:	1.0^8
	1.0^4
	1.0^2
	1.0^1
FT:	1.0^0
	1.0^-32.
	1.0^-16.
	1.0^-8
	1.0^-4
	1.0^-2
FT01:	1.0^-1
FT0:
FCP:	CAMLE A,FT0(C)
	CAMGE A,FT(C)
	C,,FT0

FPTEM:	0

PRINL2:	SKIPA R,CTYO	;PRINT NONSTANDARD POINTER
PRINL:	XCT "#,CTY
PRINL1:	MOVEI A,.
	MOVEI C,10+1
	JRST PRINI3


TYOI:	0		;GOSPER HAC TO OUTPUT
	PUSH P,.-1	;ONE CHARACTER BY <XCT "T, CTY>
	PUSH P,A
	MOVE A,TYOI
	LDB A,[270600,,-1(A)]
	PUSHJ P,(R)
	JRST POPAJ

TERPRI":	MOVEI A,15
CTYO:	PUSHJ P,TYO
	JRST FALSE


IFN MPXRF,[ 
IFE TS,[
VIDI":	JSP T,SPECBIND
	VDISLIST
	CONO VID,10
	CONSZ DIS,77
	JRST .-1
	CONO DIS,100
	DPB B,[001200,,VIDI1]
	DATAO DIS,VIDI1
	DPB A,[221200,,VIDI2]
	CONSO DIS,200
	JRST .-1
	DATAO DIS,VIDI2
	MOVEI A,1000
	CONSO DIS,200
	JRST .-1
	CONSO VID,10
	SOJG A,.-1
	DATAI VID,A
	CAIGE A,2
	ADDI A,400
	AOJA A,UNBIND

VIDI1:	20135,,220000
VIDI2:	2000,,0
]
VIDLIN":	PUSHJ P,VIDI
	CAILE A,INUM
	POPJ P,		;FOR -1 RETURNED.
	MOVEI B,400
	IDIV B,A
	MOVE A,B
	POPJ P,

VIDLOG":	PUSHJ P,VIDI
	CAILE A,INUM
	POPJ P,		;FOR -1 RETURNED.
	TLO A,232000
	FAD A,A
	FMPR A,[0.035]
	REPEAT 3,FMPR A,A
	MOVEM A,B
	REPEAT 2,FMPR A,A
	FMPR A,B
	ASH A,-33
	MOVNS A
	ADDI A,400
	POPJ P,
IFN TS,[
VIDI":	SKIPE VIDOPD
	PUSHJ P,VIDOPN
	HRL A,B
	.IOT VIDC,A
	AOJE A,VID4
	CAILE A,3
	CAILE A,400
	ADDI A,400
	POPJ P,
VID4:	SOJA A,FIX1A

NVID":	PUSHJ P,NVIDI
	TLNE A,3
	MOVSI A,(-1.0)
	MOVEI B,FLONUM
	JRST MAKNUM
NVIDI:	SKIPE NVDOPD
	PUSHJ P,NVDOPN
	HRL A,B
	MOVE AR1,A
	MOVE C,[-1,,AR1]
	PUSHJ P,NVDPRE
	MOVE A,AR1
	.IOT NVDC,A
	POPJ P,
NVFIX":	PUSH P,B
	PUSH P,A
	PUSHJ P,NUMBERP
	JUMPE A,NVFXB
	POP P,A
	POP P,B
	PUSHJ P,NVIDI
	MOVE C,[-1,,A]
	JRST NVDPST
NVFXB:	POP P,A
	MOVEI B,CARRAY
	PUSHJ P,GET
	SKIPN A
	JRST .+5
	HRRZ AR1,(A)
	MOVE A,VBPEND
	PUSHJ P,NUMVAL
	CAIG AR1,(A)
	LERR [SIXBIT /BAD ARG - NVFIX!/]
	HRRZ AR1,-1(AR1)
	POP P,A
	PUSHJ P,NUMVAL
	MOVNS A
	HRL AR1,A
	MOVE C,AR1
	PUSHJ P,NVDPRE
	SKIPE BVDOPD
	PUSHJ P,BVDOPN
NVFXB1:	MOVE A,AR1
	.IOT BVDC,A
	MOVE C,AR1
	PUSHJ P,NVDPST
	JRST FALSE

NVDPRE:	HLRZ A,(C)
	PUSHJ P,NUMVAL
	IMUL A,NVSCL
	HLLM A,(C)
	HRRZ A,(C)
	PUSHJ P,NUMVAL
	IMUL A,NVSCL
	HLRM A,(C)
	AOBJN C,NVDPRE
	POPJ P,

NVDPST:	MOVE A,(C)
	TLNE A,3
	TRZ A,-1
	MOVEI A,1(A)
	MOVEM A,(C)
	AOBJN C,NVDPST
	POPJ P,

NVSCL":	1,,

NVSET":	PUSH P,C
	PUSH P,B
	LDB C,[251700,,ONVDC]
NVFIL:	SOJL A,NVCONF
	DPB A,[100200,,C]
	TRNN A,4
	TRZA C,10_10
	TRO C,10_10
NVCONF:	POP P,A
	SOJL A,NVRES
	LSH A,1
	TRNE A,4
	TRO A,1
	DPB A,[000200,,C]
NVRES:	POP P,A
	JUMPE A,NVDIM
	PUSHJ P,NUMVAL
	HRLZI B,40000
	IDIVM B,A
	MOVEM A,NVSCL
NVDIM:	SOJL AR1,NVXYZ
	DPB AR1,[020300,,C]
NVXYZ:	JUMPE AR2A,NVST1
	SOJN AR2A,.+2
	TRZA C,340
	TRO C,340
NVST1:	DPB C,[251700,,ONVDC]
	DPB C,[251700,,OBVDC]
	SETOM NVDOPD
	MOVE A,C
	JRST FIX1A

]
]

IFN PKPKF,[
;BINARY IMAGE READ ROUTINE
;USES ONLY LISP ACS A THRU AR2A, T, AND TT
IMPT:	0
IMBYT:	(442200)	;RT. HALF TO BE FILLED
FUDGEF":	0
XDIM==-1
YDIM==-2
FXMIN==-3
FYMIN==-4
FXDEN==-5
FYDEN==-6
BYTER==-7
VDFLG==-10
LIGHT==-11
FLG==AR2A
IFE TS,TAPAC"==A

VREAD":	PUSH P,B
	PUSH P,C
	PUSH P,AR1
	PUSH P,AR2A
	PUSH P,TT
	PUSH P,T
	PUSH P,A
	HLRZ A,(A)
	MOVEI B,CARRAY
	PUSHJ P,GET
	SKIPN A
	LERR [SIXBIT /FIRST ARG NOT AN ARRAY, READPIC!/]
	HRRZ C,(A)
	MOVEI B,1
	MOVEI A,1
	PUSHJ P,(C)	;USE ARRY ACCES TO FIND
	HRRM B,IMBYT	;FIRST LOC OF ARRAY.
	MOVE A,(P)
	HRRZ A,(A)
	JSP TT,ILIST
	HRRZ AR2A,IMBYT
	POP P,A
	PUSHJ P,NUMFIN
	MOVEM A,FYDEN(AR2A)
	ADDI T,3
	JUMPGE T,STEXD	;CHECK IF ALSO XDEN
	POP P,A
	PUSHJ P,NUMFIN
	AOJE T,STEXD
	LERR [SIXBIT /WRONG # ARGS, READPIC!/]
STEXD:	MOVEM A,FXDEN(AR2A)
	POP P,A
	PUSHJ P,NUMFIN
	MOVEM A,FYMIN(AR2A)
	POP P,A
	PUSHJ P,NUMFIN
	MOVEM A,FXMIN(AR2A)
	MOVE A,IMBYT
	MOVEM A,IMPT
	JRST TAPER
DEFINE FICKS X
	MULI X,400
	TSC X,X
	ASH X+1,-243(X)
TERMIN

DEFINE FLOTE X
	TLC X,232000
	FAD X,X
TERMIN

TAPER:	MOVSI B,-20
IFN TS,[HRRI B,TPARAM
	.IOT UTIC,B]
IFE TS,[
PARRD:
	PUSHJ P,.URED"
	LERR [SIXBIT /EOF, READPIC!/]
	MOVEM TAPAC",TPARAM(B)
	AOBJN B,PARRD]
	JRST INDITC

NUMFIN:	PUSHJ P,NUMVAL
	CAIN B,FLONUM
	POPJ P,
	FLOTE A
	FDV A,[4096.0]
	POPJ P,
;CLOBBERS B
RBUFSZ==100

RBYT:	SOS XREAD
	ILDB T,RBUFBP
	SOSLE RBUFC
	POPJ P,
REFIL:	SKIPG B,RBTOT
	POPJ P,
	MOVEI B,RBUFSZ
	CAMLE B,RBTOT
	MOVE B,RBTOT
	MOVN B,B
	ADDM B,RBTOT
	HRLZ B,B
	HRRI B,RBUFF
IFN TS,[MOVEM B,RBAOBJ
	.IOT UTIC,RBAOBJ]
IFE TS,[PUSHJ P,.URED
	LERR [SIXBIT /EOF, READPIC!/]
	MOVEM TAPAC,(B)
	AOBJN B,.-3]
	HRRZ B,TBYTER
	IMULI B,RBUFSZ
	MOVEM B,RBUFC
	HLLZ B,TBYTER
	HRRI B,RBUFF
	MOVEM B,RBUFBP
	POPJ P,

RBUFBP:	0	;BYTE POINTER
RBUFC:	0	;TOTAL BYTES IN BUFFER
RBAOBJ:	0	;AOBJN POINTER FOR BLOCK INPUT
RBTOT:	0	;TOTAL NO. OF WORDS STILL TO INPUT
RBUFF:	BLOCK RBUFSZ

ENDNIL:	POP P,A	;RETURN NIL
	MOVEI A,[NIL]
	JRST ENDER+1
ENDER:	POP P,A
	POP P,T
	POP P,TT
	POP P,AR2A
	POP P,AR1
	POP P,C
	POP P,B
IFN TS, .CLOSE UTIC,
	HLRZ A,(A)
	POPJ P,

TXINT:	0
TYINT:	0
XDMP:	0
INDITC:	SETZM ERROR	;CLEAR ERROR PRINT ROUTINE ENTRY
	MOVE A,TVDFLG
	MOVEM A,VDFLG(AR2A)
	MOVE A,TLIGHT
	MOVEM A,LIGHT(AR2A)
	SKIPG XDIM(AR2A)
	STRT [SIXBIT /_XDIM <= 0, READPIC!/]
	SKIPG YDIM(AR2A)
	STRT [SIXBIT /_YDIM <= 0, READPIC!/]
	MOVE A,TXDEN
	FSC A,-1
	FSB A,TXMIN
	MOVNM A,TXINT
	FAD A,FXMIN(AR2A)
	FDV A,TXDEN
	FICKS A
	SKIPGE B
	STRT [SIXBIT /_XDIM TOO SMALL, READPIC!/]
	MOVEM B,XDMP
	ADDI B,1
	FLOTE B
	FMP B,TXDEN
	FAD B,TXINT
	MOVEM B,TFXST
	MOVE A,TYDEN
	FSC A,-1
	FSB A,TYMIN
	MOVNM A,TYINT
	FAD A,FYMIN(AR2A)
	FDV A,TYDEN
	FICKS A
	SKIPGE B
	STRT [SIXBIT /_YDIM TOO SMALL, READPIC!/]
	MOVE A,B
	ADDI A,1
	FLOTE A
	FMP A,TYDEN
	FAD A,TYINT
	MOVEM A,TFYST

	IMUL B,TXDIM
	ADDM B,XDMP
	MOVE A,TXDIM	;DONT SUB 1 TO ADD TOLERANCE
	FLOTE A
	FMP A,TXDEN
	FAD A,TXINT
	MOVE B,XDIM(AR2A)
	SOJ B,
	FLOTE B
	FMP B,FXDEN(AR2A)
	FAD B,FXMIN(AR2A)
	CAMGE A,B
	STRT [SIXBIT /_XMAX TOO BIG, READPIC!/]

	MOVE A,TYDIM
	FLOTE A
	FMP A,TYDEN
	FAD A,TYINT
	MOVE B,YDIM(AR2A)
	SOJ B,
	FLOTE B
	FMP B,FYDEN(AR2A)
	FAD B,FYMIN(AR2A)
	CAMGE A,B
	STRT [SIXBIT /_YMAX TOO BIG, READPIC!/]
	SKIPE ERROR	;CHECK IF ERROR ROUTINE RUN
	JRST ENDNIL

	MOVSI A,377777
	MOVEM A,RBTOT
	PUSHJ P,REFIL	;FILL BUFFER

	MOVE A,XDMP
	JUMPLE A,STARE
	PUSHJ P,RBYT
	SOJG A,.-1
STARE:	MOVE A,YDIM(AR2A)
	MOVE AR1,TFYST
	MOVE C,FYMIN(AR2A)

NSCAN:	PUSH P,C	;LINE SCAN ROUTINE
	PUSH P,AR1
	PUSH P,A	;A MUST BE PUSHED LAST
	MOVE A,IMPT	;RECORD STARTING LINE INPUT FOR POSSIBLE YCOPY
	MOVEM A,LLINE
	MOVE A,TXDIM	;SAVE FOR LEND
	MOVEM A,XREAD
	MOVE A,XDIM(AR2A)
	MOVE AR1,TFXST
	MOVE C,FXMIN(AR2A)

STOLP:	MOVEI T,0
	PUSHJ P,RBYT
	;	AOJ T,	;MAKE LISP NO.
	IDPB T,IMPT
	SOJLE A,LEND
XAGN:	FAD C,FXDEN(AR2A)
	CAMG C,AR1
	JRST XCOPY
TXAGN:	FAD AR1,TFXDEN
	CAMG C,AR1
	JRST STOLP
	PUSHJ P,RBYT
	JRST TXAGN

XCOPY:	IDPB T,IMPT
	SOJG A,XAGN

LEND:	SOSG ,(P)	;CHECK FOR Y END
	JRST PALL	;YES, DONT FLUSH INTERLINE BYTES
	SKIPLE A,XREAD
	PUSHJ P,RBYT
	SOJG A,.-1
PALL:	POP P,A
	POP P,AR1
	POP P,C
	JUMPLE A,ENDER	;RECHECK FOR Y END
YAGN:	FAD C,FYDEN(AR2A)
	CAMGE C,AR1
	JRST YCOPY
TYAGN:	FAD AR1,TFYDEN
	CAMG C,AR1
	JRST NSCAN
	MOVE T,TXDIM	;SKIP A LINE
	MOVEM T,THROW
	PUSHJ P,RBYT
	SOSLE THROW
	JRST .-2
	JRST TYAGN

YCOPY:	MOVE T,XDIM(AR2A)
	MOVEM T,THROW
MORC:	ILDB T,LLINE
	IDPB T,IMPT
	SOSLE THROW
	JRST MORC
	SOJG A,YAGN
	JRST ENDER

THROW:	0
LLINE:	0
XREAD:	0
TFYST:	0	;FILL WITH STARTING Y LOC. + TYDEN/2.0
TFXST:	0	;FILL WITH STARTING X LOC. + TXDEN/2.0

TPARAM:	BLOCK 7
TLIGHT:	0
TVDFLG:	0
TBYTER:	0
TFYDEN:
TYDEN:	0
TFXDEN:
TXDEN:	0
TFYMIN:
TYMIN:	0
TFXMIN:
TXMIN:	0
TYDIM:	0
TXDIM:	0
VWRITE":	PUSH P,A
	PUSH P,B
	HLRZ A,(A)
	MOVEI B,CARRAY
	PUSHJ P,GET
	HRRZ A,(A)
	MOVE A,-1(A)
	ADD A,[-21,,-20]	;SKIPS EMPTY TOP OF HEADER.

IFN TS,	.IOT UTOC,A

IFE TS,[PARWR:	MOVE TAPAC,(A)
	PUSHJ P,.UWR"
	LERR [SIXBIT /TAPE FULL!/]
	AOBJN A,PARWR]

IFN 0,[	;DONT FLUSH JUST YET
	MOVE A,-30-3-1(A)	;GET AOBJN POINTER
	MOVEM A,WRTAOB
	MOVE B,[-2,,-1] ;TO ADD -1 TO BOTH HALVES
	ADDM B,(A)	;DECREMENT LISP NOS.
	AOBJN A,.-1
	MOVE A,WRTAOB

IFN TS,	.IOT UTOC,A

IFE TS,[TAPWL:	MOVE TAPAC,(A)
	PUSHJ P,.UWR"
	LERR [SIXBIT /TAPE FULL!/]
	AOBJN A,TAPWL]

	MOVE A,WRTAOB
	MOVE B,[1,,1]
	ADDM B,(A)	;BACK TO LISP NOS.
	AOBJN A,.-1
]
	POP P,B
	POP P,A
	HLRZ A,(A)
	POPJ P,

WRTAOB:	0

DESCRX":	HLLOS FXFLP	;RESULTS TO BE ALL FIXED
	SKIPA
DESCR":	HLLZS FXFLP
	MOVSI C,741400	;READ BITS LEFT TO RT, 1=FIX
	JUMPE T,DSCR1
	POP P,A
	AOJL T,QF2
	JUMPE A,CPOPJ
	MOVEI B,CARRAY
	PUSHJ P,GET	;NO, GET ADR OF ARRAY
	JUMPN A,DSCR1+1
	LERR [SIXBIT /NOT AN ARRAY, DESCR!/]
DSCR1:	MOVEI A,TPARAM+20-33
	ADDI A,33
	MOVEI AR2A,0	;NIL
	MOVE AR1,A
BLDUP:	MOVE A,-12(AR1)	;ADR = NO. OF ENTRIES + 1
	JUMPL C,FXIT
FXFLP:	TRNN P,0
	JRST FLTIT
	FMPR A,[4096.0]
	FADR A,[0.5]
	FICKS A
	MOVE A,B
FXIT:	SKIPA B,[FIXNUM]
FLTIT:	MOVEI B,FLONUM
	PUSHJ P,MAKNUM
	MOVE B,AR2A
	PUSHJ P,CONS
	MOVEM A,AR2A
	ADDI AR1,1
	LSH C,1
	JUMPN C,BLDUP
POPJJ:	POPJ P,
]

IFN VD,[
		;DISPLAY ROUTINES

DISHD":	MOVEI B,CARRAY
	PUSHJ P,GET
	HRRZ A,(A)
	MOVE A,-1(A)
	POPJ P,

DISINI":	PUSHJ P,DISHD
	MOVE C,[373737,,373737]
	SOS A
	PUSH A,[20136,,221700]
	PUSH A,[60000,,373737]
	MOVE B,A
	MOVEI C,1(A)
	AOBJN A,.-1
	HRLI B,220600
	MOVEM B,(A)
	MOVN B,VLINEL
	SETCAM B,VDISCNT
	MOVEI B,1
	MOVEM B,VDISLP
	JRST FALSE


DISAD":	MOVEI R,DISAD2
	SKIPN B
	TLO R,-1
	PUSHJ P,DISHD
	HLRO B,A
	SUBI A,1(B)
	HRRM A,DISAD3
	MOVE A,C
	MOVEI AR1,TRUTH
	PUSHJ P,PRINTA
	HRRZ A,AR1
	POPJ P,
DISCR:	PUSH P,A
	PUSHJ P,DISAD5
	POP P,A
DISAD2:	JUMPE AR1,CPOPJ
	SOSG VDISCNT
	JRST DISCR
	CAILE A,132
	CAILE A,137
	JRST DISAD6
	MOVEI D,-133(A)
	MOVEM D,DISDSV'
	MOVEI A,36
	PUSHJ P,DISAD4
	MOVE D,DISDSV
	IMULI D,6
	MOVE A,[525446515053]
	ROT A,(D)
	PUSHJ P,DISAD4
	MOVEI A,35
DISAD6:	CAIN A,15
DISAD5:	MOVEI A,34
DISAD4:	HRRZ D,@DISAD3
	AOS D
	CAIL D,@DISAD3
	TDZA AR1,AR1
DISAD3:	IDPB A,.
	CAIE A,34
	POPJ P,
	MOVE D,VLINEL
	MOVEM D,VDISCNT
	AOS VDISLP
	SOJA A,DISAD2

]

UREAD":	JUMPE A,.+5
	PUSH P,A
	PUSHJ P,CDDR
	PUSHJ P,UINIT
	POP P,A
	PUSHJ P,UFNAME
UREAD1:
IFE TS,[
	PUSHJ P,OPNRD"
	LERR [SIXBIT /FILE NOT FOUND!/]
]
IFN TS,[	MOVEI C,2
	HRLM C,UTIN
	MOVEM A,UTIN+1
	MOVEM B,UTIN+2
	TSOPEN UTIC,UTIN
	MOVE A,[440700,,UTIB+UTBSIZ]
	MOVEM A,UTIBP
	MOVEI A,UTIC
	.EOFC A,
	HRRM A,EOFC1
	DPB A,[350700,,UTIB+UTBSIZ]
	SETZM UTIOPD
]
UEXIT:	HRRZ A,CRUNIT
	POPJ P,A

UWRITE":	PUSHJ P,UINIT
IFE TS,[
	PUSHJ P,OPNWR"
	LERR [SIXBITCH /TAPE FULL!/]
	JRST FIX1A
]
IFN TS,[
	HRLS CRUNIT
	MOVEI C,3
	HRLM C,UTIN
WRITRS:	MOVE A,[SIXBIT /.LISP./]
	MOVE B,[SIXBIT /OUTPUT/]
	MOVEM A,UTIN+1
	MOVEM B,UTIN+2
	TSOPEN UTOC,UTIN
	PUSHJ P,UTOINT
	MOVE A,UTIN
	MOVEM A,UWRT
	JRST UEXIT

UTIOPD:	-1
UTIN":	(SIXBIT /DSK/)
	BLOCK 4
UWRT":	0
]


IFE TS,[
UFILE":	PUSHJ P,UFNAME
	SETZM TAPWRT
	PUSHJ P,.FILE"
	JRST FALSE
	LDB A,[270400,,WBYTS"]
	AOS A
	HRRM A,CRUNIT
	POPJ P,
]
IFN TS,[
UFILE":	PUSHJ P,UFNAME
	SETZM TAPWRT
	MOVE C,UWRT
	MOVEM C,UTIN
	SETZM UTIN+1
	MOVEI C,4
	MOVEM C,UTIN+2
	MOVEM A,UTIN+3
	MOVEM B,UTIN+4
	.FDELE UTIN
	LERR [SIXBITCH /FILE RENAME LOST!/]
	MOVEI A,^C
	PUSHJ P,UTTYO
	MOVEI A,^L
	PUSHJ P,UTTYO
	MOVE A,UTOBP
	MOVEI B,^C
	IDPB B,A
	TLNE A,740000
	JRST .-2
	HRLZS A
	MOVSI B,UTOB-1
	SUB B,A
	HRRI B,UTOB
	.IOT UTOC,B
	EXCH NIL,CLOSS
	.CLOSE UTOC,
	EXCH NIL,CLOSS
	HLRZ A,CRUNIT
	POPJ P,

CLOSS:	014060301406
]

UKILL":	PUSHJ P,UINIT
IFE TS, PUSHJ P,TAPKIL"
IFN TS,[LDB A,[000400,,UTIN]
	.UDISMT A,
	LERR [SIXBITCH /UNF?!/]
]
	JRST FIX1A

UINIT":	SKIPN A
	HRRZ A,CRUNIT
UINIT1":	HRRM A,CRUNIT
	HLRZ A,(A)
IFE TS,[	PUSHJ P,NUMVAL
	PUSHJ P,FILEST"
	LERR [SIXBIT /TOO MANY FILE DIRECTORIES!/]
	POPJ P,
]
IFN TS,[	MOVE C,A
	PUSHJ P,NUMBERP
	JUMPE A,UINIT3
	MOVE A,C
	PUSHJ P,NUMVAL
	ADDI A,20
	DPB A,[600,,UTIN]
	JRST UINIT2
UINIT3:	HLRZ A,C
	PUSHJ P,SIXMAK
	HLRM A,UTIN
UINIT2:	HRRZ A,CRUNIT
	HRRZ A,(A)
	JUMPE A,CPOPJ
	HLRZ A,(A)
	PUSHJ P,SIXMAK
	CAME A,USN
	.WSNAME A,
	MOVEM A,USN
	POPJ P,

USN:	0	;USER SYSTEM NAME
]


UFNAME":	JUMPE A,UFNM
	PUSH P,A
	HLRZ A,(A)
	PUSHJ P,SIXMAK
	MOVEM A,UFN1
	EXCH A,(P)
	PUSHJ P,CADR
	PUSHJ P,SIXMAK
	MOVEM A,UFN2
	MOVE B,A
	JRST POPAJ
UFNM:	MOVE A,UFN1
	MOVE B,UFN2
	POPJ P,
UFN1:	0
UFN2:	0

SIXMAK":	MOVSI C,(SIXBIT /@/)
	MOVEM C,SIXMK2
	MOVE AR1,[440600,,SIXMK2]
	HRROI R,SIXMK1
	PUSHJ P,PRINTA
	MOVE A,SIXMK2
	POPJ P,
SIXMK1:	ADDI A,40
	CAIN A,77
	MOVEI A,0
	TLNE AR1,770000
	IDPB A,AR1
	POPJ P,
SIXMK2:	SIXBIT /@/
;BEGIN INTERPRETER ROUTINES

CADDDR":	SKIPA A,(A)
CADDAR":	HLRZ A,(A)
CADDR":	SKIPA A,(A)
CADAR":	HLRZ A,(A)
CADR":	SKIPA A,(A)
CAAR":	HLRZ A,(A)
QUOTE:
CAR":	HLRZ A,(A)
	POPJ P,

CDDDDR":	SKIPA A,(A)
CDDDAR":	HLRZ A,(A)
CDDDR":	SKIPA A,(A)
CDDAR":	HLRZ A,(A)
CDDR":	SKIPA A,(A)
CDAR":	HLRZ A,(A)
CDR":	HRRZ A,(A)
	POPJ P,

CAADDR":	SKIPA A,(A)
CAADAR":	HLRZ A,(A)
CAADR":	SKIPA A,(A)
CAAAR":	HLRZ A,(A)
	JRST CAAR

CDADDR":	SKIPA A,(A)
CDADAR":	HLRZ A,(A)
CDADR":	SKIPA A,(A)
CDAAR":	HLRZ A,(A)
	JRST CDAR

CAAADR":	SKIPA A,(A)
CAAAAR":	HLRZ A,(A)
	JRST CAAAR

CDDADR":	SKIPA A,(A)
CDDAAR":	HLRZ A,(A)
	JRST CDDAR

CDAADR":	SKIPA A,(A)
CDAAAR":	HLRZ A,(A)
	JRST CDAAR

CADADR":	SKIPA A,(A)
CADAAR":	HLRZ A,(A)
	JRST CADAR

RPLACA":	HRLM B,(A)
	POPJ P,

RPLACD":	HRRM B,(A)
	POPJ P,

PNGNK1: PUSHJ P,NCONS
	MOVEI B,PNAME
	PUSHJ P,XCONS
ACONS:	TROA B,-1
NCONS":	TRZA B,-1
XCONS":	EXCH B,A
CONS":	HRL B,A
AGC2:	SKIPN A,F
	JRST AGC1
	MOVE F,(F)
	MOVEM B,(A)
	POPJ P,
AGC1:	HLR A,B
	PUSHJ P,AGC
	JRST AGC2

PATOM":	CAIL A,FS	;FALSE ONLY FOR NON-ATOMIC
	CAIL A,@GCP1	;FREE-STORAGE POINTERS.
	JRST TRUE
	JRST ATOM1
ATOM":	CAIG A,INUM	;USES ONLY ACC A
	JRST TRUE
	CAIL A,FS
	CAIL A,@GCP1
	JRST FALSE
ATOM1:	HLLE A,(A)
	AOSE A

NOT":
ANULL":	JUMPN A,FALSE
TRUE":	MOVEI A,TRUTH
	POPJ P,

EQ":	CAMN A,B
	JRST,TRUE
	JRST FALSE

PROG2": CAMLE T,[-2]
	JRST QF3
	HRLI T,-1(T)
	ADD P,T
	MOVE A,2(P)
	POPJ P,


SASSOC":	PUSHJ P,SAS1
	JCALLF 0,(C)
	POPJ P,

SAS0:	HRRZ B,(B)
SAS1:	JUMPE B,CPOPJ
	MOVS T,(B)
	MOVS TT,(T)
	CAIE A,(TT)
	JRST,SAS0
	HRRZ A,T
CPOPJ1":	AOS,(P)
	POPJ P,

ASSOC":	PUSHJ P,SAS1
FALSE":	MOVEI A,0
CPOPJ":	POPJ P,

LAST":	HRRZ B,(A)
	CAIG B,INUM
	POPJ P,
	HLLE B,(B)
	AOJE B,CPOPJ
	HRRZ A,(A)
	JRST LAST+1

REVERSE":	MOVEI B,0
	JUMPE A,SPROG2
	HRRZ T,(A)
	HLRZ A,(A)
	PUSHJ P,CONS
	MOVE B,A
	MOVE A,T
	JRST REVERSE+1

LENGTH2:	MOVEI B,0
LNGTH1:	CAIG A,INUM
	JRST FIX1
	HLLE C,(A)
	AOJE C,FIX1
	HRRZ A,(A)
	AOJA B,LNGTH1

FLATSIZE":	SKIPA R,CFLAT2
FLATC":	HRROI R,FLAT2
	SETZM FLAT1
	PUSHJ P,PRINTA
FLAT1:	MOVEI A,.
	JRST FIX1A
FLAT2:	AOS FLAT1
CFLAT2:	POPJ P,FLAT2

GET0:	HRRZ A,(D)
GET":	HRRZ D,(A)
	JUMPE D,FALSE
	HLRZ A,(D)
	CAME A,B
	JRST GET0
	HRRZ D,(D)
	HLRZ A,(D)
	POPJ P,

GETL0:	HRRZ A,(T)
GETL":	HRRZ T,(A)
	JUMPE T,FALSE
	HLRZ A,(T)
	MOVE C,B
GETL1:	JUMPE C,GETL0
	HLRZ TT,(C)
	CAMN TT,A
	JRST GETL2
	HRRZ C,(C)
	JRST GETL1
GETL2:	MOVE A,T
	POPJ P,

PUTPROP":	HLRZ T,(A)	;ATOM,VALUE,INDICATOR
	CAIE T,-1
	LERR [SIXBITCH /1ST ARG NONATOM - PUTPROP!/]
	MOVE T,A
CSET3:	HRRZ A,(A)
	JUMPE A,CSET2
	HLRZ TT,(A)
	HRRZ A,(A)
	CAIE TT,(C)
	JRST CSET3
	CAIE C,VALUE
	JRST CSET1
	HRRZ T,(B)
	HLRZ A,(A)
	HRRM T,(A)
	JRST SPROG2
CSET1:	HRLM B,(A)
SPROG2:	MOVE A,B
	POPJ P,
CSET2:	HRRZ A,(T)
	PUSHJ P,XCONS
	HRRZ B,C
	PUSHJ P,XCONS
	HRRM A,(T)
	JRST CADR


REMPROP":	HRRZ T,(A)
	JUMPE T,FALSE
	MOVS TT,(T)
	CAIN B,(TT)
	JRA TT,REMP1
	HRRZ A,(T)
	JRST REMPROP
REMP1:	HRRM TT,(A)
	JRST TRUE

DEFPROP:	JUMPE A,DFPER
	HRRZ B,(A)
	JUMPE B,DFPER
	HRRZ C,(B)
	JUMPE C,DFPER
	HRRZ TT,(C)
	JUMPN TT,DFPER
	HLRZ A,(A)
	HLRZ B,(B)
	HLRZ C,(C)
PTPRP1:	PUSH P,A
	PUSHJ P,PUTPROP
	JRST POPAJ
DFPER:	MOVE B,A
	HLRZ A,(B)
	MOVEI C,QM
	PUSHJ P,PUTPROP
	JRST QF4
PA3:	0	;LH=0=>RH =NEXT PROG STATEMENT
	;LH - =>RH = TAG TO GO TO
PA4:	0	;LH=-1,RH=PNTR TO PROG LESS BOUND VAR LIST
	;LH=+,RH RETURN VALUE
	;2,1=>DONT DO UNBND

PROG":	PUSH P,PA3
	PUSH P,PA4
	HLRZ TT,(A)
	HRRZ A,(A)
	HRROM A,PA4
	MOVEM A,PA3
	JUMPE TT,PG0
	MOVSI C,1
	MOVEI B,VALUE
	MOVEM SP,SPSV
	ANDCAM C,PA4

PG7A:	HLRZ A,(TT)
	MOVEI AR1,0
	PUSHJ P,BIND
	HRRZ TT,(TT)
	JUMPN TT,PG7A
	PUSH SP,SPSV

PG0:	SFX
	SKIPA T,PA3
PG5A:	MOVE T,A
PG1:	JUMPE T,PG2
	HLRZ A,(T)
	HRRZ T,(T)
	HLLE B,(A)
	AOJE B,PG1
	MOVEM T,PA3
	PUSHJ P,EVAL
	SKIPL A,PA4
	JRST PG4	;RETURN
PG1A:	SKIPL T,PA3
	JRST PG1
PG5:	JUMPE A,EG1
PG5B:	HLRZ TT,(A)
	HRRZ A,(A)
	CAIN TT,(T)	;FOUND TAG
	JRST PG5A
	JRST PG5


PG2:	TDZA A,A
PG4:	HRRZS A
	MOVSI B,1
	TDNN B,PA4
	PUSHJ P,UNBIND
ERRP4:	POP P,PA4
	POP P,PA3
	POPJ P,

GO":	HLRZ A,(A)
	HRROM A,PA3
	HLLE B,(A)
	AOJE B,FALSE
	PUSHJ P,EVAL
	JRST GO+1

RETURN":	TLO A,-1
	MOVEM A,DO4
	HLL A,PA4
	TLZ A,-2
	MOVEM A,PA4
	POPJ P,

SETQ":	HLRZ B,(A)
	PUSH P,B
	PUSHJ P,CADR
	PUSHJ P,EVAL
	MOVE B,A
	POP P,A
SET":	MOVE AR1,B
	PUSHJ P,BIND
	SUB SP,[(1)1]
	SFX
	MOVE A,AR1
	POPJ P,

DO:	PUSH P,DO4
	SETZM DO4
	HRRZ C,(A)
	HLRZ A,(A)
	PUSH P,A	;INDEX VARIABLE
	PUSHJ P,ATOM
	SKIPN A
	LERR [SIXBIT /INDEX NON-ATOMIC - DO!/]
	HLRZ A,(C)
	HRRZ C,(C)
	HLRZ B,(C)
	PUSH P,B	;INCREMENT EXPRESSION
	HRRZ C,(C)
	HLRZ B,(C)
	PUSH P,B	;END-TEST EXPRESSION
	HRRZ C,(C)
	PUSH P,C
	MOVEM C,DO3	;SEQUENCE FOR EVAL
	PUSHJ P,EVAL
	MOVE AR1,A
	MOVE A,-3(P)
	PUSHJ P,BIND	;SETQ INITIAL INDEX VALUE
	SUB SP,[1,,1]
	SFX
	MOVEM A,-3(P)
DOLOOP:	SKIPE DO4
	JRST POP4X
	MOVE A,-1(P)
	PUSHJ P,EVAL
	JUMPN A,POP4X	;PERFORM END-TEST
DO1:	SKIPN A,DO3
	JRST DO2
	HRRZ B,(A)
	MOVEM B,(P)
	HLRZ A,(A)
	PUSHJ P,EVAL	;PERFORM SEQUENCE STATEMENTS
	JRST DO1
DO2:	MOVE A,-2(P)
	PUSHJ P,EVAL
	HRRZM A,@-3(P)	;INCREMENT INDEX AND LOOP
	JRST DOLOOP
POP4X:	HRRZ A,DO4
	SUB P,[4,,4]
	POP P,DO4
	POPJ P,

CON2:	HRRZ A,(T)
COND":	JUMPE A,CPOPJ	;ENTRY
	PUSH P,A
	HLRZ A,(A)
	HLRZ A,(A)
	CAIE A,TRUTH
	PUSHJ P,EVAL
	POP P,T
	JUMPE A,CON2
	HLRZ T,(T)
COND2:	HRRZ T,(T)
	JUMPE T,CPOPJ
	PUSH P,T
	HLRZ A,(T)
	PUSHJ P,EVAL
CPOPT:	POP P,T
	JRST COND2

IRPS OP,,DIF QUO,ERATOR,,FERENCE TIENT,FIX,,SUB IDIV,FLO,,FSB FDV
.!OP":	PUSH P,A
	PUSH P,B
	MOVNI T,2
OP!ERATOR":
	MOVE R,[JRST OP!2]
	MOVE C,R
	SOJA C,OP!1

	SKIPA C,[FLO!R TT,A]
OP!2:	MOVE C,[FIX TT,A]
	MOVEM C,PLUS3
	MOVE C,[FLO!R TT,A]
	MOVEM C,PLUS6
	MOVE TT,A
	JRST PLUS4
TERMIN

.PLUS":	PUSH P,A
	PUSH P,B
	MOVNI T,2
PLUS":	MOVE R,[ADD TT,A]
	MOVE C,[FADR TT,A]
DIF1:	MOVEI TT,0
PLUS1:	MOVEM R,PLUS3
	MOVNM T,PLUS8
	ADD T,P
	MOVEI R,FIXNUM
PLUS7:	MOVEM C,PLUS6
	HRLS PLUS8
	JRST PLUS4

PLUS5:	MOVE R,PLUS6	;FAD TT,A OR FMP TT,A
	MOVEM R,PLUS3
	MOVEI R,FLONUM
	EXCH TT,A
	PUSHJ P,FLOAT
	EXCH TT,A	;OR FAD TT,A OR IMUL TT,A
PLUS3:	ADD TT,A	;OR FMP TT,A

PLUS4:	CAMN T,P
	JRST PLUS2
PLUS9:	MOVE A,1(T)
	PUSHJ P,NUMVAL
	CAMN B,R
	AOJA T,PLUS3
	CAIE R,FLONUM
	AOJA T,PLUS5
	PUSHJ P,FLOAT
	AOJA T,PLUS3

PLUS2:	MOVE A,TT
	MOVE B,R
	SUB P,PLUS8
	JRST,MAKNUM

.TIMES":	PUSH P,A
	PUSH P,B
	MOVNI T,2
TIMES":	MOVE R,[IMUL TT,A]
	MOVE C,[FMPR TT,A]
QUO1:	MOVEI TT,1
	JRST,PLUS1		;FLOATING VERSION OF PLUS3

PLUS6":	0
PLUS8":	0

NUMVAL":	MOVEM A,NUMV1
	JUMPE A,NUMV2
	CAIG A,INUM
	SOJA A,NUMAG1
NUMV3:	CAIL A,.	;FIRST OF FWS.
	JRST NUMV4
	HRRZ A,(A)
	HLRZ B,(A)
	HRRZ A,(A)
	CAIE B,FIXNUM
	CAIN B,FLONUM
	SKIPA A,(A)
NUMV2:	SKIPA A,NUMV1
	POPJ P,
	PUSHJ P,EPRINT
	JRST E5
NUMV4:	MOVEI B,FIXNUM
	CAIGE A,@GCP
	JRST NUMV2-1
	JRST NUMV2
NUMV1:	0

NUMAG1:	MOVEI B,FIXNUM
	POPJ P,

MINUS":	PUSHJ P,NUMVAL
	MOVNS A

MAKNUM":	CAIE B,FIXNUM"
	JRST MAKNM1
MAKNM3:	JUMPL A,MAKNM1
MAKNM2:	CAIGE A,INUM
	AOJA A,CPOPJ
MAKNM1:	PUSHJ P,FWCONS
	JRST ACONS-1

FW0CNS:	MOVEI A,0
FWCONS":	JUMPN FF,FWC1
	EXCH A,FWC0
	PUSHJ P,AGC
	EXCH A,FWC0
FWC1:	EXCH A,(FF)
	EXCH A,FF
	POPJ P,
FWC0:	0


IFLOAT":	PUSH P,A
	PUSHJ P,FLOATP
	JUMPN A,POPAJ
	POP P,A
	PUSHJ P,NUMVAL
	PUSHJ P,FLOAT
FLOAT1:	MOVEI B,FLONUM
	JRST MAKNM1
FLOAT":	IDIVI A,400000
	SKIPE A
	TLC A,254000
	TLC B,233000
	FAD A,B
	POPJ P,

FIX":	PUSHJ P,NUMVAL
	CAIE B,FLONUM
	JRST FIX1A
FIX2":	MULI A,400
	TSC A,A
	ASH B,-243(A)
FIX1:	MOVE A,B
FIX1A":	MOVEI B,FIXNUM
	JRST MAKNM3

ADD1":	CAIG A,INUM
	JUMPN A,FIX1A
	PUSHJ P,NUMVAL
	CAIN B,FIXNUM
	AOJA A,MAKNUM
	FAD A,[1.0]
	JRST MAKNUM

SUB1":	CAIG A,INUM
	JUMPN A,SUB1A
	PUSHJ P,NUMVAL
	CAIN B,FIXNUM
	SOJA A,MAKNUM
	FSB A,[1.0]
	JRST MAKNUM
SUB1A:	SUBI A,2
	JRST MAKNM3


MAX":	SKIPA A,[CAMGE TT,]
MIN":	MOVSI A,(CAMLE TT,)
	HLLM A,MINMAX
	MOVNM T,PLUS8
	AOJG T,QF3
	MOVE C,[JRST MINMAX]
	JRST MNMX1

MINMAX:	CAMLE\CAMGE TT,A
	MOVE TT,A
	JRST PLUS4

REMAINDER":	PUSH P,B
	PUSHJ P,NUMVAL
	EXCH A,(P)
	PUSHJ P,NUMVAL
	POP P,B
	IDIV B,A
RMDR6:	MOVE A,C
	JRST FIX1A

ABS":	PUSHJ P,NUMVAL
	MOVMS A
	JRST MAKNUM

LSH:	PUSH P,B
	PUSHJ P,NUMVAL
	EXCH A,(P)
	PUSHJ P,NUMVAL
	POP P,B
	LSH B,(A)
	JRST FIX1

BOOLE":	MOVE TT,T
	ADDI TT,2(P)
	SOS A,-1(TT)
	DPB A,C1BOOL
	PUSHJ P,BOOLG
	MOVE C,A
BOOLL:	PUSHJ P,BOOLG
BOOLI:	CLEARB C,A
	JRST,BOOLL
BOOLG:	CAIL TT,(P)
	JRST BOOL1
	MOVE A,(TT)
	PUSHJ P,NUMVAL
	AOJA TT,CPOPJ
BOOL1:	HRLI T,-1(T)
	ADD P,T
	POP P,B
	JRST FIX1A
C1BOOL:	350400,,BOOLI

EXAMINE":	PUSHJ P,NUMVAL
	MOVE A,(A)
	JRST MAKNUM

DEPOSIT":	PUSH P,B
	PUSHJ P,NUMVAL
	MOVE C,A
	POP P,A
	PUSHJ P,NUMVAL
	MOVEM A,(C)
	JRST FALSE


NUMBERP":	CAIG A,INUM
	JUMPN A,TRUE
	HLLE T,(A)
	AOJN T,FALSE
	HRRZ B,(A)
	HLRZ B,(B)
	CAIE B,FIXNUM
	CAIN B,FLONUM
	JRST TRUE
	JRST FALSE

ZEROP":	PUSHJ P,NUMVAL
	JRST ANULL

MINUSP":	PUSHJ P,NUMVAL
	JUMPGE A,FALSE
	JRST TRUE

FIXP":	SKIPA C,[FIXNUM]
FLOATP":	MOVEI C,FLONUM
	MOVEI B,FIXNUM
	PUSHJ P,NUMBERP
	JUMPE A,FPXT
	CAIE B,(C)
	MOVEI A,0
FPXT:	POPJ P,

SMLNM:	CAILE A,INUM
	MOVEI A,0
	POPJ P,

.GREAT":	EXCH A,B
.LESS":	PUSH P,A
	PUSH P,B
	MOVNI T,2
LESSP":	SKIPA A,[CAML TT,]
GREATERP":
	MOVSI A,(CAMG TT,)
	HLLM A,GRESS
	MOVE C,[JRST GRESS]
	MOVNM T,PLUS8
	AOJGE T,QF3
MNMX1:	MOVEM C,PLUS3
	ADD T,P
	MOVE A,(T)
	PUSHJ P,NUMVAL
	MOVE TT,A
	MOVE R,B
	JRST PLUS7

GRESS:	CAML\CAMG TT,A
	JRST GRUSE
	MOVE TT,A
	CAME T,P
	JRST PLUS9
	TDZA A,A
GRUSE:	MOVEI A,FALSE-TRUE
	SUB P,PLUS8
	JRST TRUE(A)


EQUAL":	MOVEM P,EQLP'
EQUAL1:	CAMN A,B
	JRST,TRUE
	MOVE T,A
	MOVE TT,B
	PUSHJ P,PATOM
	EXCH A,B
	PUSHJ P,PATOM
	CAMN A,B
	JRST EQUAL3
EQUAL4:	MOVE P,EQLP
	JRST,FALSE

EQUAL3:	JUMPN A,EQUAL5
	PUSH P,T
	PUSH P,TT
	HLRZ A,(T)
	HLRZ B,(TT)
	PUSHJ P,EQUAL1
	JUMPE A,EQUAL4
	POP P,B
	POP P,A
	HRRZ A,(A)
	HRRZ B,(B)
	JRST,EQUAL1

EQUAL5:	CAIL T,FS	;PATOMS WHICH ARE NOT "EQ" COME HERE.
	CAIGE TT,FS
	JRST FALSE
	CAIGE T,@GCP
	CAIL TT,@GCP
	JRST FALSE
	CAIGE T,@GCP1
	CAIL TT,@GCP1
	JRST EQ3
	HRRZ T,(T)
	HLRZ A,(T)
	HRRZ T,(T)
	HRRZ TT,(TT)
	HLRZ B,(TT)
	HRRZ TT,(TT)
	CAIE A,FIXNUM	;FS POINTERS ARE "EQUAL" IF THEY
	CAIN A,FLONUM	;ARE EQUAL LISP NUMBERS.
	CAME A,B
	JRST FALSE
EQ4:	MOVE A,(T)
	CAMN A,(TT)
	JRST TRUE
	JRST FALSE
EQ3:	CAIL T,@GCP1	;FWS POINTERS ARE "EQUAL"
	CAIGE TT,@GCP1	;IF THEIR CONTENTS ARE EQUAL.
	JRST FALSE
	JRST EQ4
SUBST":	SKIPA AR1,A
SUBS0A:	SKIPA A,AR1
	SKIPA AR2A,B
	MOVE B,AR2A
	PUSH P,C
	MOVE A,C
	PUSHJ P,EQUAL
	POP P,C
	JUMPN A,SUBS4
SUBS1:	MOVE A,C
	PUSHJ P,PATOM
	JUMPE A,SUBS2
EV6A:	MOVE A,C
	POPJ P,
SUBS2:	PUSH P,C
	HLRZ C,(C)
	PUSHJ P,SUBS0A
	EXCH A,(P)
	HRRZ C,(A)
	PUSHJ P,SUBS0A
SUBS3:	POP P,B
	JRST XCONS
SUBS4:	HRRZ A,AR1
	POPJ P,

LIST":	MOVEI A,NIL
LIST1:	JUMPE T,CPOPJ
	POP P,B
	PUSHJ P,XCONS
	AOJA T,LIST1
NCONC":	TDZA R,R
APPEND":	MOVEI R,.APPEND-.NCONC
	JUMPE T,FALSE
	POP P,B
APP2:	AOJE T,SPROG2
	POP P,A
	PUSHJ P,.NCONC(R)
	MOVE B,A
	JRST APP2
.NCONC":JUMPE A,SPROG2
	MOVE TT,A
	MOVE C,TT
	HRRZ TT,(C)
	JUMPN TT,.-2
	HRRM B,(C)
	POPJ P,
.APPEND":JUMPE A,SPROG2
	MOVEI C,AR1
	MOVE TT,A
APP1:	HLRZ A,(TT)
	PUSHJ P,CONS	;SAVES B
	HRRM A,(C)
	MOVE C,A
	HRRZ TT,(TT)
	JUMPN TT,APP1
	JRST SUBS4


MEMBER":	SETZM MEMV'	;USES A,B,AR1,AR2A,T,TT
	MOVE AR1,A
	MOVE AR2A,B
	PUSHJ P,PATOM
	JUMPN A,MEMQA
MEMB1:	CAIL AR2A,FS
	CAIL B,@GCP1
	JRST FALSE
	MOVE A,AR1
	HLRZ B,(AR2A)
	PUSHJ P,EQUAL
	JUMPN A,MEMB2		;TRUE
	HRRM AR2A,MEMV
	HRRZ AR2A,(AR2A)
	JRST,MEMB1
MEMB2:	MOVE A,AR2A
	POPJ P,

MEMQA:	MOVE A,AR1
MEMQ":	SETZM MEMV	;USES A,B,AR1
	JRST MEMQ2
MEMQ1:	HRRM B,MEMV
	HRRZ B,(B)
MEMQ2:	CAIL B,FS
	CAIL B,@GCP1
	JRST FALSE
	HLRZ AR1,(B)
	CAME A,AR1
	JRST MEMQ1
	MOVE A,B
	POPJ P,

DELQ":	SKIPA C,[MEMQ]	;USES A,B,C,AR1,T
DELETE":MOVEI C,MEMBER	;USES A,B,C,AR1,AR2A,T,TT
	MOVEI A,-1
	CAME T,[-3]
	JRST .+3
	POP P,A
	PUSHJ P,NUMVAL
	MOVEM A,DLTC'
	SKIPA B,(P)
DLT2:	MOVEM B,(P)
DLT3:	MOVE A,-1(P)
	SOSGE DLTC
	JRST DLT1
	PUSHJ P,(C)
	JUMPE A,DLT1
	HRRZ B,(A)
	SKIPN AR1,MEMV
	JRST DLT2
	HRRM B,(AR1)
	JRST DLT3
DLT1:	POP P,A
POP1J:	SUB P,[1,,1]
	POPJ P,

AAND":	HRLI A,TRUTH
OR:	HLRZ C,A
	PUSH P,C
ANDOR:	HRRZ C,A
	JUMPE C,POPAJ
	MOVSI C,(SKIPE (P))
	TLNE A,-1
	MOVSI C,(SKIPN (P))
	XCT C
	JRST POPAJ
	MOVEM A,(P)
	HLRZ A,(A)
	PUSHJ P,EVAL
	EXCH A,(P)
	HRR A,(A)
	JRST ANDOR

GENSYM":	MOVEI B,GNUM
	HRLI B,10700
	MOVNI C,4
	MOVEI TT,60

GENSY2:	LDB T,B
	AOS T
	DPB T,B
	CAIG T,71
	JRST,GENSY1
	DPB TT,B
	ADD B,[070000,,0]
	AOJN C,GENSY2

GENSY1:	MOVE A,GNUM
	PUSHJ P,FWCONS
	PUSHJ P,NCONS
	JRST PNGNK1

GNUM:	ASCII /G0000/

;PDL STRUCTURE
;<CAR OR NOP>,,RETURN
;.,,NIL OR LIST1	;EVENTUAL VALUE
;LIST1		;2ND ARG
;LIST2		;3RD ARG
;.
;.
;LISTN		
;-N,,FCN	;1ST ARG
MAPC":	TLOA A,(@)
MAP":	TLOA A,(@)
MAPCAR":	TLO A,(HRRZM A,(P))
MAPLIST":	TLO A,(JFCL)
	AOJGE T,QF3
	MOVE AR1,T
	ADD AR1,P
	TLZE A,(@)	;NO CONS FLAG
	SKIPA B,1(AR1)
	HRLZ B,AR1
	EXCH B,(AR1)	;EVENTUAL VALUE _ FIRST LIST OR CONSAGE
	HRL B,T
	PUSH P,B
	HLLM A,-1(AR1)
CMAPL6:	AOJ AR1,MAPL6
MAPL2:	MOVNM P,C
	HLLZ B,-2(AR1)
	PUSH P,CMAPL6
MAPL21:	SKIPGE A,(AR1)
	JCALLF 16,(A)
	CAIG A,INUM
	JRST MAPL4
	PUSH P,A
	HLRE A,(A)
MAPL69:	XCT B
	AOJE A,MAPL4
	AOJA AR1,MAPL21

MAPL6:	HLRE T,(P)
	MOVE AR1,T
	ADDI AR1,(P)
	MOVEM AR1,MAPL5
	HLRZ C,-1(AR1)
	JUMPE C,MAPL7
	PUSHJ P,NCONS
	HRRM A,(C)
	HRLM A,-1(AR1)
MAPL7:	HRRZ A,@(AR1)
	MOVEM A,(AR1)
	SKIPL 1(AR1)
	AOJA AR1,MAPL7
MAPL5:	MOVE AR1,.
	JRST MAPL2

MAPL4:	SETCM P,C
	HRLI T,-2(T)
	ADD P,T
	POP P,AR1
	JRST SUBS4

;INTERPRETER  PART 3

EV3:	SKIPE EXTEVL
	PUSHJ P,@EXTEVL
	HLRZ A,(AR1)
	MOVEI B,VALUE
	PUSHJ P,GET
	JUMPE A,QA2A	;FUNCTION OBJECT HAS NO DEFINITION
	HRRZ A,(A)
	HRRZ B,(AR1)
	CAIE A,UNBOUND
	CAIN A,(B)
	JRST QA2A
EV4:	PUSHJ P,CONS	;EVAL (CONS (CDR A)(CDR AR1))
	JRST EVAL
EXTEVL:	0	;ALLOW NON-STANDARD FORMS

OEVAL":	AOJN T,AEVAL
	POP P,A
EVAL":
EV0:	HRRZM A,AR1
	CAIG A,INUM
	JUMPN A,CPOPJ
	HLRZ T,(A)
	CAIN T,-1
	JRST,EE1		;X IS ATOMIC
	CAIG T,INUM
	JUMPN A,QA2A
	HLRE TT,(T)
	AOJE TT,EE2		;CAR (X) IS ATOMIC
	CAIN TT,LAMBDA+1	;REQUIRES LAMBDA, LABEL, FUNARG < 400000
	JRST EXP3
	CAIE TT,FUNARG+1
	CAIN TT,LABEL+1
	JRST EXP3
	PUSH P,(A)
	MOVE A,T
	PUSHJ P,EVAL
	POP P,B
	JRST EV4

EE1:
EV5:	HRRZ AR1,(AR1)
	JUMPE AR1,QA8
	HLRZ TT,(AR1)
	CAIE TT,FLONUM
	CAIN TT,FIXNUM
	POPJ P,
	HRRZ AR1,(AR1)
	CAIE TT,VALUE
	JRST EV5
	HLRZ AR1,(AR1)
	HRRZ AR1,(AR1)
	CAIN AR1,UNBOUND
	JRST QA8
	MOVEM AR1,A
	POPJ P,


ALIST:	SKIPE  A,-1(P)
	PUSHJ P,NUMBERP
	MOVEM SP,SPSV
	JUMPN A,AEVAL7
	MOVE C,SC2
	MOVEM C,AEVAL5
	SETOM AEVAL2
AEVAL8:	MOVE C,SP
AEVAL6:	CAMN C,AEVAL5
	JRST AEVAL1
	POP C,T
AEVAL4:	CAMN C,T
	JRST AEVAL6	;THRU WITH BLOCK
	POP C,AR1
	MOVSS AR1
	PUSH SP,(AR1)
	HLRZM AR1,(AR1)
	HRLM AR1,(SP)
	JRST AEVAL4

AEVAL5:	0


AEVAL:	PUSHJ P,ALIST
	POP P,A
	MOVEI A,UNBIND
	EXCH A,(P)
	JRST EVAL

AEVAL1:	SKIPGE AEVAL2
	SKIPN B,-1(P)
	JRST ABIND3

ABIND1:	MOVEM SP,SPSV
ABIND2:	MOVE A,B
	HRRZ B,(A)
	HLRZ A,(A)
	HRRZ AR1,(A)
	HLRZ A,(A)
	PUSHJ P,BIND
	JUMPN B,ABIND2
ABIND3:	PUSH SP,SPSV
	SFX
	POPJ P,

AEVAL7:	MOVE A,-1(P)
	PUSHJ P,NUMVAL
	CLEARM AEVAL2
	MOVEM A,AEVAL5
	JRST AEVAL8

AEVAL2:	0

EE2:	HRRZ T,(T)
	JUMPE T,EV3
	HLRZ TT,(T)
	HRRZ T,(T)
	CAIN TT,SUBR
	JRST,ESB
	CAIN TT,LSUBR
	JRST EELS
	CAIN TT,EXPR
	JRST,AEXP
	CAIN TT,FSUBR
	JRST,EFS
	CAIE TT,MACRO
	JRST,EFM
	CAIN TT,CARRAY
	JRST EAR
	CAIN TT,FEXPR
	JRST EE2

EFX:	HLRZ T,(T)
	HLL T,(AR1)
	PUSH P,T
	HRRZ A,(A)
	TLO A,400000
	PUSH P,A
	MOVNI T,1
	JRST IAPPLY

AEXP:	HLRZ T,(T)
	HLL T,(AR1)
EXP3:	PUSH P,T
EXP1:	HRRZ A,(AR1)
CILIST:	JSP TT,ILIST
EXP2:	JRST IAPPLY

EFS:	HLRZ T,(T)
	HRRZ A,(AR1)
	JRST (T)

EAR:	HLRZ TT,(T)
	HRRZ T,(TT)
	HRRZ A,(AR1)
	JRST UUOS2+1
ESB:	HRRZ A,(AR1)
UUOS2:	HLRZ T,(T)
	HLL T,(AR1)
	PUSH P,T
	JSP TT,ILIST
ESB1:	JRST .+NACS+1(T)
REPEAT NACS-1,POP P,A+NACS-1-.RPCNT
POPAJ":	POP P,A
	POPJ P,

EFM:	HLRZ T,(T)
	CALLF 1,(T)
	JRST EVAL
EELS:	HLRZ TT,(T)	;INTERPRET LSUBR CALL
	HRRZ A,(AR1)
ILIST:	MOVEI T,0
	JUMPE A,ILIST2
ILIST1:	PUSH P,A
	HLRZ A,(A)
	PUSH P,TT
	HRLM T,(P)
	PUSHJ P,EVAL
ILIST3:	POP P,TT
	HLRE T,TT
	EXCH A,(P)
	HRRZ A,(A)
	SOS T
	JUMPN A,ILIST1
ILIST2:	JRST (TT)

SETARG":	HRRM B,@ARG
ARG":	HRRZ A,.(A)
	POPJ P,
FUNCT":	PUSH P,A
	MOVE A,SP
	PUSHJ P,FIX1A
	POP P,B
	HLRZ B,(B)
	PUSHJ P,XCONS
	MOVEI B,FUNARG
	JRST XCONS

APFNG1:	0
APPLY:	MOVEI TT,AP2
	CAME T,[-3]
	JRST PDLARG
	MOVEM T,APFNG1
	PUSHJ P,ALIST
	MOVE T,APFNG1
	JSP TT,PDLARG
	PUSH P,CUNBIND
AP2:	PUSH P,A
	MOVEI T,0
AP3:	JUMPE B,IAPPLY
	HLRZ C,(B)
	PUSH P,C
	HRRZ B,(B)
	SOJA T,AP3



APFNG:	SOS T
	MOVEM T,APFNG1
	JSP TT,PDLARG
	HRRZ A,(A)
	HRRZ D,(A)
	HLRZ A,(A)
	HRLZ R,APFNG1
	PUSH P,CUNBIND
	JSP TT,ARGP1
	PUSH P,D
	PUSHJ P,ALIST
	POP P,D
	AOS T,APFNG1

IAPPLY:	MOVE C,T	;STATE OF WORLD AT ENTRANCE
	ADDI C,(P)	;T HAS - NUMBER OF ARGS ON PDL
ILP1A:	HRRZ B,(C)	;NEXT PDL SLOT HAS FUNCTION- POSS FUN NAME IN LH
ILP1:	CAIG B,INUM
	JUMPN B,QA1
	HLRZ A,(B)
	CAIN A,-1
	JRST IAPATM	;FN IS ATOMIC
	CAIN A,LAMBDA
	JRST IAPLMB
	CAIN A,FUNARG
	JRST APFNG
	CAIN A,LABEL
	JRST APLBL
	PUSH P,T
	MOVE A,B
	PUSHJ P,EVAL
	POP P,T
	MOVE C,T
	ADDI C,(P)
ILP1B:	MOVEM A,(C)
	JRST ILP1A

IAPATM:	HRRZ B,(B)
	JUMPE B,IAP2
	HLRZ TT,(B)
	HRRZ B,(B)
	CAIN TT,EXPR
	JRST IAPXPR
	CAIN TT,LSUBR
	JRST IAPLSB
	CAIN TT,SUBR
	JRST IAPSBR
	CAIE TT,CARRAY
	JRST IAPATM
IAPARR:	HLRZ B,(B)
	SKIPA B,(B)
IAPSBR:	HLRZ B,(B)
	HRRM B,(C)
	JRST ESB1
IAPXPR:	HLRZ A,(B)
	JRST ILP1B
IAPLSB:	MOVEI TT,CPOPJ
	HRRM TT,(C)
	HLRZ B,(B)
	JRST (B)


IAPLMB:	HRRZ B,(B)
	HLRZ TT,(B)
	MOVEM SP,SPSV
	HRRZ B,(B)
	HLRZ D,(TT)
	CAIN D,-1
	JUMPN TT,IAP3
	MOVE R,T
IPLMB1:	JUMPE T,IPLMB2	;NO MORE ARGS
	JUMPE TT,QF2	;TOO MANY ARGS SUPPLIED
IAP5:	HLRZ A,(TT)
	MOVEI AR1,1(T)
	ADD AR1,P
	HLLZ D,(AR1)
	HRLM A,(AR1)
	HRRZ TT,(TT)
	AOJA T,IPLMB1

IPLMB2:	JUMPN TT,IAP4	;TOO FEW ARGS SUPPLIED
	JUMPE R,IAP69
IPLMB4:	POP P,AR1
	HLRZ A,AR1
	AOJG R,IPLMB3
	PUSHJ P,BIND	;BIND VALUES TO LAMBDA VARIABLES
	JRST IPLMB4
IPLMB3:	SKIPE VBAKGAG
	JRST APBK1
APBK2:	HLRZ A,(B)
	PUSH SP,SPSV
	PUSHJ P,EVAL
	JRST UNBIND

APBK1:	HRRI AR1,CPOPJ 
	TLNE AR1,-1
	PUSH P,AR1
	JRST APBK2
IAP69:	POP P,(P)
	HLRZ A,(B)
	JRST EVAL


IAP2:	HRRZ A,(C)
	MOVEI B,VALUE
	PUSHJ P,GET
	JUMPE A,QA1
	HRRZ A,(A)
	CAIN A,UNBOUND
	JRST QA1
	JRST ILP1B

IAP3:	MOVNI AR1,-1(T)
	MOVE A,TT
	PUSHJ P,BIND
	PUSH P,ARG
	SOS C
	HRRM C,ARG
	PUSH SP,SPSV
	SFX
	HLRZ A,(B)
	PUSHJ P,EVAL
	HRRZ T,ARG
	POP P,ARG
	SUBI T,(P)
	HRLI T,-1(T)
	ADD P,T
CUNBIN:	JRST UNBIND

IAP4:	JUMPGE D,QF3	
	AOJN R,QF3
	PUSH P,B
	MOVE A,SP
	PUSHJ P,FIX1A
	EXCH A,(P)
	MOVE B,A
	MOVNI R,2
	SOJA T,IAP5

APLBL:	MOVEM SP,SPSV
	HRRZ B,(B)
	HLRZ A,(B)
	HRRZ B,(B)
	HLRZ AR1,(B)
	MOVEM AR1,(C)
	PUSHJ P,BIND
	MOVEI A,APLBL1
	EXCH A,-1(C)
	EXCH A,LBLAD
	HRLI A,LBLAD
	PUSH SP,A
	PUSH SP,SPSV
	SFX
	JRST IAPPLY
APLBL1:	PUSH P,LBLAD
	JRST UNBIND
LBLAD:	0

BIND":	PUSH P,B
	HRRZM A,BIND3
BIND2:	MOVEI B,VALUE	;BIND ATOM IN A TO VALUE IN AR1,SAVE
	PUSHJ P,GET	;OLD BINDING ON S PDL
	JUMPE A,BIND1	;ADD VALUE CELL
	PUSH SP,(A)
	HRLM A,(SP)
	HRRZM AR1,(A)
POPBJ":	POP P,B
	POPJ P,

BIND1:	MOVEI B,UNBOUND
	MOVEI A,0
	PUSHJ P,CONS
	HRRZ B,@BIND3
	PUSHJ P,CONS
	MOVEI B,VALUE
	PUSHJ P,XCONS
	HRRM A,@BIND3
	MOVE A,BIND3
	JRST BIND2


SPECBIND":	MOVEM SP,SPSV
SPEC1:	LDB R,[271500,,(T)]
	CAILE R,17
	JRST SPECX
	SKIPE R
	MOVE R,(R)
	EXCH R,@(T)
	HRL R,(T)
	PUSH SP,R
	AOJA T,SPEC1
SPECX:	PUSH SP,SPSV
	SFX
	JRST (T)

SPSV:	0
SPEXT:	REPEAT NSFC,CONC ZZM,\.RPCNT
SPST:	BLOCK NSFC
LSPST==NSFC

UNBIND":
SPECSTR":	POP SP,TT
UNBND1:	CAMN SP,TT
	JRST UNBND2
	POP SP,T
	MOVSS T
	HLRZM T,(T)
	JRST UNBND1

UNBND2:	MOVE T,(SP)
	MOVEM T,SPSV
	SFX
	POPJ P,

OREADCH":	JSP TT,ORD
READCH":	PUSHJ P,TYI
	MOVSI AR1,AR1
CEXPL1:	PUSHJ P,EXPL1
	HLRZ A,(AR1)
	POPJ P,

ORD:	JUMPE T,(TT)
	AOSE T
	LERR [SIXBIT /TMA-READ!/]
	SETOM EOFF
	MOVEM P,EOFRTN'
	PUSHJ P,(TT)
	SUB P,[1,,1]
	JRST ORD2
ORD1:	MOVE P,EOFRTN
	POP P,A
ORD2:	SETZM EOFF
	POPJ P,

EXPLODE":	SKIPA R,CEXPL1
%EXPLODE":	HRROI R,EXPL1
	MOVSI AR1,AR1
	PUSHJ P,PRINTA
	JRST SUBS4

EXPL1:	PUSH P,B
	PUSH P,C
	ANDI A,177
	CAIL A,60
	CAILE A,71
	JRST,EXPL2
	subi a,60
	PUSHJ P,FIX1A
	JRST,EXPL4

EXPL2:	LSH A,35
	PUSHJ P,FWCONS
	PUSHJ P,NCONS
	PUSH P,AR1
	PUSH P,TT
	PUSH P,T
	PUSHJ P,RINTERN
	POP P,T
	POP P,TT
	POP P,AR1
EXPL4:	PUSHJ P,NCONS
	HLR B,AR1
	HRRM A,(B)
	HRLM A,AR1
	POP P,C
	JRST POPBJ


READLIST":	SKIPA T,READ0B
MAKNAM":	MOVEI T,PNGNK1
	JUMPE A,E7
	HRRM A,MKNM3
	MOVEI A,MKNAM2
	PUSHJ P,READ0
	HRRZ T,MKNM3
	CAIE T,-1
	JUMPN T,[LERR [SIXBIT /MORE THAN ONE S-EXPRESSION-MKNAM!/]]
	POPJ P,

MKNAM2:	PUSH P,B
	PUSH P,T
	PUSH P,TT
MKNAM3:	HRRZ TT,MKNM3
	JUMPE TT,MKNAM6
	CAIN TT,-1
	LERR [SIXBIT /READ UNHAPPY-MAKNAM!/]
	HRRZ B,(TT)
	HRRM B,MKNM3
	HLRZ A,(TT)
	PUSHJ P,NUMBERP
	JUMPE A,MKNAM5
	HLRZ A,(TT)
	PUSHJ P,NUMVAL
	ADDI A,60
MKNAM4:	POP P,TT
	POP P,T
	JRST POPBJ
MKNAM5:	HLRZ A,(TT)
	MOVEI B,PNAME
	PUSHJ P,GET
	HLRZ A,(A)
	LDB A,C3MKN
	JRST MKNAM4
MKNAM6:	MOVEI A,40
	HLLOS MKNM3
	JRST MKNAM4

C3MKN:	(350700+A)0


	;HAIRY READER

OREAD":	JSP TT,ORD
READ":	MOVEI A,TYI
READ0B:	MOVEI T,RINTERN
READ0:	HRRM A,READ2
	HRRM T,RDSP20
	MOVE T,VIBASE
	SUBI T,1
	MOVEM T,RDIBS
	HRRI T,RDOBJ
	SKIPN RDMOD
	HRRI T,RDQTE
	HRRM T,READ2A

READ0A:	HRLZI T,2200
	JRST,.+2
READ1:	HRLZI T,2000
READ2:	PUSHJ P,.
READ2A:	PUSHJ P,.	;RDOBJ OR RDQTE
	CAIE B,40
	CAIN B,",
	JRST,RDSPA
	CAIN B,"(
	JRST,RDOPN
	CAIN B,")
	JRST,RDCLOS
	CAIN B,".
	JRST,RDDOT
	JRST,RDDELE

RDQTE:	CAIE A,"'
	JRST RDOBJ
	PUSHJ P,@READ2
	PUSHJ P,RDOBJ
	PUSH P,B
	PUSHJ P,CONS
	MOVEI B,CQUOTE
	PUSHJ P,XCONS
	JRST POPBJ


RDSPA:	TLNE T,200
	POPJ P,
	JSP C,RDSPA3
	JRST,READ2

RDSPA3:	PUSHJ P,NCONS
	TLNN T,2000
	JRST .+3
	HRRM A,(A)
	JRST RDSPA2
	POP P,AR1
	HRRZ B,(AR1)
	HRRM B,(A)
	HRRM A,(AR1)
RDSPA2:	PUSH P,A
	MOVEI T,0
	JRST,(C)


RDOPN:	TLNE T,200	;FIRST OBJECT ON INPUT LIST ILLEGAL
	JRST,ER1
	JSP C,RDSPA3
RDPAR1:	HRRZI A,50
	JRST READ2+1

RDDOT:	TLNE T,100
	JRST ER2
	JSP C,RDSPA3
	HRLZI T,100
	JRST,READ2

RDCLOS:	TLNE T,200
	JRST,READ0A
	TLNN T,100
	PUSHJ P,NCONS
	TLNE T,2000
	POPJ P,
	POP P,AR1
	HRRZ B,(AR1)
	HRRM A,(AR1)
	JRST SPROG2


;FLAGS

;1 FLOATING NUMBER
;2 DECIMAL NUMBER
;4 E, ^, OR _ TYPED ON A NUMBER
;10 DOT
;20 LET
;40 NUM
;100 DOT PAIR
;200 TOP LEVEL
;400 MINUS
;1000 -E
;2000 1ST OBJECT
;4000 ^ OR _ TYPED ON A NUMBER
;10000 _ TYPED ON A NUMBER
;20000 READ INITIAL + OR -

RDOBJ:	CLEARB B,C
	JRST RDCH1
RDCH:	PUSHJ P,@READ2
RDCH1:	CAIG A,"Z
	CAIGE A,"A
	JRST,RDOBJ1
RDLET1:	PUSHJ P,RDLET
	JRST RDCH

RDLET:	TLNE T,40
	JRST,RDLETE
	TLON T,20
RDNEW:	TLZE T,10	;FIX-UP T-CONDITION WHEN SECOND PAR
	TLOA T,100	;OF DOT PAIR BEGINS TO COME IN
	JRST .+3
	TLNE T,2000
	JRST ER1
	HRRM A,RDRLD1
	PUSHJ P,FW0CNS
	HRLI A,700
	MOVEM A,RDPTR
	PUSHJ P,NCONS
	HRRM A,(A)
	JRST,RDRLD2

RDRLOD:	HRRM A,RDRLD1
	HRRZ B,(AR1)
	PUSHJ P,FW0CNS
	HRRM A,RDPTR
	PUSHJ P,CONS
	HRRM A,(AR1)
RDRLD2:	SOS,RDPTR
	MOVE AR1,A
	MOVEI B,5
RDRLD1:	TDZA A,A
RDPAK:	SOJLE B,RDRLOD
	IDPB A,RDPTR
	POPJ P,

RDPTR:	0
RDLETE:	TLNN T,3
	JRST RDLTE1
	CAIE A,105
	JRST,ER1
	TLON T,5
	MOVEI TT,0
	MOVEI C,0
	POPJ P,
RDLTE1:	SKIPE RDMOD
	TLNN T,20000
	JRST ER1
	TLZ T,20
	CAIL A,"A
	SUBI A,"A-10.-"0
	CLEARB B, C
	SUB P,[1,,1]
	JRST RDNUM

RDOBJ1:	CAIG A,"9
	CAIGE A,"0
	JRST,RDOBJ2
RDNUM:	TLNE T,20
	JRST,RDLET1
	SUBI A,"0
	TLNE T,4
	JRST,RDEXP
	TLO T,40
	TLZN T,12
	JRST,RDNUM1
	TLO T,1
	MOVEI TT,0
RDNUM1:	IMULI B,12
	ADD B,A
	PUSH P,C+1
	MUL C,RDIBS
	LSH C+1,1
	LSHC C,35.
	POP P,C+1
	ADD C,A
	SOJA TT,RDCH

RDEXP:	TLNE T,4000
	IMUL B,RDIBS
	IMULI C,12
	TLNE T,4000
	ADD B,A
	ADD C,A
	JRST,RDCH

RDOBJ2:	CAIN A,11
	MOVEI A,",
	CAIE A,40
	CAIN A,",
	JRST,RDSP
	CAIE A,"(
	JRST,RDOBJ3

RDOP:	TLNE T,60
	JRST,RDSP1
	TLZE T,10
	TLO T,100
	PUSH P,T
	PUSHJ P,READ1
	MOVEI B,",
	POP P,T
	JRST,RDSPS+1

RDOBJ3:	CAIE A,")
	JRST RDOBJ4

RDCL:	TLNE T,60
	JRST,RDSP1
	TLO T,100
	MOVEI B,")
	JRST FALSE


RDOBJ4:	CAIE A,".
	JRST,RDOBJ5

RDDT:	TLNE T,40
	JRST,RDDT1
	TLNE T,20
	JRST,RDDT2
	TLOE T,10
	TLO T,100
	JRST,RDCH
RDDT1:	TLON T,2
	TDZA TT,TT
	TLOA T,10
	JRST RDCH
	JRST RDSP1
RDDT2:	TLNE T,200
	JRST,ER2
	JRST,RDSP1

RDOBJ5:	CAIN A,"+
	JRST .+3
	CAIE A,"-
	JRST RDOBJ7
	TLNE T,20
	JRST RDLET1
	TLNE T,4
	JRST RDOBJ6
	TLNE T,40
	JRST ER1
	CAIE A,"-
	TLO T,400
	SKIPE RDMOD
	TLO T,20060
;	JSP TT,...	;;;XXX can't read
	PUSHJ P,RDNEW
	JRST RDCH
RDOBJ6:	CAIN A,"-
	TLO T,1000
	JRST RDCH


RDSP:	TLZE T,10
	JRST,RDSP2
RDSP1:	HRRM A,RDSPS
	TLNN T,20
	JRST,RDSP3
	HRRZ A,(AR1)
	HLRM A,(AR1)
	PUSH P,T
RDSP20:	PUSHJ P,.
	POP P,T
RDSPS:	HRRZI B,0
	TLNE T,100
	JRST,RDSP8
	POPJ P,

RDSP3:	TLNN T,40
	JRST,RDCH
	TLNE T,1
	JRST,RDSP4
	TLNE T,4000
	JRST RDSP51
	TLNE T,2
	MOVE C,B
	MOVE A,C
RDSP31:	MOVEI B,FIXNUM
RDSP5:	TLNE T,400
	MOVNS,A
	PUSHJ P,MAKNUM
	JRST,RDSPS
RDSP51:	TLNN T,2
	MOVE C,B
	MOVE A,RDBSV
	ANDI C,77
	TLNN T,10000
	JRST RDSP52
	LSH A,@C
	JRST RDSP31
	MUL A,RDIBS
	LSH A+1,1
	LSHC A,35.
RDSP52:	SOJGE C,.-3
	JRST RDSP31

RDSP4:	TLNE T,1000
	MOVNS,C
	TLNE T,4
	ADD TT,C
	TLNE TT,400000
	TLO T,1000
	MOVE A,B
	PUSHJ P,FLOAT
	MOVM B,TT
	JRST,RDSP6

RDOBJ7:	CAIE A,177
	JRST,RDOBJ8
RDRUB:	TLZE T,35577
	JRST,RUBOUT
	MOVE B,A
	POPJ P,

RUBOUT:	PUSHJ P,@READ2
	JRST,RDOBJ

RDSP7:	TLNE T,1000
	FDVR A,C1RD
	TLNN T,1000
	FMPR A,C1RD
RDSP6:	SOJGE B,RDSP7
	MOVEI B,FLONUM
	JRST,RDSP5

RDSP2:	TLNE T,200
	JRST,ER2
	TLO T,100
	JRST,RDCH

RDSP8:	EXCH A,B
	CAIN A,")
	JRST,RDSP9
	CAIE A,40
	CAIN A,",
	JRST,.+4
	CAIN A,177
	JRST,RDRUB
	JRST,ER1
	PUSHJ P,@READ2
	JRST,RDSP8+1
RDSP9:	EXCH A,B
	POPJ P,

C1RD:	10.0
RDBSV:	0
RDIBS:	0

RDOBJ8:	CAIE A,"/
	JRST RDOBJ9
	PUSHJ P,@READ2
	JRST,RDLET1

RDDELE:	TLNE T,200
	JRST,READ0A
	TLNN T,2000
	JRST,RDDEL1
	POP P,T
	POP P,T
	JRST,RUBOUT
RDDEL1:	POP P,AR1
	MOVE A,AR1
RDDEL3:	HRRZ B,(A)
	CAMN B,AR1
	JRST,RDDEL2
	MOVE A,B
	JRST,RDDEL3
RDDEL2:	CAMN A,AR1
	JRST,READ1
	HRRZ B,(AR1)
	HRRM B,(A)
	PUSH P,A
	MOVEI T,0
	JRST,READ2

RDOBJ9:	CAIL A,"'
	TLNN T,40
	JRST RDOBJ0
	TLO T,4004
	TLZN T,2
	MOVE B,C
	MOVEM B,RDBSV
	CLEARB B,C
	CAIE A,"'
	TLO T,10000
	JRST RDCH

RDOBJ0:	CAIE A,33
	CAIL A,40
	TLZE T,40
	JRST RDCH
	JRST RDLET1

RINTERN":	SETZM RINF'	;INTERN WITH ARG  MERELY A POINTER
	JRST INTRN		;TO PNAME - - MAY RECLAIM SUCH, IF
INTERN":	AOS RINF	;ALREADY ON OBLIST.
	MOVEM A,INTRN1'
	MOVEI B,PNAME
	PUSHJ P,GET
	JUMPE A,E6
INTRN:	HLRZ C,(A)
	LDB B,[(004300)(C)]
	IDIVI B,OBTSIZ
	HLRZ TT,OBTBL(B+1)
	HRLM B+1,MAKA2
	JRST,MAKF+1

MAKF:	HRRZ TT,(TT)
	JUMPE TT,MAKA
	HLRZ AR1,(TT)
MAKF1:	HRRZ AR1,(AR1)
	JUMPE AR1,E6		;NO PRINT NAME
	HLRZ T,(AR1)
	HRRZ AR1,(AR1)
	CAIE T,PNAME
	JRST,MAKF1
	HRRZ T,A
	HLRZ AR1,(AR1)
MAK2:	JUMPE AR1,MAK1
	JUMPE T,MAKF
	HLRZ B,(AR1)
	MOVE B,(B)
	HLRZ C,(T)
	CAME B,(C)
	JRST,MAKF
	HRRZ AR1,(AR1)
	HRRZ T,(T)
	JRST,MAK2

MAKA:	SKIPE RINF
	SKIPA A,INTRN1
	PUSHJ P,PNGNK1
MAKA2:	MOVEI C,.
	HLRZ B,OBTBL(C)
	PUSHJ P,CONS
	HRLM A,OBTBL(C)
	JRST CAR

MAK1:	JUMPN T,MAKF
	SKIPE RINF
	JRST MAK1A	;ON RINTERN ENTRY, RECLAIM  ARG.
MAK1B:	HLRZ B,(A)
	MOVEM FF,(B)
	MOVE FF,B
	HRRZ B,(A)
	MOVEM F,(A)
	MOVE F,A
	SKIPE A,B
	JRST MAK1B
MAK1A:	HLRZ A,(TT)
	POPJ P,

IFN 0,[	;GC A PARTICULAR LIST.
RECLAIM:	HRRZS (A)
RECL1:	CAIGE A,@GCP
	CAIGE A,FS
	POPJ P,
	CAIGE A,@GCP1
	JRST REFS
REFWS:	MOVEM FF,(A)
	MOVE FF,A
	POPJ P,
REFS:	PUSH P,A
	HLRZ A,(A)
	CAIN A,-1
	JRST POPAJ
	PUSHJ P,RECL1
	HRRZ A,@(P)
	PUSHJ P,RECL1
	MOVEM F,@(P)
	POP P,F
	POPJ P,
]

REMOB":	JUMPE A,FALSE
	PUSH P,A
	HLRZ A,(A)
	PUSHJ P,INTERN
	HLRZ B,@(P)
	CAME A,B
	JRST REMOB2
	HRRZ B,MAKA2
	HLRZ C,OBTBL(B)
	HLRZ T,(C)
	CAMN T,A
	JRST REMOB1
REMOB3:	MOVE TT,C
	HRRZ C,(C)
	HLRZ T,(C)
	CAME T,A
	JRST REMOB3
	HRRZ T,(C)
	HRRM T,(TT)
REMOB2:	POP P,A
	HRRZ A,(A)
	JRST REMOB
REMOB1:	HRRZ TT,(C)
	HRLM TT,OBTBL(B)
	JRST REMOB2

GC":	PUSHJ P,AGC
	JRST FALSE

GCTWA":	AOJL T,QF2
	JUMPN T,.+3
	POP P,VGCTWA
	POPJ P,
	SETOM IRMVF
	POPJ P,

IRMVF:	0	;REMOVAL OVERRIDE SWITCH

IFN TS,[
NORET":	HRRZM A,VNORET
	POPJ P,

GCNRT":	JSP T,SPECBIND	;GC WITHOUT ATTEMPTING
		VNORET
	AOS VNORET
	PUSHJ P,AGC	;TO RETURN SPACE.
	JRST SPECSTR
]


STGPNT:	HLLZS PRINL1	;GCGAG PRINTOUT
	JUMPE A,PRINL2
	HRRZ A,(A)
	AOS PRINL1
	JRST .-3

	;GARBAGE COLLECTOR

AGC":	MOVEM R,GC10SV
	MOVEM FP,GC10SV+1
	MOVEM D,GC10SV+2
	HRRZI D,OBTBL
	SKIPN VGCTWA
	AOSN IRMVF	;F OVERRIDE IS ON, THEN
	MOVSS D		;DO REMOVAL ANYHOW.
	HRRZM D,GCRMV'
AGC3==.
	PUSH P,D	;OBTBL OR NIL, DEPENDING ON VGCTWA.
	PUSH P,PA3
	PUSH P,PA4
IFN <1-TS>*VD,	PUSH P,DISPNR
AGCSUB==.-AGC3	;NUMBER TO UNSCREW PDL

GCP4:	MOVEI FP,0	;PDLAC
	BLT FP,0	;PDLAC+N
	MOVEI AR1,GCEND
	SKIPN GCMKL
	JRST .+5
	MOVE A,VBPEND
	PUSHJ P,NUMVAL
	HRRM A,GCARR
	MOVEI AR1,GCARR
	HRRM AR1,GCP1C	;SETUP LIMITS FOR ARRAYS
	SKIPN AR1,GCRMV
	MOVEI AR1,OBTBL+OBTSIZ
	HRRM AR1,GCP1B	;OBLIST MARKING ?
GCP2:	SETZB NIL,0	;GC INDICATOR, INIT. FOR BIT TABLE ZERO
IFN VD,[MOVE A,VDISLIST
	MOVEM A,SVDLST'
	SETZM VDISLIST]
	MOVE A,C3GC
GCP5:	BLT A,0	;ZERO BIT TABLES
	SKIPN GCGAGV
	JRST GCP5A
	SKIPN F
	STRT [SIXBIT /_FREE STG EXHAUSTED!/]
	SKIPN FF
	STRT [SIXBIT /_FULL WORD SPACE EXHAUSTED!/]
GCP5A:	MOVEI TT,1
GCP3:	MOVEI C,0	;MARK PDL, AC'S,  AND
	PUSHJ P,GCMARK	;PRECIOUS ATOMS.
GCP6:	HRRZ R,SC2
GCP6C:	CAIL R,(SP)	;MARK SP
	JRST GCP6A
	PUSH P,(R)
	JSP T,GCMK1
	AOJA R,GCP6C

GCP6A:	SKIPE GCRMV
	JRST .+3
	JSP R,GCGEN	;TRY MARKING FROM 
		GCP8I	;NON-TRIVIAL P-LISTS.
	HRRZ R,GCMKL
GCP6A1:	JUMPE R,GCP6H
	HLRZ A,(R)
	MOVE D,(A)	;IF ARRAY IS ALIVE,
	TLNE D,40	;AND BIT 11 IS ON,
	TLNN D,100
	JRST GCP6F	;THEN MARK FROM
	MOVE D,-1(D)	;ARRAY ENTRIES.
GCP6E:	PUSH P,(D)
	JSP T,GCMK1
	JSP T,GCMK1A
	AOBJN D,GCP6E
GCP6F:	HRRZ R,(R)
	HRRZ R,(R)
	JRST GCP6A1

GCP6H:	MOVEI R,GCMKL	;DELETE ARRAYS FROM
IFN VD,[MOVE A,SVDLST
	MOVEM A,VDISLIST
]
GCP6H1:	HRRZ FP,(R)	;GCMKL IF NOT SAVED
	JUMPE FP,GCP7
	HLRZ D,(FP)
	MOVE A,(D)
	TLZE A,40
	JRST GCP6G
	HRRZ F,(FP)
	HRRZ F,(F)
	HRRM F,(R)
IFN VD,[MOVE A,D
	PUSHJ P,RM5 	;DLT FROM DISLIST.
]
	JRST GCP6H1
GCP6G:	MOVEM A,(D)
	HRRZ R,(FP)
	JRST GCP6H1
GCP7:	MOVEI C,GCEND	;PROTECT GCMKL, BUT FIRST
	HRRM C,GCP1C	;CRUCIFY THE ARRAY SAVIOUR.
	PUSH P,GCMKL
	JSP T,GCMK1
IFN VD,[HRRZ A,VDISLIST
	JSP T,GCMARK	;MARK DISLIST
]
	SKIPE GCRMV
	JRST GCSWP
	MOVEI R,FS
	HRRM R,GCP1B	;ALLOW OBTBL MARKING.
	JSP R,GCGEN	;PROTECT OBLIST, BUT FIRST
		GCP8G	;WIPE OUT T.W.A.'S
	PUSH P,[OBTBL]
	JSP T,GCMK1

;GARBAGE COLLECTOR SWEEP

GCSWP:	MOVSI R,GFSWPP	;RELOCATE INNER LOOP TO AC'S.
	BLT R,LPROG	;FOR FS SWEEP.
	MOVEI F,0
	MOVE D,C3GCS
	MOVEI R,FS
GCBTL1:	HRLI R,.	;-(32-<FS&37>
	MOVE FF,(D)
GCBTL2:	ROT FF,.	;FS&37
	AOBJN D,GFSP1

GFSPR:	MOVSI R,GFWSWP
	BLT R,LSP2	;RELOCATE FOR FWS SWEEP.
	MOVE R,C1GCS
	MOVE D,C2GCS
	MOVEI FF,0
	JRST GFSP2
GCSP2R:	SKIPN GCGAGV
	JRST GCSP3
	PUSHJ P,TERPRI
	MOVE A,F
	PUSHJ P,STGPNT
	STRT [SIXBIT / FREE STG,!/]
	MOVE A,FF
	PUSHJ P,STGPNT
	STRT [SIXBIT / FULL WORDS AVAILABLE_!/]
GCSP3:	SUB P,[AGCSUB,,AGCSUB]
	JUMPE F,[LER2 [SIXBIT /NO FREE STG LEFT!/]]
	JUMPE FF,[LER2 [SIXBIT /NO FW STG LEFT!/]]
	SETOM DIEF
IFN TS,[SKIPN VNORET
	PUSHJ P,RETSP]
GCSP1:	HRLZI FP,.
	BLT FP,NACS+2
	MOVE FP,GC10SV+1
	MOVE D,GC10SV+2
	MOVE R,GC10SV
GCSP2:	AOSN BELFLG
	JSR QUIT
	POPJ P,


GFSWPP:		;FS SWEEP, RELOCATED TO AC'S.
OFFSET -.
GFSP1==.
	JUMPL FP,.+3
	HRRZM F,(R)
	HRRZ F,R
	ROT FF,1
	AOBJN R,.-4
	MOVE FF,(D)
	HRLI R,-32.
	AOBJN D,GFSP1

LPROG==.
	JRST GFSPR
OFFSET 0

GFWSWP:		;FWS SWEEP, ALSO RELOCATED.
OFFSET -.
GFSP2==.
	ILDB T,D
	JUMPN T,.+3
	HRRZM FF,(R)
	HRRZ FF,R
	AOBJN R,.-4
LSP2==.
	JRST GCSP2R
OFFSET 0


GC10SV:	BLOCK 3
C1GCS:	0
C2GCS:	100,,0
C3GCS:	0	;-N WDS IN BT,,BT
C2GC:	( 430100+AR1)0	;BTF
C3GC:	0	;(BIT) BIT+1
GCBT:	REPEAT 32., SETZ_-.RPCNT
	;MARKING ROUTINES

GCMK1A:	PUSH P,(D)
	MOVSS (P)
GCMK1:	SKIPN (P)
	JRST GCMK2
	HRRZ C,P
	PUSHJ P,GCMARK
GCMK2:	SUB P,[(1)1]
	JRST (T)

GCMARK:	MOVE FP,P
	HLL C,P
	MOVEI B,0
GC1:	CAMN C,FP
	POPJ P,
	HRRZ A,(C)

GCP":	CAIGE A,.	;BT, RESET BY ALLOC.
GCP1B:	CAIGE A,FS	;FS OR OBTBL+OBTSIZ
GCP1C:	JRST GCARR	;GCARR OR GCMKND
GCP1":	CAIL A,.	;FWS, RESET BY ALLOC.
	JRST,GCMFWS
	MOVE F,(A)
	LSHC A,-5
	ROT B,5
	MOVE AR1,GCBT(B)
GCBTP2:	TDOE AR1,.(A)	;BIT TAB- (FS_-5)
	JRST GCEND
GCBTP1:	MOVEM AR1,.(A)	;BIT TAB- (FS_-5)
	PUSH P,F
	HLRZ A,F
	JRST,GCP
GCARR:	CAIG A,.	;FILLED IN ABOVE
	JRST GCEND
	MOVE AR1,GCMKL	;SEARCH LIVING ARRAYS
GCA1:	JUMPE AR1,GCEND	;TO SEE IF POINTER IS
	HLRZ AR2A,(AR1)	;AN ARRAY ENTRY.
	HRRZ F,(AR2A)
	HRRZ AR1,(AR1)
	HRRZ AR1,(AR1)
	CAME A,F
	JRST GCA1
	HRLZI A,40	;BIT 12 (THE JESUS BIT)
	IORM A,(AR2A)	;SAVES LIVES OF ARRAYS.
	JRST GCEND
GCMFWS:	MOVEI AR1,.(A)	;-FWS
	IDIVI AR1,36.
	MOVNS,AR2A
	LSH AR2A,36
	ADD AR2A,C2GC
	DPB TT,AR2A
GCEND:	CAMN P,FP
	AOJA C,GC1
	POP P,A
	HRRZS A
	JRST,GCP

GCGEN:	SKIPA FF,[OBTBL]	;GENERATE NON-NULL BUCKETS
GCP8A:	HRRZ FF,(FF)	;AND APPLY FUNCTION
	JUMPE FF,1(R)	;TO THEM.
	HLRZ D,(FF)
	JRST @(R)

GC8TST:	JUMPE A,(T)
	HRRZ AR2A,(A)	;NO SKIP IF ALREADY
	CLEARM B	;MARKED ATOM - -
	LSHC A,-5	;SKIP 1 IF NON-TRIVIAL
	ROT B,5 	;P-LIST - -
	MOVE AR1,GCBT(B)	;SKIP 2 IF T.W.A.
GC8T1:	TDNE AR1,.(A)	;(TRULY WORTHLESS ATOM).
	JRST (T)
	HRRZ AR2A,(AR2A)
	HRRZ AR2A,(AR2A)
	SKIPE AR2A
	JRST 1(T)
	JRST 2(T)

GCP8J:	HRRZ D,(D)	;MARK ATOMS ON OBLIST
GCP8I:	JUMPE D,GCP8A	;WHICH HAVE NON-TRIVIAL
	HLRZ A,(D)	;P-LIST STRUCTURE.
	JSP T,GC8TST
	JRST GCP8J
	JSP T,GCMK1A
	JRST GCP8J

GCP8G:	JUMPE D,GCP8A	;REMOVE T.W.A.'S FROM
	HLRZ A,(D)	;BUCKETS OF OBLIST.
	JSP T,GC8TST
	JRST GCP8B
	JRST GCP8B
	HRRZ D,(D)
	HRLM D,(FF)
	JRST GCP8G
GCP8C:	HRRZ D,(D)
GCP8B:	HRRZ A,(D)
GCP8D:	JUMPE A,GCP8A
	HLRZ A,(A)
	JSP T,GC8TST
	JRST GCP8C
	JRST GCP8C
	HRRZ A,(D)
	HRRZ A,(A)
	HRRM A,(D)
	JRST GCP8D


DEFINE	APUSH A
	AOS INSP
	MOVEM A,@INSP
TERMIN


IFN PKPKF,[
VARRAY":	SETOM VARFLG'
	SKIPA
ARRAY":	SETZM VARFLG
]
IFE PKPKF,ARRAY":
	MOVEM A,ADDSAR'
	PUSH P,A
	PUSHJ P,CDDR
	JUMPE A,QA2A
	JSP TT,ILIST
	HRLM T,NARINS'
	MOVN AR1,T
	LSH AR1,1
	CAIG AR1,3
	ADDI AR1,1	;L.H. HAS NO. OF DIMS
	HRRM AR1,NARINS	;R.H. HAS NO. OF INS+1.
IFN PKPKF,[SKIPE VARFLG
	ADDI AR1,30 ]
	MOVEI A,1
ARRY1:	MOVEI B,1(P)
	ADD B,T
	SOS (B)
	IMUL A,(B)
	AOJL T,ARRY1
	ADDI A,1	;NO. OF CELLS OCCUPIED BY
	LSH A,-1	;ARRAY ENTRIES.
	MOVNM A,BPPNR
	ADDI A,1(AR1)
	HRLM AR1,TOTSPC'	;L.H. HAS HEADER SIZE - 1,
	HRRM A,TOTSPC	;R.H. HAS TOTAL SPACE.
	HLRZ A,@ADDSAR
	MOVEI B,CARRAY
	PUSHJ P,GET
	JUMPN A,ARRY6
	CLEARB A,B	;NO SAR CELL YET
	PUSHJ P,CONS	;EXISTS - SO
	HLRZ B,@ADDSAR
	EXCH A,B
	MOVEI C,CARRAY	;PUTPROP ONE.
	PUSHJ P,PUTPROP


ARRY6:	MOVEM A,ADDSAR
	MOVE B,A	;REMOVE FROM GCMKL
	MOVEI A,GCMKL	;IF ALREADY THERE
	PUSHJ P,REMPROP
IFN VD,[MOVE A,ADDSAR
	PUSHJ P,RM5]
	HRRZ A,TOTSPC
	PUSHJ P,GTSPC	;GET ENOUGH SPACE.
	HLRE T,NARINS	;RESTORE T.
	JUMPE A,ARRY8
	HRRZ AR1,TOTSPC
	SUB AR1,A 	;NUMERIC BPEND IN A.
	MOVNS AR1
	MOVEM AR1,INSP'
	HLRZ C,TOTSPC
	ADDI C,2(AR1)
	HRL C,BPPNR
	MOVEM AR1,BPPNR
	APUSH C	;ARRAY BITE POINTER
	MOVEI AR1,1
	MOVEI AR2A,1
	AOJGE T,ARRYS
	MOVEI D,A-1
	SUB D,T
IFN PKPKF,[	SKIPL VARFLG
	JRST ARRY2
	POP P,TT	;REVERSE DIMENSIONS
	EXCH TT,(P)
	PUSH P,TT
	POP P,VARG2'	;GET LAST ARG FOR VARRAY
	SKIPA TT,VARG2	]
ARRY2:	POP P,TT
	IMULB TT,AR1
	ADDM AR1,AR2A
	TLC TT,(IMULI)
	DPB D,[270400,,TT]
IFN PKPKF,[SKIPGE VARFLG	;CHANGE AC TO B
	ADD TT,[0 B-A,]	 ]
	APUSH TT
	CAIN D,A
	JRST ARRY3
	MOVSI TT,(ADDI(1))(D)
	DPB D,[270400,,TT]
	APUSH TT
	SOJA D,ARRY2

ARRY3:	MOVN TT,AR2A
	TLCA TT,-(ADDI A,(B))
ARRYS:	HRLOI TT,+(ADDI A,)-1
ARRY3A:	ADD TT,INSP
	ADD TT,INSP
	ADDI TT,10
IFN PKPKF,[SKIPGE VARFLG
	ADD TT,[0 B-A,30*2(,A-B)]	;CHANGE AC, INDEX, AND SKIP HEADER
]
	APUSH TT
	MOVE TT,[JSP C, ARYCOM]
	APUSH TT
IFN PKPKF,[SKIPGE VARFLG	;CHANGE TO JSP C, VARYCM
	SOS @INSP ]
	HRRZ C,TOTSPC
	ADD C,BPPNR	;CHECK CELL - HAS
	APUSH C	;LAS LOC OF ARRAY
	MOVE A,INSP
	ADDI A,1
	SETZM (A)
	CAMN A,C
	JRST .+4
	HRL A,INSP		;ZERO OUT ARRAY
	ADD A,[1,,1]
	BLT A,(C)
	POP P,C
IFN PKPKF, MOVEM C,VARG1'	;GET FIRST ARG FOR PICARRAY
	POP P,AR2A
	HRLZ AR1,NARINS
	LSH AR1,11.
	IOR AR1,[700000,,2]
	HRRZ B,(AR2A)
	HLRZ B,(B)
	SKIPE B
	TLO AR1,100
	ADD AR1,BPPNR	;AR1 WILL BE PUT INTO SAR CELL.
	MOVEM AR1,@ADDSAR
	HRRZ A,TOTSPC	;WILL MAKE AN ENTRY
	PUSHJ P,FIX1A	;ON GCMKL.
	PUSHJ P,NCONS	
	MOVE B,ADDSAR
	PUSHJ P,XCONS	;EXPECT ORIGINAL ARG
	MOVE B,A	;IN AR2A
	MOVEI A,GCMKL
	PUSHJ P,.NCONC
IFN PKPKF,[	SKIPL VARFLG
	JRST ARRY4
	MOVE B,INSP
	ADDI B,30
	MOVE A,VARG2	;STORE DIMENSIONS
	MOVEM A,XDIM+1(B)
	MOVE A,VARG1
	MOVEM A,YDIM+1(B)
	MOVSI A,165400	;1/4096
	MOVEM A,FXDEN+1(B)
	MOVEM A,FYDEN+1(B)
	MOVE A,[442200,,2]
	MOVEM A,BYTER+1(B)
]


ARRY4:	MOVE A,BPPNR
	PUSHJ P,FIX1A
	MOVEM A,VBPEND 
	HLRZ A,(AR2A)
RM4:	POPJ P,
ARRY8:	SOS T
	HRLS T
	ADD P,T
	POPJ P,



REMAR":	SKIPA C,A
RM1:	HRRZ C,@RM4
	JUMPE C,GTSP2	;REMOVE ARRAYS BY PUTTING
	MOVEM C,RM4	;ADDRESS OF ERROR ROUTINE
	HLRZ A,(C)	;IN SAR CELL.
	MOVEI B,CARRAY
	PUSHJ P,GET
	JUMPE A,RM1
IFN VD, PUSHJ P,RM5
IFE VD,[MOVEI B,QA2A
	MOVEM B,(A)]
	JRST RM1
IFN VD,[
RM5":	HRRM A,RM3
	SKIPN B,VDISLIST
	JRST RM6
	CALLF 2,CDELQ
	MOVEM A,VDISLIST
	SOSE DLTC	;DLTC=1 MEANS NO DELETION
	SKIPN DISON
	JRST RM6
IFN TS,	PUSHJ P,DSTRT
IFE TS,	SETZM DISPNR
RM6:	MOVEI B,QA2A
RM3:	MOVEM B,.
	POPJ P,
]


IFN PKPKF,VARYCM:	EXCH A,B	;X COORD. FIRST
ARYCOM":	ROT A,-1
	SKIPGE B,A
	TLCA B,402200
	TLC B,222200
ACM1:	HRRZ A,B
	HRRZS C
	CAMLE A,C
	CAMLE A,(C)
	LERR [SIXBIT /ARRAY ACCESS ERROR!/]
ACM2:	LDB A,B
	POPJ P,

NOCHK":	JUMPE A,.+2
	SKIPA B,[ACM2,,ACM1]
	MOVE B,[ACM3,,ACM1]
	BLT B,ACM1+1
	POPJ P,
ACM3:	HRRZ A,B
	HRRZS C

STORE":	PUSH P,A
	HRRZ A,(A)
	HLRZ A,(A)
	PUSHJ P,EVAL
	EXCH A,(P)
	HLRZ A,(A)
	PUSHJ P,EVAL
	POP P,A
	DPB A,B
	POPJ P,
CSTORE":	PUSH P,A
	PUSHJ P,CADR
	PUSHJ P,EVAL
	EXCH A,(P)
	HLRZ A,(A)
	PUSHJ P,EVAL
	MOVEM B,C
	POP P,B
	PUSHJ P,XCONS
	DPB A,C
	POPJ P,
NSTORE":	PUSHJ P,STORE
	MOVEM B,C
	LDB A,C
	PUSHJ P,NUMVAL
	DPB A,C
	POPJ P,

GETSP":	PUSHJ P,NUMVAL
	PUSHJ P,GTSPC
GTSP2:	SKIPE A
	MOVE A,VBPEND
	POPJ P,

GTSPC":	MOVEM A,GAMNT'	;INSURE THAT BPEND-BPORG
	MOVE A,VBPORG	;IS AT LEAST AS LARGE AS
	PUSHJ P,NUMVAL	;GAMNT.
	ADD A,GAMNT
	MOVE C,A
	MOVE A,VBPEND	;LEAVE BPEND IN A WHEN
	PUSHJ P,NUMVAL	;XITING.
	CAMLE A,C
GREL2:	POPJ P,
	SUB C,A
	MOVEM C,GAMNT
	PUSHJ P,GFSPC	;SEE IF RELOCATING
	JRST GRELAR	;LIVING ARRAYS WILL
IFE TS, PUSHJ P,GC	;YIELD ENOUGH SPACE,
IFN TS, PUSHJ P,GCNRT
	PUSHJ P,GFSPC	;OTHERWISE, GC AND
	JRST GRELAR	;TRY AGAIN.
IFN TS,[MOVE A,GAMNT
	LSH A,-10.
	HRRZ B,BCORE
	ADDI A,1(B)
	PUSHJ P,ACORE
	SKIPG A
]
	JRST FALSE

GRELAR:	HRRZ A,BPSH
	MOVEM A,GSBPN'	;TEMPORARY BPEND
	CLEARM NIL	;MOBY DELAYED QUIT FEATURE.
	PUSHJ P,GSGEN	;RELOCATE ARRAYS
		GTSPC3
IFN VD,[SKIPE DSTPF
	PUSHJ P,DSTRT]
GREL1:	MOVE A,GSBPN
	PUSHJ P,FIX1A
	MOVEM A,VBPEND
GREL3:	MOVE A,GSBPN
	HRROI NIL,CNIL2
	JRST GCSP2	;CHECK FOR ^G THEN POPJ P,


GFSPC:	MOVEI C,0	;ZERO OUT SPACE COUNT.
	PUSHJ P,GSGEN	;COUNT SPACE OCCUPIED BY
		GTSPC2	;ARRAYS, IN C.
	MOVE A,VBPEND
	PUSHJ P,NUMVAL
	HRRZ B,BPSH
	SUBI B,(C)
	SUB B,A
	CAMGE C,GAMNT
GSX1:	AOS (P)	;SKIP IF NOT ENOUGH SPACE.
RTSP3:	POPJ P,

GSGEN:	MOVEI AR2A,VGCMKL	;GENERATE TAILS OF
GGEN2:	HRRZ AR2A,(AR2A)
	HRRZ AR2A,(AR2A)
GGEN1:	JUMPE AR2A,GSX1		;GCMKL IN AR2A, SPACE
	HLRZ A,(AR2A)
	HLRZ A,(A)
	JUMPE A,GGEN2
	HRRZ A,(AR2A)	;OCCUPIED BY NEXT ARRAY
	HLRZ A,(A)	;IN A.
	PUSH P,@(P)	;NUMVAL RETURNS TO SUBROUTINE,
	JRST NUMVAL	;WHICH RETURNS TO GGEN2.

GTSPC2:	ADD C,A
	JRST GGEN2


GTSPC3:	MOVEI AR1,-1(A)		;LNGTH-1 OF ARRAY IN AR1.
	HLRZ A,(AR2A)
IFN VD,[SKIPE DISON
	SKIPN B,VDISLIST
	JRST GT3F
	PUSHJ P,MEMQ
	JUMPE A,GT3E
	PUSHJ P,DSTOP
GT3E:	HLRZ A,(AR2A)
GT3F:
]
	HRRZ A,(A)
	SOS A		;ARRAY AOBJN PTR LOC IN A.
	MOVE C,GSBPN
	SUB C,AR1
	MOVEM C,GSBPN	;LOC NEW BPTR IN C.
	MOVEI B,(C)
	SUBI B,1(A)	;RELOCATION AMNT-1 IN B.
	CAML A,C	;IS ARRAY ALREADY IN PLACE?
	JRST GT3C	;YES, SO XIT.
	SUBI C,(AR1)
	CAIGE A,(C)
	JRST GT3A	;GOOD, EASY BLT.
	ADDI C,(AR1)
	ADDI AR1,1(A)	;FIRST DESTINATION LOC.
GT3B:	HRRZI C,(AR1)
	SUBI AR1,1(B)	;CONSTRUCT SOURCE ADDRESS.
	HRLI C,(AR1)
	HRRM C,.+1
	BLT C,(B)
	CAMLE AR1,GSBPN
	JRST GT3B
	ADDI AR1,(B)
	SUB AR1,GSBPN
	MOVE A,GSBPN
	SUBI A,1(B)
GT3A:	MOVE C,GSBPN
	HRL C,A
	HRRM C,.+1
	BLT C,(AR1)
	JSP AR1,GT3D
GT3C:	SOS GSBPN
	JRST GGEN2

GT3D:	AOS B
	HLRZ A,(AR2A)
	ADDM B,(A)	;CHANGE SAR CELL.
	HRRZ C,(A)
	ADDM B,-1(C)	;CHANGE AOBJN PTR.
	LDB A,[3504_30 (A)]
	ADDI A,-3(C)
	ADDM B,2(A)	;CHANGE CHECK CELL.
	LSH B,1
	ADDM B,(A)	;CHANGE ADDI INSTRUCTION.
	JRST (AR1)


IFN TS,[
RETSP":	AOSN RTSPFL
	JRST GTSP2
	CLEARB C,GAMNT	;COMPACTIFY ARRAYS AND SHIFT 
	SKIPE GCMKL	;DOWNWARDS AS FAR AS POSSIBLE.
	PUSHJ P,GFSPC	;CHANGE CORE SETTING
	MOVE A,VBPORG	;PARSIMONOUSLY, (IF CHANGE IS
	PUSHJ P,NUMVAL	;GREATER THAN TWO.)
	ADDI A,1(C)
	LSH A,-10.
	ADDI A,2
	HRLM A,GREL2	;NEW CORE SETTING.
	HRRZ B,BCORE
	SUBI B,2(A)
	JUMPL B,GTSP2
	LSH A,10.
	SUBI A,2001(C)
	HRRM A,RTSP3	;NEW BPEND.
	JUMPE C,RTSP5
	HRRM C,RTSP4	;NO. OF CELLS TO MOVE.
	PUSHJ P,GRELAR	;(LEAVES BPEND-AFTER-RELOCATION IN A.)
	HRL AR1,A
	HRR AR1,RTSP3	;BLOCK PTR.
	SUBI A,(AR1)
	JUMPLE A,RTSP2
	AOS A
	HRRM A,RTSP1	;LGNTH+1 OF RELOCATION.
	HRRM AR1,RTSP1
	ADD AR1,[1,,1]
	EXCH NIL,RTNIL	;MOBY DELAYED QUITE FEATURE.
IFN VD,PUSHJ P,DSTOP
RTSP4:	MOVEI C,.
RTSP1:	BLT AR1,.(C)
	PUSHJ P,GSGEN
		RTSPC1
IFN VD,[SKIPE DSTPF
	PUSHJ P,DSTRT]
RTSP2:	HRRZ A,GREL2
	PUSHJ P,ACORE
	JUMPE A,.+5
	HRRZ A,@VBPEND	;FIX-UP BPEND.
	HRRZ A,(A)
	HRRZ A,RTSP3
	MOVEM B,(A)
	EXCH NIL,RTNIL
	JUMPL NIL,GCSP2
	POPJ P,
RTSP5:	PUSHJ P,FIX1A
	MOVEM A,VBPEND
	JRST RTSP2
RTNIL:	0
RTSPFL:	-1	;TO KILL RET ON VERY FIRST GC

RTSPC1:	MOVNI B,0	;-(SIZE OF SHIFT + 1).
	MOVEI AR1,GGEN2
	JRST GT3D
]


MACDMP":
IFE TS,[HRROI R,MACW1-1
	JUMPE T,MACD1
	POP P,A
	MOVE B,[440700,,MACSA]
	MOVEM B,MACCR
	MOVEM B,MACW2
	PUSHJ P,[AOJA R,PRINTA]
	MOVEI A,0
	IDPB A,MACW2
MACD1:
]
	PUSHJ P,AGC
	MOVE A,FF
	MOVE B,F
	PUSHJ P,.NCONC
	PUSHJ P,FW0CNS
	JUMPN FF,.-1
IFN TS,.VALUE
IFE TS,[PUSHJ P,UWAIT"
	CONSZ TTY,30
	JRST .-2
IFN PL,[
	CONSZ PLT,7
	JRST .-1
]
	MOVEI A,NDIR"-1
	CLEARM .DIRTAB"(A)
	SOJGE A,.-1
	JRST BSUP+37401-MACW1"(R)

MACW2:	0

MACW1:	IDPB A,MACW2
	POPJ P,
]

IFN LAPFL,[
.AMAKE":	PUSH P,A	;MAKE ALIST FOR FSUBR THAT REQUIRES IT
	MOVE A,SP
	PUSHJ P,FIX1A
	MOVE B,A
	JRST POPAJ

.UDT":	PUSHJ P,PRINT	;ERROR PRINT FOR UNDEFINED COMPUTED GO TAG
	STRT [SIXBIT /UNDEFINED COMPUTED GO TAG IN !/]
	HRRZ R,(P)
	PUSHJ P,ERRADR
	JRST BACTRC

.LCALL":	MOVN A,T	;SET UP ROUTINE FOR COMPILE LSUBR
	PUSHJ P,FIX1A
	ADDI T,+1-1-1(P)
	PUSH P,T
	PUSHJ P,(3)
	POP P,T
	SUBI T,-1(P)
	HRLI T,-1(T)
	ADD P,T
	POPJ P,

LAPSETUP":	JUMPN A,LSORG
	SKIPA AR1,[LLSUPT]
LSUP1:	HRRZ B,(B)	;CLOBBER-IN LOCATIONS OF SOME ATOMS.
	HLRZ A,(B)
	HRRM A,@LSUPT-1(C)
	SOJN C,LSUP1
	HRLI A,(CALL 1,)
	HLLM A,KRMLEV
	POPJ P,
LSUPT:
IRPS A,,[KRMORG,KRMNCR,KRMLOC,KRMLEV,KRMAS,
	KRMSC,KRM6BT,KRMBLK,KRMASC]
	A
TERMIN
LLSUPT==.-LSUPT

LSORG:	MOVEI A,10
	PUSHJ P,GTSPC	;INSURE SOME CORE,
	SKIPN A
	LERR [SIXBIT /NOT ENOUGH CORE - LAP!/]
	MOVEM A,VRPATCH

KRMORG:	MOVE A,.	;(SPECIAL ORGIN)
	PUSHJ P,NUMVAL 
	SUBI A,1
	MOVEM A,VGWD	;ADDRESS TO BEGIN LAPPING IN.
	POPJ P,


GWD:	HLRZ C,(A)
KRMASC:	CAIN C,.	;(QUOTE ASCII)
	JRST GWDASC
KRMBLK:	CAIN C,.	;(QUOTE BLOCK)
	JRST GWDBLK
KRM6BT:	CAIN C,.	;(QUOTE SIXBIT)
	JRST GWD6BT
	PUSH P,A
	SETZM GWDSUM'
	MOVE C,(P)
	MOVEI B,5
GWDA:	MOVEM B,VLAPSETUP	;USED AS COUNTER FOR FIELDS 
	JUMPE C,GWDXIT		;(4 3 2 1)
	HLRZ A,(C)
KRMSC:	CAIN A,.	;(QUOTE ;)
	JRST GWDXIT
	MOVEM C,(P)
KRMAS:	CAIN A,.	;(QUOTE @)
	JRST GWDAS
	SOSG VLAPSETUP
	JRST GWDXIT
KRMLEV:	CALL 1,.	;(CALL LAPEVAL)
	PUSHJ P,NUMVAL
	MOVE B,VLAPSETUP
	XCT GWDEX-1(B)
	ADDM A,GWDSUM
GWDB:	HRRZ C,@(P)
	JRST GWDA
GWDBK1:	ADD P,[1,,1]
GWDXIT:	MOVE A,GWDSUM
	MOVE C,VGWD	;NUMERIC ORGIN IN VGWD
KRMLOC:	ADD C,.		;(SPECIAL LOC)
	CAMLE C,VRPATCH	;NUMERIC BPEND IN VRPATCH
	JRST GWDXT1
	MOVEM A,(C)
	MOVEI A,(C)
	AOS @KRMLOC
	JRST POP1J

GWDXT1:	MOVE A,@KRMLOC
	ADDI A,3
KRMNCR:	CALLF 1,.	;(QUOTE CORE/|)
	PUSHJ P,NUMVAL
	MOVEM A,VRPATCH
	JRST GWDXIT

GWDEX:	MOVSS A
	HRRZS A
	LSH A,23.
	JFCL


GWDAS:	HRLZI A,20
	ADDM A,GWDSUM
	JRST GWDB

GWDASC:	SKIPA AR1,GWDBP7
GWD6BT:	MOVE AR1,GWDBP6
	HRR AR1,VGWD
	ADD AR1,@KRMLOC
	PUSHJ P,CADR
	PUSH P,A
	PUSHJ P,FLATC
	SUBI A,2
	MOVEI C,5
	TLNN AR1,100
	MOVEI C,6
	IDIV A,C
	ADDI A,1
	PUSHJ P,GWDBLK+2
	HRROI R,GWDE1
	TLNN AR1,100
	HRROI R,GWDE3
	POP P,A
	JRST PRINTA
GWDE3:	ADDI A,40
GWDE1:	IDPB A,AR1
	POPJ P,
GWDBP7:	440700,,
GWDBP6:	440600,,

GWDBLK:	PUSHJ P,CADR
	PUSHJ P,NUMVAL
	MOVEM A,VLAPSETUP
	SETZM GWDSUM
	SOSGE VLAPSETUP
	POPJ P,
	PUSHJ P,GWDBK1
	JRST .-3

RPATCH:	JUMPN C,PTCH	;USES A,B,C,AR1,AR2A
	ADD A,VGWD
	ADD B,VGWD
	HRRM A,(B)
	POPJ P,
PTCH:	MOVE AR1,B
	PUSHJ P,NUMVAL
	MOVE AR2A,A
PTCH2:	JUMPE AR1,CPOPJ
	MOVE A,AR2A
	HLRZ B,(AR1)
	HRRZ C,(B)
PTCH1:	HLRZ B,(B)
	XCT GWDEX-1(C)
	ADD B,VGWD
	ADD A,(B)
	CAIN C,3
	LSH A,-23.
	DPB A,PTCHTB-1(C)	;THIS IS THE CLOBBER
	HRRZ AR1,(AR1)
	JRST PTCH2
PTCHTB:	(004400+B)
	(002200+B)
	(270400+B)
	(004400+B)

]

PSYM:	AOS PSYMF
	MOVEM A,PSMAS	;P.$X, DONE IN DDT,
	MOVEM B,PSMBS	;WILL PRINT CONTENTS
	MOVEI A,LPSMTB	;OF CURRENT OPEN CELL
	MOVE B,@PSMTB-1(A)	;IN LISP FORMAT.
	MOVEM B,PSMS-1(A)
	SOJN A,.-2
	PUSH P,[PSYMX]
	JSP C,ERSTP
	.VALUE [ASCIZ /.=1!Q
:VP /]
	MOVE C,PSMS
	MOVE B,PSMBS
	MOVEM A,PS.S
	CAIN A,1
	SKIPA A,PSMAS
	MOVE A,(A)
	PUSHJ P,PRINT
	JSP C,ERR1
	SUB P,[3,,3]
PSYMX:	MOVE A,PS.S
	.VALUE [ASCIZ \1/Q!\]
	MOVEI A,LPSMTB
	MOVE B,PSMS-1(A)
	MOVEM B,@PSMTB-1(A)
	SOJN A,.-2
	MOVE A,PSMAS
	MOVE B,PSMBS
	SETZM PSYMF
	POPJ P,

ZZ==.
PSMTB:	IRP A,,[C,D,T,TT,R,TYOI,FPTEM,PCNT]
	A
	TERMIN
LPSMTB==.-ZZ
PSMS:	BLOCK LPSMTB
PSMAS:	0
PSMBS:	0
PS.S:	0

PSYMF:	0	;NON-ZERO DURING EXECUTION OF PSYM.
P."=PUSHJ P,PSYM	;P.$X IN DDT WILL PRINT OUT IN LIST FORMAT.

VANDC:	0
CONSTANTS
VARIABLES
PATCH:	BLOCK 100

INUM":	INUM

GCMKL":	0
BPPNR:	0

INFORM [LENGTH OF THIS BAGBITER]\.

;DEFINED ATOMS
FS:
DEFINE MAKBUC A\B
DEFINE OT!A
B=.
TERMIN
(B)IFN OBTSIZ-1-A,.+1
IF1 B=0
TERMIN

OBTBL:
OBLIST:	REPEAT OBTSIZ,MAKBUC \.RPCNT

DEFINE ADDOB A,C\B
OT!A
DEFINE OT!A
B=.
TERMIN
IF1 B=0
(C)B
TERMIN

DEFINE PUTOB A,B
ZZ=<(377777),-1>&ASCII /A/
ZZ=-ZZ/OBTSIZ*OBTSIZ+ZZ
ADDOB \ZZ,B
TERMIN

DEFINE PSTRCT A
ZZ=[ASCII /A/]
ZY=.LENGTH /A/
(ZZ)!REPEAT <ZY-1>/5,[.+1
(ZZ+.RPCNT+1)]
TERMIN

DEFINE MKAT A,B,C,D
PUTOB A,.+1
D(,-1).+1
(B).+1
(,C!A).+1
(PNAME).+1
(.+1)
PSTRCT A
TERMIN

DEFINE MKAT1 A,B,C,D,E
	PUTOB E!C,.+1
	-1,,.+1
	B,,.+1
	D!A,,.+1
	PNAME,,.+1
	.+1,,
	PSTRCT E!C
TERMIN

IRPS A,,[RPLACA,RPLACD,MINUS,TERPRI,CAR,CDR,CAAR,
CADR,CDAR,CDDR,CAAAR,CAADR,CADAR,CADDR,CDAAR,CDADR,CDDAR,CDDDR,
CAAAAR,CAAADR,CAADAR,CAADDR,CADAAR,CADADR,CADDAR,CADDDR,CDAAAR,
CDAADR,CDADAR,CDADDR,CDDAAR,CDDADR,CDDDAR,CDDDDR,MAKNUM,CONS,
ATOM,EQ,PRIN1,PRINT,RETURN,EXPLODE,SASSOC,ASSOC,
NUMBERP,EQUAL,SUBST,GET,INTERN,MEMBER,PATOM,
MAKNAM,NOT,GENSYM,ZEROP,FIXP,FLOATP,
FIX,SET,LENGTH,READLIST,LAST,ADD1,SUB1,
REVERSE,GC,GETL,MEMQ,ERR,
PUTPROP,PRINC,FLATSIZE,EXAMINE,DEPOSIT,LSH,
IOG,NCONS,XCONS,REMPROP,ARG,MINUSP,REMAINDER,ABS,
TIME,FLATC,SETARG,GETSP]
MKAT A,SUBR
TERMIN

IRPS A,,[COND,PROG,SETQ,UREAD,UKILL,REMAR,
	ERRSET,REMOB,OR,GO,STORE,CSTORE,
	NSTORE,UWRITE,IOC,UFILE,
	DO,MACDMP]
	MKAT A,FSUBR
TERMIN

IRPS A,,[LIST PLUS TIMES DIFFERENCE QUOTIENT APPEND NCONC
	BOOLE APPLY GREATERP LESSP MAP MAPLIST MAPCAR
	MAX MIN PROG2 DELETE]
	MKAT A,LSUBR
TERMIN

IRP A,,[PLUS,DIF,QUO,TIMES,APPEND,GREAT,LESS]B,,[*PLUS
*DIF,*QUO,*TIMES,*APPEND,*GREAT,*LESS]
MKAT1 A,SUBR,B,.
TERMIN

IRP A,,[EVAL,READ,READCH]
	MKAT A,LSUBR,O
TERMIN
IRP A,,[ARRAY,QUOTE,MAPC,DELQ]B,,[F,F,L,L]
	MKAT A,B!SUBR,,C!A!":
TERMIN
MKAT EXPLODEC,SUBR,%
MKAT TYO,SUBR,I
MKAT1 FLOAT,SUBR,I
MKAT1 SMLNM,SUBR,SMALLNUMP
MKAT1 QUOTE,FSUBR,FUNCTION
MKAT1 FUNCT,FSUBR,*FUNCTION
CEVAL=.+1
MKAT1 EVAL,SUBR,*EVAL

IFN PL,[
	IRPS A,,[PLOT,CLENGTH,IPL,PLOTLIST,NEXTPLOT]
	MKAT A,SUBR
	TERMIN
]

IFN TS,[
	IFN MPXRF,[
		IRPS A,,[NVID,NVSET,NVFIX]
		MKAT A,SUBR
		TERMIN
	]
	MKAT SLEEP,SUBR
	MKAT RUNTIME,SUBR
	MKAT LISTEN,SUBR,A
]

IFN PKPKF,[
	IRP A,,[VREAD,VWRITE,VARRAY]B,,[READPIC,WRITEPIC,PICARRAY]
	MKAT1 A,FSUBR,B
	TERMIN
	MKAT DESCR,LSUBR
	MKAT DESCRX,LSUBR
]

IFN MPXRF,[
	IRPS A,,[VIDI,VIDLIN,VIDLOG,RD760,WR760,IMPX,OMPX,MPX]
	MKAT A,SUBR
	TERMIN
]

IFN VD,[MKAT LPEN,SUBR
	MKAT DISAD,SUBR
	MKAT DISINI,SUBR
]

IFE TS,[
	IRPS A,,[SETTIME]
	MKAT A,SUBR
	TERMIN
]


DEFINE MKFV A,B,C,D,E,F
	PUTOB [A!F].+1
	-1,,.+1
	(C).+1
IFSE B,,(A).+1
IFSN B,,(B).+1
	(VALUE).+1
IFSE E,,(V!A).+1
IFSN E,,(V!B).+1
	(PNAME).+1
	(.+1)
	PSTRCT [A!F]
IFSE E,,V!A":	0!D
IFSN E,,V!B":	0!D
TERMIN

IRP A,,[NOUUO,NOCHK,BAKGAG]
	MKFV A,,SUBR
TERMIN

MKFV *NOPOINT,.NOPOINT,SUBR,,T
MKFV *RSET,.RSET,SUBR,,T
MKFV GCTWA,,LSUBR
IFN TS,[
	MKFV CORE,ACORE,SUBR,+21,,
	MKFV NORET,,SUBR
]
IFN LAPFL,[
	IRP A,,[GWD,RPATCH,LAPSETUP]
	MKFV A,,SUBR,,,[|]
	TERMIN
]

MKAT NULL,SUBR,A
MKAT AND,FSUBR,A

	;ATOMS WITH VALUES

	PUTOB T,.+1
TRUTH:	(,-1).+1
	(VALUE).+1
	(VTRUTH).+1
	(PNAME).+1
	(.+1)
	PSTRCT T
VTRUTH:	TRUTH

	PUTOB NIL,0
CNIL2:	(VALUE).+1
	(VNIL).+1
	(PNAME).+1
	(.+1)
	PSTRCT NIL
VNIL":	NIL

IRPS A,,[OBLIST,TTY,BASE,IBASE,LINEL,CHRCT,BPEND,BPORG,
		GCMKL,ERRLIST]
	MKAT A,VALUE,V
TERMIN
VERRLI:	NIL
VTTY:	NIL
VLINEL:	72.+1
VCHRCT:	71.+1
VOBLIST:	OBLIST
VBASE:	8+1
VIBASE:	8+1
VGCMKL:	GCMKL
VBPORG: .+1
	-1,,.+1
	FIXNUM,,VBP1
VBPEND:	.+1
	-1,,.+1
	FIXNUM,,VBPE1

IFN VD,[
	IRPS A,,[DISLIST,DISCNT,DISLP]
		MKAT A,VALUE,V
	TERMIN
		VDISLIST:	NIL
		VDISCNT:	71.+1
		VDISLP: 0+1
	MKAT1 DISON,VALUE,^F
		DISON":	NIL
	IFN TS,[
		MKAT1 DISPON,VALUE,^N
		DISPON":	NIL	
	]
]

IRP A,,[^Q,^W,^R,^D,^B,^P,^A,^H]B,,[TAPRED,TTYOFF,TAPWRT
GCGAGV,LPTON,PLTON,SIGNAL,RDMOD]
	MKAT1 B,VALUE,A
	B":	NIL
TERMIN

]


IRPS A,,[PNAME,FIXNUM,FLONUM,VALUE,LAMBDA,SUBR,FSUBR,EXPR,FEXPR,SYM,
	LABEL,FUNARG,LSUBR,MACRO]
	PUTOB A,.+1
A":	(,-1).+1
	(PNAME).+1
	(.+1)
	PSTRCT A
TERMIN

IFN LAPFL,[

IRPS A,,[SPECBIND,SPECSTR,NUMVAL,FIX1A,FIX2,FLOAT,PRINTA]
	MKAT1 A,SYM,A,1+
TERMIN

IRPS A,,[AMAKE,UDT,LCALL]
	MKAT1 A,SYM,A,1+.,*
TERMIN

VINUM:	-1,,.+1
	FIXNUM,,[INUM]
	MKAT INUM,SYM,V

]
		;SOME TEMP CELLS THAT
MKNM3:	0	;DONT WANT TO BE GARBAGED
BIND3:	0	;RH=CURRET I/O DEV
CRUNIT:	0	;LH=THAT FOR LAST UWRITE.


UNBOUND":	-1,,.+1
	(PNAME).+1
	(.+1)
	PSTRCT UNBOUND

	PUTOB [?].+1
QM:	-1,,.+1
	(PNAME).+1
	(.+1)
	PSTRCT [?]

BFWS:
CONSTANTS
VBP1:	0
VBPE1:	0
EFWS:	0

INFORM [INITIAL LIST STRUCTURE]\<.-FS>

;LISP STORAGE ALLOCATOR

ALLTYO:	HRLOI A,700+A
	HLLM A,(P)
	ILDB C,(P)
	JUMPE C,ALLPOP
	PUSHJ P,ALLTYC
	JRST .-3

IFE TS,[
ALLTYI:	CONSO TTY,40
	JRST .-1
	DATAI TTY,C
	ANDI C,177
ALLTYC:	CONSZ TTY,20
	JRST .-1
	DATAO TTY,C
]
IFN TS,[
ATYI:	10,,(SIXBIT /TTY/)
	SIXBIT /.ALLOC/
	SIXBIT /INPUT/
ATYO:	(11+SIXBIT /TTY/)
	SIXBIT /.ALLOC/
	SIXBIT /OUTPUT/
ALLTYI:	.IOT TYIC,C
	POPJ P,
ALLTYC:	CAIE C,12
	.IOT TYOC,C
]
ALLPOP:	POPJ P,

ALLNUM:	MOVSI A,400000
	PUSHJ P,ALLTYI
	CAIN C,177
	JRST ALLRUB
	CAIL C,60
	CAIL C,72
	POPJ P,
	TLZ A,400000
	IMULI A,10
	ADDI A,-60(C)
	JRST ALLNUM+1

ALLPDL:	BLOCK 10

ALLRUB:	PUSHJ P,ALLTYO
	ASCII /X/
	JRST ALLNUM

ALLOC":	IFE TS, CONO 200000
IFN TS,[.OPEN TYOC,ATYO
	.VALUE
	.OPEN TYIC,ATYI
	.VALUE
]
	MOVEI P,ALLPDL-1
	PUSHJ P,ALLTYO
	ASCIZ /LISP /
	MOVE AR1,[.FNAM2]
ALLOCB:	MOVEI C,0
	LSHC C,6
	JUMPE C,ALLOCA
	ADDI C,40
	PUSHJ P,ALLTYO
	JRST ALLOCB
ALLOCA:	PUSHJ P,ALLTYO
	ASCII /
ALLOC? /
	PUSHJ P,ALLTYI
	CAIGE C,"O
	JRST ALLC00
	PUSHJ P,ALLTYO
IFE TS,[	ASCII /
MEMTOP=/
	PUSHJ P,ALLNUM
	SKIPG A
	MOVEI A,37170+IFE TS,BSUP
	HRRM A,ALLC01]
IFN TS,[ASCII /
CORE= /
	PUSHJ P,ALLNUM
	CAIGE A,20
	MOVEI A,20
	HRLI A,(.CORE)
	MOVEM A,ALCOR
	MOVEI B,1(A)
	HRRM B,BCORE
	HRRZM B,VCORE
	LSH A,10.
	SOS A
	HRRM A,ALLC01
]	MOVE B,[JRST TYIN+3]
IFE TS,[	CAILE A,37777
	MOVEM B,TYIN
]	PUSHJ P,ALLTYO
	ASCII /
FULL WDS=/
	PUSHJ P,ALLNUM
	SKIPGE A
	MOVEI A,400
	HRRM A,ALLC02
	PUSHJ P,ALLTYO
	ASCII /
BIN.PROG.SP=/
	PUSHJ P,ALLNUM
	SKIPGE A
	MOVEI A,1000
	HRRM A,ALLC10
	PUSHJ P,ALLTYO
	ASCII /
SPEC.PDL=/
	PUSHJ P,ALLNUM
	SKIPGE A
	MOVEI A,1000
	HRRM A,ALLC20
	MOVNS A
	HRRM A,ALLC21
	PUSHJ P,ALLTYO
	ASCII /
REG. PDL=/
	PUSHJ P,ALLNUM
	SKIPGE A
	MOVEI A,1000
	HRRM A,ALLC30

ALLC00:	IFE TS,[	MOVEI A,LISPGO
	HRRM A,100
]
IFN TS,[	MOVSI A,(JFCL)
	MOVEM A,LISPGO
]
	PUSHJ P,ALLTYO
	ASCII /
/
ALLC01:	IFE TS,MOVEI A,37170+BSUP
	IFN TS,MOVEI A,37777
IFN TS,[	ALCOR:	.CORE 20
	JRST ALLOC
]
	HRRM A,BPSH
	MOVEM A,VBPE1
ALLC10:	SUBI A,1000	;ADDR MAY BE CHANGED BY ALLOC
	HRRM A,BPSL
IFN TS,[
	LDB B,[121000,,A]
	ADDI B,2
	HRRM B,ACORE]
	MOVEM A,VBP1
	SOS A
ALLC20:	SUBI A,1000
ALLC21:	HRLI A,-1000
	MOVEM A,SC2
	SUBI A,FS
	HRRZS B,A
	ASH A,-4
ALLC02:	ADDI A,400
	MOVE C,B
	ASH C,-6
ALLC30:	ADDI C,1000
;STG ORDER PRGM FS FWS  BT BTF PDLAC PDL SP BPS
	MOVEI T,44
	IDIVM A,T
	AOS ,T		;SIZE OF BTF
	SUB B,T
	SUB B,A
	SUB B,C		;REMAINING STORAGE
	MOVEI TT,32.+1
	IDIVM B,TT	;BT SIZE -1
	SUBI B,1(TT)	;FREE STORAGE SIZE
	HRRZ AR1,B
	ADDI B,FS
	HRRM B,GCP1
	HRRM B,NUMV3
	MOVN SP,B
	HRRM SP,GCMFWS
	HRLZM A,C1GCS	;LENGTH OF FWS
	MOVNS ,C1GCS
	HRRM B,C1GCS
	ADDI B,-1(A)
ALLOC1:	AOS ,B
	MOVEI SP,FS
	LSH SP,-5
	SUBM B,SP
	HRRM SP,GCBTP2
	HRRM SP,GCBTP1
	HRRM SP,GC8T1
	HRLM B,C3GC
	HRRM B,GCP2
	HRRM B,GCP
	MOVNI SP,-1(TT)
	HRLM SP,C3GCS
	HRRM B,C3GCS
	AOS ,B
ALLC2A:	MOVEI SP,FS
	ANDI SP,37
	HRRM SP,GCBTL2
	SUBI SP,32.
	HRRM SP,GCBTL1
	HRRM B,C3GC
	ADDI B,-1(TT)	;BOTTOM OF BTF-1
	HRRM B,C2GCS
	AOS ,B
	HRRM B,C2GC
	ADDI B,-1(T)	
	HRRM B,GCP5	;BTF TOP
	AOS,B		;PDLAC
	HRRM B,GCP3
	MOVEM B,A
	HRLI A,INDK
	BLT A,LINDK-1(B)
	ADDI B,LINDK
	HRRM B,GCSP1	;PDL-1
	HRRM B,GCP4
	ADDI B,10	;NUMBER ACS PROTECTED
	HRRM B,GCP4+1
	AOS ,B
	HRRM B,C2
	MOVNI A,-LINDK-12(C)
	HRLM A,C2
;RELOCATOR

RLOCA:	MOVE B,AR1
	HRLI AR1,BFWS
	HRRI AR1,FS(B)
	HRRZI AR2A,EFWS-BFWS(AR1)
	BLT AR1,(AR2A)
	MOVEI AR1,FS-BFWS(B)
	MOVEI AR2A,FS

REL1:	HLRZ A,(AR2A)
	CAIG A,EFWS
	CAIGE A,BFWS
	JRST REL2
	ADD A,AR1
	HRLM A,(AR2A)
REL2:	HRRZ A,(AR2A)
	CAIG A,EFWS
	CAIGE A,BFWS
	JRST REL3
	ADD A,AR1
	HRRM A,(AR2A)
REL3:	CAIGE AR2A,BFWS-1
	AOJA AR2A,REL1
IFE TS,[
	CONSZ TTY,20
	JRST .-1
]
	JRST LISPGO

INDK:
IRPS A,,[FIXNUM,FLONUM,SYM,VALUE,PNAME,
	EXPR,FEXPR,SUBR,FSUBR,LSUBR,CNIL2,
	LABEL,LAMBDA,FUNARG,MACRO,UNBOUND,
	CRUNIT,MKNM3,BIND3,QM]
	A
TERMIN
LINDK==.-INDK

END LISPGO

